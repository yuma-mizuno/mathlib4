/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Andrew Yang
-/
module

public import Mathlib.Algebra.Category.Ring.Under.Limits
public import Mathlib.AlgebraicGeometry.Morphisms.Affine
public import Mathlib.AlgebraicGeometry.PullbackCarrier
public import Mathlib.RingTheory.RingHom.FaithfullyFlat

/-!
# Flat morphisms

A morphism of schemes `f : X ‚ü∂ Y` is flat if for each affine `U ‚äÜ Y` and
`V ‚äÜ f ‚Åª¬π' U`, the induced map `Œì(Y, U) ‚ü∂ Œì(X, V)` is flat. This is equivalent to
asking that all stalk maps are flat (see `AlgebraicGeometry.Flat.iff_flat_stalkMap`).

We show that this property is local, and are stable under compositions and base change.

-/

@[expose] public section

noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe v u

namespace AlgebraicGeometry

variable {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y)

/-- A morphism of schemes `f : X ‚ü∂ Y` is flat if for each affine `U ‚äÜ Y` and
`V ‚äÜ f ‚Åª¬π' U`, the induced map `Œì(Y, U) ‚ü∂ Œì(X, V)` is flat. This is equivalent to
asking that all stalk maps are flat (see `AlgebraicGeometry.Flat.iff_flat_stalkMap`).
-/
@[mk_iff]
class Flat (f : X ‚ü∂ Y) : Prop where
  flat_appLE (f) :
    ‚àÄ {U : Y.Opens} (_ : IsAffineOpen U) {V : X.Opens} (_ : IsAffineOpen V) (e : V ‚â§ f ‚Åª¬π·µÅ U),
      (f.appLE U V e).hom.Flat

alias Scheme.Hom.flat_appLE := Flat.flat_appLE

@[deprecated (since := "2026-01-20")] alias Flat.flat_of_affine_subset := Scheme.Hom.flat_appLE

namespace Flat

instance : HasRingHomProperty @Flat RingHom.Flat where
  isLocal_ringHomProperty := RingHom.Flat.propertyIsLocal
  eq_affineLocally' := by
    ext X Y f
    rw [flat_iff, affineLocally_iff_forall_isAffineOpen]

instance (priority := 900) [IsOpenImmersion f] : Flat f :=
  HasRingHomProperty.of_isOpenImmersion
    RingHom.Flat.containsIdentities

instance : MorphismProperty.IsStableUnderComposition @Flat :=
  HasRingHomProperty.stableUnderComposition RingHom.Flat.stableUnderComposition

instance comp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [hf : Flat f] [hg : Flat g] : Flat (f ‚â´ g) :=
  MorphismProperty.comp_mem _ f g hf hg

instance : MorphismProperty.IsMultiplicative @Flat where
  id_mem _ := inferInstance

instance isStableUnderBaseChange : MorphismProperty.IsStableUnderBaseChange @Flat :=
  HasRingHomProperty.isStableUnderBaseChange RingHom.Flat.isStableUnderBaseChange

instance (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [Flat g] : Flat (pullback.fst f g) :=
  MorphismProperty.pullback_fst _ _ inferInstance

instance (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [Flat f] : Flat (pullback.snd f g) :=
  MorphismProperty.pullback_snd _ _ inferInstance

instance (f : X ‚ü∂ Y) (V : Y.Opens) [Flat f] : Flat (f ‚à£_ V) :=
  IsZariskiLocalAtTarget.restrict ‚Äπ_‚Ä∫ V

instance (f : X ‚ü∂ Y) (U : X.Opens) (V : Y.Opens) (e) [Flat f] : Flat (f.resLE V U e) := by
  delta Scheme.Hom.resLE; infer_instance

lemma of_stalkMap (H : ‚àÄ x, (f.stalkMap x).hom.Flat) : Flat f :=
  HasRingHomProperty.of_stalkMap RingHom.Flat.ofLocalizationPrime H

lemma stalkMap [Flat f] (x : X) : (f.stalkMap x).hom.Flat :=
  HasRingHomProperty.stalkMap (P := @Flat)
    (fun f hf J hJ ‚Ü¶ hf.localRingHom J (J.comap f) rfl) ‚Äπ_‚Ä∫ x

lemma iff_flat_stalkMap : Flat f ‚Üî ‚àÄ x, (f.stalkMap x).hom.Flat :=
  ‚ü®fun _ ‚Ü¶ stalkMap f, fun H ‚Ü¶ of_stalkMap f H‚ü©

instance {X : Scheme.{u}} {Œπ : Type v} [Small.{u} Œπ] {Y : Œπ ‚Üí Scheme.{u}} {f : ‚àÄ i, Y i ‚ü∂ X}
    [‚àÄ i, Flat (f i)] : Flat (Sigma.desc f) :=
  IsZariskiLocalAtSource.sigmaDesc (fun _ ‚Ü¶ inferInstance)

/-- A surjective, quasi-compact, flat morphism is a quotient map. -/
@[stacks 02JY]
lemma isQuotientMap_of_surjective {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [Flat f] [QuasiCompact f]
    [Surjective f] : Topology.IsQuotientMap f := by
  rw [Topology.isQuotientMap_iff]
  refine ‚ü®f.surjective, fun s ‚Ü¶ ‚ü®fun hs ‚Ü¶ hs.preimage f.continuous, fun hs ‚Ü¶ ?_‚ü©‚ü©
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ let ùí∞ := Y.affineCover
    rw [ùí∞.isOpenCover_opensRange.isOpen_iff_inter]
    intro i
    rw [Scheme.Hom.coe_opensRange, ‚Üê Set.image_preimage_eq_inter_range]
    apply (ùí∞.f i).isOpenEmbedding.isOpenMap
    refine this (f := pullback.fst (ùí∞.f i) f) _ ?_ ‚ü®_, rfl‚ü©
    rw [‚Üê Set.preimage_comp, ‚Üê TopCat.coe_comp, ‚Üê Scheme.Hom.comp_base, pullback.condition,
      Scheme.Hom.comp_base, TopCat.coe_comp, Set.preimage_comp]
    exact hs.preimage (Scheme.Hom.continuous _)
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S
  ¬∑ have _ : CompactSpace X := QuasiCompact.compactSpace_of_compactSpace f
    let ùí∞ := X.affineCover.finiteSubcover
    let p : ‚àê (fun i : ùí∞.I‚ÇÄ ‚Ü¶ ùí∞.X i) ‚ü∂ X := Sigma.desc (fun i ‚Ü¶ ùí∞.f i)
    refine this (f := (‚àê (fun i : ùí∞.I‚ÇÄ ‚Ü¶ ùí∞.X i)).isoSpec.inv ‚â´ p ‚â´ f) _ _ ?_ ‚ü®_, rfl‚ü©
    rw [‚Üê Category.assoc, Scheme.Hom.comp_base, TopCat.coe_comp, Set.preimage_comp]
    exact hs.preimage (_ ‚â´ p).continuous
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  refine ((PrimeSpectrum.isQuotientMap_of_generalizingMap ?_ ?_).isOpen_preimage).mp hs
  ¬∑ exact (surjective_iff (Spec.map œÜ)).mp inferInstance
  ¬∑ apply RingHom.Flat.generalizingMap_comap
    rwa [‚Üê HasRingHomProperty.Spec_iff (P := @Flat)]

/-- A flat surjective morphism of schemes is an epimorphism in the category of schemes. -/
@[stacks 02VW]
lemma epi_of_flat_of_surjective (f : X ‚ü∂ Y) [Flat f] [Surjective f] : Epi f := by
  apply CategoryTheory.Functor.epi_of_epi_map (Scheme.forgetToLocallyRingedSpace)
  apply CategoryTheory.Functor.epi_of_epi_map (LocallyRingedSpace.forgetToSheafedSpace)
  apply SheafedSpace.epi_of_base_surjective_of_stalk_mono _ ‚ÄπSurjective f‚Ä∫.surj
  intro x
  apply ConcreteCategory.mono_of_injective
  algebraize [(f.stalkMap x).hom]
  have : Module.FaithfullyFlat (Y.presheaf.stalk (f x)) (X.presheaf.stalk x) :=
    @Module.FaithfullyFlat.of_flat_of_isLocalHom _ _ _ _ _ _ _
      (Flat.stalkMap f x) (f.toLRSHom.prop x)
  exact ‚ÄπRingHom.FaithfullyFlat _‚Ä∫.injective

lemma flat_and_surjective_iff_faithfullyFlat_of_isAffine [IsAffine X] [IsAffine Y] :
    Flat f ‚àß Surjective f ‚Üî f.appTop.hom.FaithfullyFlat := by
  rw [RingHom.FaithfullyFlat.iff_flat_and_comap_surjective,
    MorphismProperty.arrow_mk_iso_iff @Surjective (arrowIsoSpecŒìOfIsAffine f),
    MorphismProperty.arrow_mk_iso_iff @Flat (arrowIsoSpecŒìOfIsAffine f),
    ‚Üê HasRingHomProperty.Spec_iff (P := @Flat), surjective_iff]
  rfl

end Flat

lemma flat_and_surjective_SpecMap_iff {R S : CommRingCat.{u}} (f : R ‚ü∂ S) :
    Flat (Spec.map f) ‚àß Surjective (Spec.map f) ‚Üî f.hom.FaithfullyFlat := by
  rw [HasRingHomProperty.Spec_iff (P := @Flat),
    RingHom.FaithfullyFlat.iff_flat_and_comap_surjective, surjective_iff]
  rfl

section sections

/-!
## Sections of fibered products

Suppose we are given the following cartesian square:
```
Y --g-‚Üí X
|       |
iY      iX
‚Üì       |
T --f-‚Üí S
```
Let `U‚Çõ` be an open of `S`, `U‚Çì` and `U‚Çú` be opens of `X` and `T` mapping into `U‚Çõ`.
There is a canonical map `Œì(X, U‚Çì) ‚äó[Œì(S, U‚Çõ)] Œì(T, U‚Çú) ‚ü∂ Œì(X √ó‚Çõ T, pr‚ÇÅ ‚Åª¬π U‚Çì ‚à© pr‚ÇÇ ‚Åª¬π U‚Çú)`.

We show that this map is
1. `isIso_pushoutSection_of_isAffineOpen`:
  bijective when `U‚Çõ`, `U‚Çú`, and `U‚Çì` are all affine.
2. `mono_pushoutSection_of_isCompact_of_flat_right`:
  injective when `U‚Çõ`, `U‚Çú` are affine, `U‚Çì` is compact, and `f` is flat.
3. `isIso_pushoutSection_of_isQuasiSeparated_of_flat_right`:
  bijective when `U‚Çõ`, `U‚Çú` are affine, `U‚Çì` is qcqs, and `f` is flat.
4. `mono_pushoutSection_of_isCompact_of_flat_right_of_ringHomFlat`:
  injective when `U‚Çõ` is affine, `U‚Çú` is compact, `U‚Çì` is qcqs, `f` is flat,
  and `Œì(T, U‚Çú)` is flat over `Œì(S, U‚Çõ)` (typically true when `S = Spec k`.)
5. `isIso_pushoutSection_of_isCompact_of_flat_right_of_ringHomFlat`:
  bijective when `U‚Çõ` is affine, `U‚Çú` and `U‚Çì` are qcqs, `f` is flat,
  and `Œì(T, U‚Çú)` is flat over `Œì(S, U‚Çõ)` (typically true when `S = Spec k`.)

-/

variable {X Y S T : Scheme.{u}} {f : T ‚ü∂ S} {g : Y ‚ü∂ X} {iX : X ‚ü∂ S} {iY : Y ‚ü∂ T}
  (H : IsPullback g iY iX f)
  {US : S.Opens} {UT : T.Opens}
  {UX : X.Opens} (hUST : UT ‚â§ f ‚Åª¬π·µÅ US) (hUSX : UX ‚â§ iX ‚Åª¬π·µÅ US)
  {UY : Y.Opens} (hUY : UY = g ‚Åª¬π·µÅ UX ‚äì iY ‚Åª¬π·µÅ UT)

/-- The canonical map `Œì(X, U‚Çì) ‚äó[Œì(S, U‚Çõ)] Œì(T, U‚Çú) ‚ü∂ Œì(X √ó‚Çõ T, pr‚ÇÅ ‚Åª¬π U‚Çì ‚à© pr‚ÇÇ ‚Åª¬π U‚Çú)`.
This is an isomorphism under various circumstances. -/
abbrev pushoutSection : pushout (iX.appLE US UX hUSX) (f.appLE US UT hUST) ‚ü∂ Œì(Y, UY) :=
  pushout.desc (g.appLE UX UY (by simp [*])) (iY.appLE UT UY (by simp [*]))
    (by simp only [Scheme.Hom.appLE_comp_appLE, H.w])

lemma isIso_pushoutSection_iff :
    IsIso (pushoutSection H hUST hUSX hUY) ‚Üî IsPushout (iX.appLE US UX hUSX) (f.appLE US UT hUST)
      (g.appLE UX UY (by simp [*])) (iY.appLE UT UY (by simp [*])) :=
  ‚ü®fun _ ‚Ü¶ .of_iso (.of_hasPushout _ _) (.refl _) (.refl _) (.refl _)
    (asIso (pushoutSection H hUST hUSX hUY)) (by simp) (by simp) (by simp) (by simp),
    fun h ‚Ü¶ inferInstanceAs (IsIso h.isoPushout.inv)‚ü©

attribute [local simp] IsAffineOpen.isoSpec_hom in
attribute [local simp‚Üê ] Scheme.Hom.resLE_eq_morphismRestrict in
lemma isIso_pushoutSection_of_isAffineOpen (hUS : IsAffineOpen US) (hUT : IsAffineOpen UT)
    (hUX : IsAffineOpen UX) : IsIso (pushoutSection H hUST hUSX hUY) := by
  refine (isIso_pushoutSection_iff ..).mpr (IsPullback.of_map_of_faithful Scheme.Spec ?_).unop
  have : IsAffine _ := hUS
  have : IsAffine _ := hUT
  have : IsAffine _ := hUX
  have hUY' : IsAffineOpen UY :=
    .of_isIso (Scheme.Hom.isPullback_resLE H hUST hUSX hUY).isoPullback.hom
  exact .of_iso (Scheme.Hom.isPullback_resLE H hUST hUSX hUY).flip hUY'.isoSpec hUT.isoSpec
    hUX.isoSpec hUS.isoSpec (by simp) (by simp) (by simp) (by simp)

open TensorProduct in
lemma mono_pushoutSection_of_iSup_eq {Œπ : Type*} [Finite Œπ] (VX : Œπ ‚Üí X.Opens) (hVU : iSup VX = UX)
    (hV : ‚àÄ i, Mono (pushoutSection H hUST (show VX i ‚â§ _ by aesop) rfl))
    (hT : (f.appLE US UT hUST).hom.Flat) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  /-
  We shall show that `Œì(T, U‚Çú) ‚äó[Œì(S, U‚Çõ)] Œì(X, U) ‚ü∂ Œì(X √ó‚Çõ T, pr‚ÇÅ ‚Åª¬π U ‚à© pr‚ÇÇ ‚Åª¬π U‚Çú)` is
  injective using the following diagram
  ```
  Œì(T, U‚Çú) ‚äó Œì(X, U) ------‚Üí Œì(T, U‚Çú) ‚äó ‚àè·µ¢ Œì(X, V·µ¢)
           |                          |
           ‚Üì                          ‚Üì
  Œì(X √ó‚Çõ T, U ‚à© U‚Çú)  ------‚Üí ‚àè·µ¢ Œì(X √ó‚Çõ T, V·µ¢ ‚à© U‚Çú)
  ```
  -/
  have (i : _) : VX i ‚â§ iX ‚Åª¬π·µÅ US := by clear hV; aesop
  classical
  algebraize [(iX.appLE US UX hUSX).hom, (f.appLE US UT hUST).hom]
  let (i : _) := (iX.appLE US (VX i) (by aesop)).hom.toAlgebra
  -- This is the map `Œì(X √ó‚Çõ T, U ‚à© U‚Çú) ‚ü∂ ‚àè·µ¢ Œì(X √ó‚Çõ T, V·µ¢ ‚à© U‚Çú)` on the bottom.
  let œàY : Œì(Y, UY) ‚Üí+* Œ† i, Œì(Y, g ‚Åª¬π·µÅ VX i ‚äì iY ‚Åª¬π·µÅ UT) := Pi.ringHom fun i ‚Ü¶
      (Y.presheaf.map (homOfLE (by subst hUY hVU; gcongr; exact le_iSup _ _)).op).hom
  -- The map `Œì(X, U) ‚ü∂ ‚àè·µ¢ Œì(X, V·µ¢)`
  let œà : Œì(X, UX) ‚Üí‚Çê[Œì(S, US)] Œ† i, Œì(X, VX i) := Pi.algHom _ _ fun i ‚Ü¶
    ‚ü®(X.presheaf.map (homOfLE (hVU ‚ñ∏ le_iSup VX i)).op).hom, fun r ‚Ü¶ by
      dsimp [RingHom.algebraMap_toAlgebra]
      simp only [‚Üê CommRingCat.comp_apply, Scheme.Hom.appLE_map]‚ü©
  -- ... is injective by the sheaf axiom,
  have hœà : Function.Injective œà := by
    intro s t est
    apply X.IsSheaf.section_ext fun x hx ‚Ü¶ ?_
    simp only [‚Üê hVU, Opens.mem_iSup] at hx
    obtain ‚ü®i, hxi‚ü© := hx
    exact ‚ü®_, _, hxi, congr($est i)‚ü©
  -- ... and remains injective after tensoring with `Œì(T, U‚Çú)` by the flatness assumption.
  have hœà' : Function.Injective (Algebra.TensorProduct.map (AlgHom.id Œì(T, UT) Œì(T, UT)) œà) :=
    Module.Flat.lTensor_preserves_injective_linearMap œà.toLinearMap hœà
  simp_rw [@ConcreteCategory.mono_iff_injective_of_preservesPullback] at hV ‚ä¢
  cases nonempty_fintype Œπ
  -- And the map at the right
  let œÜ : (Œì(T, UT) ‚äó[Œì(S, US)] Œ† i, Œì(X, VX i)) ‚Üí+* Œ† i, Œì(Y, g ‚Åª¬π·µÅ VX i ‚äì iY ‚Åª¬π·µÅ UT) :=
    (Pi.ringHom fun i ‚Ü¶ (pushoutSection H hUST (show VX i ‚â§ _ by aesop) rfl).hom.comp
      ((CommRingCat.isPushout_tensorProduct _ _ _).flip.isoPushout.hom.hom.comp
      (by exact Pi.evalRingHom _ _))).comp (Algebra.TensorProduct.piRight _ Œì(S, US) _ _).toRingHom
  -- ... is also injective by our hypotheses on `V·µ¢`.
  have hœÜ : Function.Injective œÜ := by
    dsimp [œÜ]
    refine .comp ?_ (Algebra.TensorProduct.piRight _ Œì(S, US) _ _).injective
    exact .piMap fun i ‚Ü¶ (hV _).comp <| CommRingCat.isPushout_tensorProduct _ _ _
      |>.flip.isoPushout.commRingCatIsoToRingEquiv.injective
  let e : pushout (iX.appLE US UX hUSX) (f.appLE US UT hUST) ‚âÖ
      .of (Œì(T, UT) ‚äó[Œì(S, US)] Œì(X, UX)) :=
    (CommRingCat.isPushout_tensorProduct _ _ _).flip.isoPushout.symm
  -- It remains to check that the square indeed commutes, and we may concluce that the map
  -- at the left is also injective.
  suffices (œàY.comp (pushoutSection H hUST hUSX hUY).hom).comp e.inv.hom = œÜ.comp
      (Algebra.TensorProduct.map (AlgHom.id Œì(T, UT) Œì(T, UT)) œà).toRingHom by
    refine .of_comp (f := œàY) ?_
    convert (hœÜ.comp hœà').comp e.commRingCatIsoToRingEquiv.injective
    ext1 x; simpa using congr($this (e.hom x))
  ext1
  ¬∑ have H‚ÇÅ : e.inv.hom.comp Algebra.TensorProduct.includeLeftRingHom =
        (pushout.inr (C := CommRingCat) _ _).hom :=
      congr($((CommRingCat.isPushout_tensorProduct _ _ _).flip.inr_isoPushout_hom).hom)
    have H‚ÇÇ (x j) : œÜ (x ‚äó‚Çú[‚ÜëŒì(S, US)] 1) j = pushoutSection H hUST (UX := VX j) (by aesop) rfl
        (pushout.inr (C := CommRingCat) _ _ x) := congr(pushoutSection H hUST (UX := VX j) _ rfl
        ($((CommRingCat.isPushout_tensorProduct ‚ÜëŒì(S, US)
          ‚ÜëŒì(T, UT) ‚ÜëŒì(X, VX j)).flip.inr_isoPushout_hom) x))
    simp only [RingHom.comp_assoc, H‚ÇÅ]
    ext x j
    simp [œàY, H‚ÇÇ, ‚Üê CommRingCat.comp_apply, -CommRingCat.hom_comp, -ConcreteCategory.comp_apply,
      pushoutSection]
  ¬∑ have H‚ÇÅ : e.inv.hom.comp Algebra.TensorProduct.includeRight.toRingHom =
        (pushout.inl (C := CommRingCat) _ _).hom :=
      congr($((CommRingCat.isPushout_tensorProduct _ _ _).flip.inl_isoPushout_hom).hom)
    have H‚ÇÇ (x j) : œÜ (1 ‚äó‚Çú[‚ÜëŒì(S, US)] x) j = pushoutSection H hUST (UX := VX j) (by aesop) rfl
        (pushout.inl (C := CommRingCat) _ _ (x j)) := congr(pushoutSection H hUST (UX := VX j) _ rfl
        ($((CommRingCat.isPushout_tensorProduct ‚ÜëŒì(S, US)
          ‚ÜëŒì(T, UT) ‚ÜëŒì(X, VX j)).flip.inl_isoPushout_hom) (x j)))
    simp only [RingHom.comp_assoc, H‚ÇÅ]
    ext x j
    simp [œàY, H‚ÇÇ, ‚Üê CommRingCat.comp_apply, -CommRingCat.hom_comp, -ConcreteCategory.comp_apply,
      pushoutSection, œà]

lemma isIso_pushoutSection_of_iSup_eq
    {Œπ : Type u} [Finite Œπ] (VX : Œπ ‚Üí X.Opens) (hVU : iSup VX = UX)
    (hV : ‚àÄ i, IsIso (pushoutSection H hUST (show VX i ‚â§ _ by aesop) rfl))
    (hV' : ‚àÄ i j, Mono (pushoutSection H hUST
      (show VX i ‚äì VX j ‚â§ _ from inf_le_left.trans (by clear hV; aesop)) rfl))
    (hT : (f.appLE US UT hUST).hom.Flat) :
    IsIso (pushoutSection H hUST hUSX hUY) := by
  classical
  /-
  We shall show that `Œì(T, U‚Çú) ‚äó[Œì(S, U‚Çõ)] Œì(X, U) ‚ü∂ Œì(X √ó‚Çõ T, pr‚ÇÅ ‚Åª¬π U ‚à© pr‚ÇÇ ‚Åª¬π U‚Çú)` is
  injective using the following diagram
  ```
  0 ‚Üí Œì(T, U‚Çú) ‚äó Œì(X, U) ------‚Üí Œì(T, U‚Çú) ‚äó ‚àè·µ¢ Œì(X, V·µ¢) ---‚Üí Œì(T, U‚Çú) ‚äó ‚àè·µ¢‚±º Œì(X, V·µ¢ ‚à© V‚±º)
           |                              |                           |
           ‚Üì                              ‚Üì                           ‚Üì
  0 ‚Üí Œì(X √ó‚Çõ T, U ‚à© U‚Çú)  ------‚Üí ‚àè·µ¢ Œì(X √ó‚Çõ T, V·µ¢ ‚à© U‚Çú)  ---‚Üí ‚àè·µ¢ Œì(X √ó‚Çõ T, V·µ¢ ‚à© V‚±º ‚à© U‚Çú)
  ```
  The two rows are exact because of the sheaf axiom (and additionally the flatness assumption for
  the top row). The vertical arrow in the middle is an isomorphism by assumption, and the one
  one the right is monomorphic by assumption. Hence the left arrow is also an isomorphism.

  In the actual proof we use `Pairwise`-indexed diagrams instead of nested limits because it works
  better with the existing API.
  -/
  -- The diagram consisting of `Œì(X, V·µ¢) ‚ü∂ Œì(X, V·µ¢ ‚à© V‚±º)`.
  let D := Pairwise.diagram VX
  have h : iSup D.obj = UX := by
    refine le_antisymm (iSup_le_iff.mpr ?_) ?_
    ¬∑ subst hVU; rintro (i | ‚ü®i, j‚ü©); exacts [le_iSup VX _, inf_le_left.trans (le_iSup VX _)]
    ¬∑ subst hVU; exact iSup_le_iff.mpr fun i ‚Ü¶ le_iSup D.obj (.single i)
  let c‚ÇÄ : Cocone D := (colimit.cocone _).extend
    (eqToIso (Y := UX) (by simpa [CompleteLattice.colimit_eq_iSup])).hom
  -- The diagram consisting of `Œì(T, U‚Çú) ‚äó Œì(X, V·µ¢) ‚ü∂ Œì(T, U‚Çú) ‚äó Œì(X, V·µ¢ ‚à© V‚±º)`.
  let F := Under.lift _ ((Functor.const _).map (iX.appLE US UX hUSX) ‚â´
    ((X.presheaf.mapCone c‚ÇÄ.op).œÄ)) ‚ãô Under.pushout (f.appLE US UT hUST) ‚ãô Under.forget _
  let G : X.Opens ‚•§ Y.Opens :=
    { obj U := g ‚Åª¬π·µÅ U ‚äì iY ‚Åª¬π·µÅ UT, map h := homOfLE (by gcongr; exact h.le) }
  -- The natural transformation between the diagrams at the top and bottom.
  let Œ±F : F ‚ü∂ D.op ‚ãô G.op ‚ãô Y.presheaf :=
  { app i := (pushout.congrHom (by simp) rfl).hom ‚â´
      pushoutSection H hUST (by grw [‚Üê hUSX, ‚Üê h]; exact le_iSup D.obj i.unop) rfl }
  -- `Œì(T, U‚Çú) ‚äó Œì(X, U)` as a (limit) cone over the top diagram.
  let c : Cone F := (Under.pushout (f.appLE US UT hUST) ‚ãô Under.forget _).mapCone
    (Under.liftCone (X.presheaf.mapCone c‚ÇÄ.op) _)
  have := CommRingCat.Under.preservesFiniteLimits_of_flat _ hT
  cases nonempty_fintype Œπ
  let hc : IsLimit c :=
    haveI HX := ((TopCat.Presheaf.isSheaf_iff_isSheafPreservesLimitPairwiseIntersections
      _).mp X.IsSheaf VX).preserves (c := c‚ÇÄ.op)
    haveI HX := (HX (IsColimit.extendIso _ (colimit.isColimit _)).op).some
    isLimitOfPreserves (Under.pushout _ ‚ãô Under.forget _) (Under.isLimitLiftCone _ _ HX)
  let c'‚ÇÄ : Cocone (D ‚ãô G) := (colimit.cocone _).extend
    (eqToIso (Y := UY) (by
      simp only [colimit.cocone_x, CompleteLattice.colimit_eq_iSup]
      eta_expand
      dsimp [G]
      rw [‚Üê iSup_inf_eq, ‚Üê Scheme.Hom.preimage_iSup, h, hUY])).hom
  -- `Œì(X √ó‚Çõ T, U ‚à© U‚Çú)` as a (limit) cone over the bottom diagram.
  let c' : Cone (D.op ‚ãô G.op ‚ãô Y.presheaf) := Y.presheaf.mapCone c'‚ÇÄ.op
  let hc' : IsLimit c' := by
    letI e : D ‚ãô G ‚âÖ Pairwise.diagram fun i ‚Ü¶ g ‚Åª¬π·µÅ VX i ‚äì iY ‚Åª¬π·µÅ UT :=
      NatIso.ofComponents (fun | .single i => .refl _ | .pair i j => eqToIso (by
        dsimp [D, G]; rw [Scheme.Hom.preimage_inf, inf_inf_distrib_right]))
    haveI HX := ((TopCat.Presheaf.isSheaf_iff_isSheafPreservesLimitPairwiseIntersections _).mp
      Y.IsSheaf (fun i ‚Ü¶ g ‚Åª¬π·µÅ VX i ‚äì iY ‚Åª¬π·µÅ UT)).preserves
        (c := ((Cocones.precompose e.inv).obj c'‚ÇÄ).op)
    exact (IsLimit.postcomposeHomEquiv (Functor.isoWhiskerRight (NatIso.op e.symm) Y.presheaf) _)
      ((HX ((IsColimit.precomposeInvEquiv e _).symm
        (IsColimit.extendIso _ (colimit.isColimit _))).op).some.ofIsoLimit (Cones.ext (.refl _)))
  have HŒ±F‚ÇÇ (i j : _) : Mono (Œ±F.app (.op <| .pair i j)) := by infer_instance
  -- We construct the morphisms between the cone points,
  let f‚ÇÅ : c.pt ‚ü∂ c'.pt := hc'.lift ((Cones.postcompose Œ±F).obj c)
  let f‚ÇÇ : c'.pt ‚ü∂ c.pt := hc.lift ‚ü®c'.pt, ‚ü®fun
    | .op (.single i) => c'.œÄ.app _ ‚â´ inv (Œ±F.app (.op <| .single i))
    | .op (.pair i j) => c'.œÄ.app (.op (.single i)) ‚â´ inv (Œ±F.app (.op <| .single i)) ‚â´
        F.map (Quiver.Hom.op <| Pairwise.Hom.left i j), by
    rintro ‚ü®i‚ü© ‚ü®j‚ü© f
    obtain ‚ü®i | ‚ü®i, j‚ü© | ‚ü®i, j‚ü© | ‚ü®i, j‚ü©, rfl‚ü© :=
      (show Function.Surjective Quiver.Hom.op from Quiver.Hom.opEquiv.surjective) f
    ¬∑ simp [show Pairwise.Hom.id_single i = ùüô (Pairwise.single i) from rfl]
    ¬∑ simp [show Pairwise.Hom.id_pair i j = ùüô (Pairwise.pair i j) from rfl]
    ¬∑ simp
    ¬∑ rw [‚Üê cancel_mono (Œ±F.app _)]
      simpa using (c'.w (Quiver.Hom.op <| Pairwise.Hom.left i j)).trans
        (c'.w (Quiver.Hom.op <| Pairwise.Hom.right i j)).symm‚ü©‚ü©
  -- and prove that they form an isomorphism.
  let e : c.pt ‚âÖ c'.pt := by
    refine ‚ü®f‚ÇÅ, f‚ÇÇ, hc.hom_ext ?_, hc'.hom_ext ?_‚ü©
    ¬∑ rintro ‚ü®i | ‚ü®i, j‚ü©‚ü© <;> simp [f‚ÇÅ, f‚ÇÇ]
    ¬∑ rintro ‚ü®i | ‚ü®i, j‚ü©‚ü©
      ¬∑ simp [f‚ÇÅ, f‚ÇÇ]
      ¬∑ simpa [f‚ÇÅ, f‚ÇÇ] using c'.w (Quiver.Hom.op <| Pairwise.Hom.left i j)
  convert e.isIso_hom using 1
  ¬∑ refine hc'.hom_ext fun i ‚Ü¶ ?_
    rw [hc'.fac]
    ext1
    ¬∑ simp [Œ±F, c, Under.liftCone, c', c‚ÇÄ]
    ¬∑ simp [Œ±F, c, c']

lemma mono_pushoutSection_of_isCompact_of_flat_right [Flat f]
    (hUS : IsAffineOpen US) (hUT : IsAffineOpen UT) (hUX : IsCompact (X := X) UX) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  classical
  obtain ‚ü®I, hI, e‚ü© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hUX
  have := hI.to_subtype
  exact mono_pushoutSection_of_iSup_eq (Œπ := I) H hUST hUSX hUY (¬∑) (by rwa [iSup_subtype, eq_comm])
    (fun i ‚Ü¶ have := isIso_pushoutSection_of_isAffineOpen H hUST _ rfl hUS hUT i.1.2; inferInstance)
    (f.flat_appLE hUS hUT _)

lemma mono_pushoutSection_of_isCompact_of_flat_left [Flat iX]
    (hUS : IsAffineOpen US) (hUX : IsAffineOpen UX) (hUT : IsCompact (X := T) UT) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  suffices Mono (pushoutSection H.flip hUSX hUST (hUY.trans (inf_comm _ _))) by
    rw [‚Üê mono_comp_iff_of_isIso (pushoutSymmetry _ _).hom]; convert this; cat_disch
  exact mono_pushoutSection_of_isCompact_of_flat_right _ _ _ _ hUS hUX hUT

lemma isIso_pushoutSection_of_isQuasiSeparated_of_flat_right [Flat f]
    (hUS : IsAffineOpen US) (hUT : IsAffineOpen UT)
    (hUX : IsCompact (X := X) UX) (hUX' : IsQuasiSeparated (Œ± := X) UX) :
    IsIso (pushoutSection H hUST hUSX hUY) := by
  classical
  obtain ‚ü®I, hI, e‚ü© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hUX
  have hIUX (i : I) : i.1 ‚â§ UX := by rw [e]; intro i; aesop
  have := hI.to_subtype
  exact isIso_pushoutSection_of_iSup_eq H hUST hUSX hUY (fun i : I ‚Ü¶ i) (by rwa [iSup_subtype,
    eq_comm]) (fun i ‚Ü¶ isIso_pushoutSection_of_isAffineOpen _ _ _ _ hUS hUT i.1.2) (fun i j ‚Ü¶
    mono_pushoutSection_of_isCompact_of_flat_right _ _ _ _ hUS hUT (hUX' _ _ (hIUX _) i.1.1.2
    i.1.2.isCompact (hIUX _) j.1.1.2 j.1.2.isCompact))
    (f.flat_appLE hUS hUT _)

lemma isIso_pushoutSection_of_isQuasiSeparated_of_flat_left [Flat iX]
    (hUS : IsAffineOpen US) (hUX : IsAffineOpen UX)
    (hUT : IsCompact (X := T) UT) (hUT' : IsQuasiSeparated (Œ± := T) UT) :
    IsIso (pushoutSection H hUST hUSX hUY) := by
  suffices IsIso (pushoutSection H.flip hUSX hUST (hUY.trans (inf_comm _ _))) by
    rw [‚Üê isIso_comp_left_iff (pushoutSymmetry _ _).hom]; convert this; cat_disch
  exact isIso_pushoutSection_of_isQuasiSeparated_of_flat_right _ _ _ _ hUS hUX hUT hUT'

lemma mono_pushoutSection_of_isCompact_of_flat_left_of_ringHomFlat [Flat iX]
    (hUS : IsAffineOpen US) (hUT : IsCompact (X := T) UT)
    (hUX : IsCompact (X := X) UX) (hf : (f.appLE US UT hUST).hom.Flat) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  obtain ‚ü®I, hI, e‚ü© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hUX
  have := hI.to_subtype
  exact mono_pushoutSection_of_iSup_eq _ _ _ _ (fun i : I ‚Ü¶ i) (by rwa [iSup_subtype, eq_comm])
    (fun i ‚Ü¶ mono_pushoutSection_of_isCompact_of_flat_left _ _ _ _ hUS i.1.2 hUT) hf

lemma mono_pushoutSection_of_isCompact_of_flat_right_of_ringHomFlat [Flat f]
    (hUS : IsAffineOpen US) (hUT : IsCompact (X := T) UT)
    (hUX : IsCompact (X := X) UX) (hiX : (iX.appLE US UX hUSX).hom.Flat) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  suffices Mono (pushoutSection H.flip hUSX hUST (hUY.trans (inf_comm _ _))) by
    rw [‚Üê mono_comp_iff_of_isIso (pushoutSymmetry _ _).hom]; convert this; cat_disch
  exact mono_pushoutSection_of_isCompact_of_flat_left_of_ringHomFlat _ _ _ _ hUS hUX hUT hiX

include H in
lemma isIso_pushoutSection_of_isCompact_of_flat_right_of_ringHomFlat [Flat f]
    (hUS : IsAffineOpen US) (hUT : IsCompact (X := T) UT) (hUT' : IsQuasiSeparated (Œ± := T) UT)
    (hUX : IsCompact (X := X) UX) (hUX' : IsQuasiSeparated (Œ± := X) UX)
    (hiX : (iX.appLE US UX hUSX).hom.Flat) :
    IsIso (pushoutSection H hUST hUSX hUY) := by
  suffices IsIso (pushoutSection H.flip hUSX hUST (hUY.trans (inf_comm _ _))) by
    rw [‚Üê isIso_comp_left_iff (pushoutSymmetry _ _).hom]; convert this; cat_disch
  obtain ‚ü®I, hI, e‚ü© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hUT
  have hIUT (i : I) : i.1 ‚â§ UT := by rw [e]; intro i; aesop
  have := hI.to_subtype
  exact isIso_pushoutSection_of_iSup_eq _ _ _ _ (fun i : I ‚Ü¶ i) (by rwa [iSup_subtype, eq_comm])
    (fun i ‚Ü¶ isIso_pushoutSection_of_isQuasiSeparated_of_flat_left _ _ _ _ hUS i.1.2 hUX hUX')
    (fun i j ‚Ü¶ mono_pushoutSection_of_isCompact_of_flat_left_of_ringHomFlat _ _ _ _ hUS hUX
    (hUT' _ _ (hIUT _) i.1.1.2 i.1.2.isCompact (hIUT _) j.1.1.2 j.1.2.isCompact) hiX) hiX

end sections

end AlgebraicGeometry
