/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.AlgebraicGeometry.AffineScheme
public import Mathlib.AlgebraicGeometry.RelativeGluing
public import Mathlib.CategoryTheory.Sites.DenseSubsite.InducedTopology

/-!

# The small affine Zariski site

`X.AffineZariskiSite` is the small affine Zariski site of `X`, whose elements are affine open
sets of `X`, and whose arrows are basic open sets `D(f) ‚ü∂ U` for any `f : Œì(X, U)`.

Every presieve on `U` is then given by a `Set Œì(X, U)` (`presieveOfSections_surjective`), and
we endow `X.AffineZariskiSite` with `grothendieckTopology X`, such that `s : Set Œì(X, U)` is
a cover if and only if `Ideal.span s = ‚ä§` (`generate_presieveOfSections_mem_grothendieckTopology`).

This is a dense subsite of `X.Opens` (with respect to `Opens.grothendieckTopology X`) via the
inclusion functor `toOpensFunctor X`,
which gives an equivalence of categories of sheaves (`sheafEquiv`).

Note that this differs from the definition on stacks project where the arrows in the small affine
Zariski site are arbitrary inclusions.

-/

@[expose] public section

universe u

open CategoryTheory Limits

noncomputable section

namespace AlgebraicGeometry

variable {X : Scheme.{u}}

/--
`X.AffineZariskiSite` is the small affine Zariski site of `X`, whose elements are affine open
sets of `X`, and whose arrows are basic open sets `D(f) ‚ü∂ U` for any `f : Œì(X, U)`.

Note that this differs from the definition on stacks project where the arrows in the small affine
Zariski site are arbitrary inclusions.
-/
def Scheme.AffineZariskiSite (X : Scheme.{u}) : Type u := { U : X.Opens // IsAffineOpen U }

namespace Scheme.AffineZariskiSite

/-- The inclusion from `X.AffineZariskiSite` to `X.Opens`. -/
abbrev toOpens (U : X.AffineZariskiSite) : X.Opens := U.1

instance : Preorder X.AffineZariskiSite where
  le U V := ‚àÉ f : Œì(X, V.toOpens), X.basicOpen f = U.toOpens
  le_refl U := ‚ü®1, Scheme.basicOpen_of_isUnit _ isUnit_one‚ü©
  le_trans := by
    rintro ‚ü®U, hU‚ü© ‚ü®V, hV‚ü© ‚ü®W, hW‚ü© ‚ü®f, rfl‚ü© ‚ü®g, rfl‚ü©
    exact hW.basicOpen_basicOpen_is_basicOpen g f

lemma toOpens_mono :
    Monotone (toOpens (X := X)) := by
  rintro ‚ü®U, hU‚ü© ‚ü®V, hV‚ü© ‚ü®f, rfl‚ü©
  exact X.basicOpen_le _

lemma toOpens_injective : Function.Injective (toOpens (X := X)) := Subtype.val_injective

instance : PartialOrder X.AffineZariskiSite where
  le_antisymm _ _ hUV hVU := Subtype.ext ((toOpens_mono hUV).antisymm (toOpens_mono hVU))

/-- The basic open set of a section, as an element of `AffineZariskiSite`. -/
@[simps] def basicOpen (U : X.AffineZariskiSite) (f : Œì(X, U.toOpens)) : X.AffineZariskiSite :=
  ‚ü®X.basicOpen f, U.2.basicOpen f‚ü©

lemma basicOpen_le (U : X.AffineZariskiSite) (f : Œì(X, U.toOpens)) : U.basicOpen f ‚â§ U :=
  ‚ü®f, rfl‚ü©

variable (X) in
/-- The inclusion functor from `X.AffineZariskiSite` to `X.Opens`. -/
@[simps! obj]
def toOpensFunctor : X.AffineZariskiSite ‚•§ X.Opens := toOpens_mono.functor

instance : (toOpensFunctor X).Faithful where

variable (X) in
/-- The forgetful functor from `X.AffineZariskiSite` to `Scheme` is isomorphic to `Spec Œì(X, -)`. -/
@[simps! hom_app inv_app]
def restrictIsoSpec : toOpensFunctor X ‚ãô X.restrictFunctor ‚ãô Over.forget _ ‚âÖ
    toOpensFunctor X ‚ãô X.presheaf.rightOp ‚ãô Scheme.Spec :=
  NatIso.ofComponents (fun U ‚Ü¶ U.2.isoSpec)
    fun _ ‚Ü¶ (Scheme.Opens.toSpecŒì_SpecMap_presheaf_map ..).symm

section GrothendieckTopology

instance : (toOpensFunctor X).IsLocallyFull (Opens.grothendieckTopology X) where
  functorPushforward_imageSieve_mem := by
    intro U V h x hx
    obtain ‚ü®f, hfU, hxf‚ü© := V.2.exists_basicOpen_le ‚ü®x, hx‚ü© (h.le hx)
    exact ‚ü®X.basicOpen f, homOfLE hfU, ‚ü®V.basicOpen f,
      ‚ü®_, (X.basicOpen_res f h.op).trans (inf_eq_right.mpr hfU)‚ü©, ùüô _,
      ‚ü®‚ü®f, rfl‚ü©, rfl‚ü©, rfl‚ü©, hxf‚ü©

instance : (toOpensFunctor X).IsCoverDense (Opens.grothendieckTopology X) where
  is_cover := by
    intro U x hx
    obtain ‚ü®_, ‚ü®V, hV, rfl‚ü©, hxV, hVU‚ü© := X.isBasis_affineOpens.exists_subset_of_mem_open hx U.2
    exact ‚ü®V, homOfLE hVU, ‚ü®‚ü®V, hV‚ü©, ùüô _, homOfLE hVU, rfl‚ü©, hxV‚ü©

variable (X) in
/-- The Grothendieck topology on `X.AffineZariskiSite` induced from the topology on `X.Opens`.
Also see `mem_grothendieckTopology_iff_sectionsOfPresieve`. -/
def grothendieckTopology : GrothendieckTopology X.AffineZariskiSite :=
  (toOpensFunctor X).inducedTopology (Opens.grothendieckTopology X)

lemma mem_grothendieckTopology {U : X.AffineZariskiSite} {S : Sieve U} :
    S ‚àà grothendieckTopology X U ‚Üî
      ‚àÄ x ‚àà U.toOpens, ‚àÉ (V : _) (f : V ‚ü∂ U), S.arrows f ‚àß x ‚àà V.toOpens := by
  apply forall‚ÇÇ_congr fun x hxU ‚Ü¶ ‚ü®?_, ?_‚ü©
  ¬∑ rintro ‚ü®V, f, ‚ü®W, g, h, hg, rfl‚ü©, hxV‚ü©
    exact ‚ü®W, g, hg, h.le hxV‚ü©
  ¬∑ rintro ‚ü®W, g, hg, hxW‚ü©
    exact ‚ü®W.toOpens, homOfLE (toOpens_mono g.le), ‚ü®W, g, ùüô _, hg, rfl‚ü©, hxW‚ü©

instance : (toOpensFunctor X).IsDenseSubsite
    (grothendieckTopology X) (Opens.grothendieckTopology X) where
  functorPushforward_mem_iff := Iff.rfl

/-- The presieve associated to a set of sections.
This is a surjection, see `presieveOfSections_surjective`. -/
def presieveOfSections (U : X.AffineZariskiSite) (s : Set Œì(X, U.toOpens)) : Presieve U :=
  fun V _ ‚Ü¶ ‚àÉ f ‚àà s, X.basicOpen f = V.toOpens

/-- The set of sections associated to a presieve. -/
def sectionsOfPresieve {U : X.AffineZariskiSite} (P : Presieve U) : Set Œì(X, U.toOpens) :=
  { f | P (homOfLE (U.basicOpen_le f)) }

lemma presieveOfSections_sectionsOfPresieve {U : X.AffineZariskiSite} (P : Presieve U) :
    presieveOfSections U (sectionsOfPresieve P) = P := by
  refine funext‚ÇÇ fun ‚ü®V, hV‚ü© ‚ü®f, hf‚ü© ‚Ü¶ eq_iff_iff.mpr ‚ü®?_, ?_‚ü©
  ¬∑ rintro ‚ü®_, H, rfl‚ü©
    exact H
  ¬∑ intro H
    obtain rfl : _ = V := hf
    exact ‚ü®_, H, rfl‚ü©

lemma presieveOfSections_surjective {U : X.AffineZariskiSite} :
    Function.Surjective (presieveOfSections U) :=
  fun _ ‚Ü¶ ‚ü®_, presieveOfSections_sectionsOfPresieve _‚ü©

lemma presieveOfSections_eq_ofArrows (U : X.AffineZariskiSite) (s : Set Œì(X, U.toOpens)) :
    presieveOfSections U s = .ofArrows _ (fun i : s ‚Ü¶ homOfLE (U.basicOpen_le i.1)) := by
  refine funext‚ÇÇ fun ‚ü®V, hV‚ü© ‚ü®f, hf‚ü© ‚Ü¶ eq_iff_iff.mpr ‚ü®?_, ?_‚ü©
  ¬∑ rintro ‚ü®f, hfs, rfl‚ü©
    exact .mk (Œπ := s) ‚ü®f, hfs‚ü©
  ¬∑ rintro ‚ü®‚ü®f, hfs‚ü©‚ü©
    exact ‚ü®f, hfs, rfl‚ü©

lemma generate_presieveOfSections
    {U V : X.AffineZariskiSite} {s : Set Œì(X, U.toOpens)} {f : V ‚ü∂ U} :
    Sieve.generate (presieveOfSections U s) f ‚Üî ‚àÉ f ‚àà s, ‚àÉ g, X.basicOpen (f * g) = V.toOpens := by
  obtain ‚ü®V, hV‚ü© := V
  constructor
  ¬∑ rintro ‚ü®‚ü®W, hW‚ü©, ‚ü®f‚ÇÅ, hf‚ÇÅ‚ü©, -, ‚ü®f‚ÇÇ, hf‚ÇÇs, rfl‚ü©, rfl‚ü©
    subst hf‚ÇÅ
    obtain ‚ü®f‚ÇÉ, hf‚ÇÉ‚ü© := U.2.basicOpen_basicOpen_is_basicOpen f‚ÇÇ f‚ÇÅ
    refine ‚ü®f‚ÇÇ, hf‚ÇÇs, f‚ÇÉ, ?_‚ü©
    rw [X.basicOpen_mul, hf‚ÇÉ, inf_eq_right]
    exact X.basicOpen_le _
  ¬∑ rintro ‚ü®f‚ÇÅ, hf‚ÇÅs, f‚ÇÇ, rfl‚ü©
    refine ‚ü®U.basicOpen f‚ÇÅ, ‚ü®f‚ÇÇ |_ _, ?_‚ü©, ‚ü®f‚ÇÅ, rfl‚ü©, ‚ü®f‚ÇÅ, hf‚ÇÅs, rfl‚ü©, rfl‚ü©
    exact (X.basicOpen_res _ _).trans (X.basicOpen_mul _ _).symm

lemma generate_presieveOfSections_mem_grothendieckTopology
    {U : X.AffineZariskiSite} {s : Set Œì(X, U.toOpens)} :
    Sieve.generate (presieveOfSections U s) ‚àà grothendieckTopology X U ‚Üî Ideal.span s = ‚ä§ := by
  rw [‚Üê U.2.self_le_iSup_basicOpen_iff, mem_grothendieckTopology, SetLike.le_def]
  refine forall‚ÇÇ_congr fun x hx ‚Ü¶ ?_
  simp only [exists_and_left, TopologicalSpace.Opens.iSup_mk,
    TopologicalSpace.Opens.carrier_eq_coe, Set.iUnion_coe_set, TopologicalSpace.Opens.mem_mk,
    Set.mem_iUnion, SetLike.mem_coe, exists_prop, generate_presieveOfSections]
  constructor
  ¬∑ simp only [basicOpen_mul]
    rintro ‚ü®‚ü®V, hV‚ü©, ‚ü®f, hfs, g, rfl‚ü©, -, hxV‚ü©
    exact ‚ü®f, hfs, hxV.1‚ü©
  ¬∑ rintro ‚ü®f, hfs, hxf‚ü©
    refine ‚ü®U.basicOpen _, ‚ü®f, hfs, 1, rfl‚ü©, ‚ü®_, rfl‚ü©, by simpa using hxf‚ü©

lemma mem_grothendieckTopology_iff_sectionsOfPresieve
    {U : X.AffineZariskiSite} {S : Sieve U} :
    S ‚àà grothendieckTopology X U ‚Üî Ideal.span (sectionsOfPresieve S.1) = ‚ä§ := by
  rw [‚Üê generate_presieveOfSections_mem_grothendieckTopology, presieveOfSections_sectionsOfPresieve,
    Sieve.generate_sieve]

variable {A} [Category* A]
variable [‚àÄ (U : X.Opens·µí·µñ), Limits.HasLimitsOfShape (StructuredArrow U (toOpensFunctor X).op) A]

/-- The category of sheaves on `X.AffineZariskiSite` is equivalent to the categories of sheaves
over `X`. -/
abbrev sheafEquiv : Sheaf (grothendieckTopology X) A ‚âå TopCat.Sheaf A X :=
    (toOpensFunctor X).sheafInducedTopologyEquivOfIsCoverDense _ _

end GrothendieckTopology

variable (X) in
/-- The directed cover of a scheme indexed by `X.AffineZariskiSite`.
Note the related `Scheme.directedAffineCover`, which has the same (defeq) cover but a different
category instance on the indices. -/
@[simps] abbrev directedCover : X.OpenCover where
  I‚ÇÄ := X.AffineZariskiSite
  X U := U.1
  f U := U.1.Œπ
  mem‚ÇÄ := by
    rw [presieve‚ÇÄ_mem_precoverage_iff]
    refine ‚ü®fun x ‚Ü¶ ?_, inferInstance‚ü©
    obtain ‚ü®U, hxU‚ü© := TopologicalSpace.Opens.mem_iSup.mp
      ((iSup_affineOpens_eq_top X).ge (Set.mem_univ x))
    exact ‚ü®U, ‚ü®x, hxU‚ü©, rfl‚ü©

noncomputable instance : (Scheme.AffineZariskiSite.directedCover X).LocallyDirected where
  trans f := X.homOfLE (((Scheme.AffineZariskiSite.toOpensFunctor _).map f).le)
  directed {U V} x := by
    let a := (pullback.fst _ _ ‚â´ U.1.Œπ) x
    have haU : a ‚àà U.1 := (pullback.fst U.1.Œπ V.1.Œπ x).2
    have haV : a ‚àà V.1 := by unfold a; rw [pullback.condition]; exact (pullback.snd U.1.Œπ V.1.Œπ x).2
    obtain ‚ü®f, g, e, hxf‚ü© := exists_basicOpen_le_affine_inter U.2 V.2 _ ‚ü®haU, haV‚ü©
    refine ‚ü®U.basicOpen f, homOfLE (U.basicOpen_le f), eqToHom (Subtype.ext (by exact e)) ‚â´
      homOfLE (V.basicOpen_le g), ‚ü®a, hxf‚ü©, ?_‚ü©
    apply (pullback.fst _ _ ‚â´ U.1.Œπ).isOpenEmbedding.injective
    dsimp
    change (pullback.lift _ _ _ ‚â´ pullback.fst _ _ ‚â´ U.1.Œπ) _ = _
    simp only [pullback.lift_fst_assoc, homOfLE_Œπ, Opens.Œπ_apply]
    rfl

section PreservesLocalization

/-!
## "Quasi-coherent `ùí™‚Çì`-algebras"

A presheaf `F` of rings on `X.AffineZariskiSite` with a structural morphism `Œ± : ùí™‚Çì ‚ü∂ F`
is said to be `Coequifibered` if `F(D(f)) = F(U)[1/f]`
for every open `U` and any section `f : Œì(X, U)`.
(See `coequifibered_iff_forall_isLocalizationAway`)

Under this condition we can construct a family of gluing data (See `relativeGluingData`) and glue
`F` into a scheme over `X` via `(relativeGluingData _).glued`,
Also see the relative gluing API in `Mathlib/AlgebraicGeometry/RelativeGluing.lean`.

This is closely related to the notion of quasi-coherent `ùí™‚Çì`-algebras, and we shall link them
together once the theory of quasi-coherent `ùí™‚Çì`-algebras are developed.
-/

variable (X) in
/-- `X` is the colimit of its affine opens. See `isColimit_cocone` below. -/
@[simps] noncomputable def cocone :
    Limits.Cocone (toOpensFunctor X ‚ãô X.presheaf.rightOp ‚ãô Scheme.Spec) where
  pt := X
  Œπ.app U := U.2.fromSpec
  Œπ.naturality {U V} f := by dsimp; rw [V.2.map_fromSpec U.2]; simp

lemma coequifibered_iff_forall_isLocalizationAway {F : X.AffineZariskiSite·µí·µñ ‚•§ CommRingCat}
    {Œ± : (AffineZariskiSite.toOpensFunctor X).op ‚ãô X.presheaf ‚ü∂ F} :
    Œ±.Coequifibered ‚Üî ‚àÄ (U : X.AffineZariskiSite) (f : Œì(X, U.1)),
      letI := (F.map (homOfLE (U.basicOpen_le f)).op).hom.toAlgebra
      IsLocalization.Away (Œ±.app (.op U) f) (F.obj (.op (U.basicOpen f))) := by
  trans ‚àÄ (U : X.AffineZariskiSite) (f : Œì(X, U.1)),
    IsPushout (X.presheaf.map (homOfLE (X.basicOpen_le f)).op)
      (Œ±.app _) (Œ±.app (.op (U.basicOpen f))) (F.map (homOfLE (U.basicOpen_le f)).op)
  ¬∑ refine ‚ü®fun H U f ‚Ü¶ H (homOfLE (U.basicOpen_le f)).op, fun H ‚ü®V‚ü© ‚ü®U‚ü© ‚ü®f, hf‚ü© ‚Ü¶ ?_‚ü©
    obtain rfl : V.basicOpen f = U := Subtype.ext hf
    exact H V f
  refine forall‚ÇÇ_congr fun U f ‚Ü¶ ?_
  set Œ±U : Œì(X, U.toOpens) ‚ü∂ F.obj (.op U) := Œ±.app (.op U)
  set Œ±Uf : Œì(X, X.basicOpen f) ‚ü∂ F.obj (.op (U.basicOpen f)) := Œ±.app (.op (U.basicOpen f))
  algebraize [(X.presheaf.map (homOfLE (X.basicOpen_le f)).op).hom, Œ±U.hom, Œ±Uf.hom,
    (F.map (U.basicOpen_le f).hom.op).hom, (F.map (U.basicOpen_le f).hom.op).hom.comp Œ±U.hom]
  have : IsScalarTower Œì(X, U.toOpens) Œì(X, X.basicOpen f) (F.obj (.op (U.basicOpen f))) :=
    .of_algebraMap_eq' congr($(Œ±.naturality (U.basicOpen_le f).hom.op).hom).symm
  have : IsLocalization.Away f Œì(X, X.basicOpen f) := U.2.isLocalization_basicOpen _
  refine (CommRingCat.isPushout_iff_isPushout ..).trans ?_
  rw [Algebra.IsPushout.comm]
  refine (Algebra.isLocalization_iff_isPushout (.powers f) Œì(X, X.basicOpen f)).symm.trans ?_
  simp [RingHom.algebraMap_toAlgebra]

@[deprecated (since := "2026-02-01")] alias PreservesLocalization := NatTrans.Coequifibered

/-- The relative gluing data associated to a quasi-coherent `ùí™‚Çì` algebra. -/
def relativeGluingData {F : X.AffineZariskiSite·µí·µñ ‚•§ CommRingCat}
    {Œ± : (AffineZariskiSite.toOpensFunctor X).op ‚ãô X.presheaf ‚ü∂ F}
    (H : Œ±.Coequifibered) :
    (AffineZariskiSite.directedCover X).RelativeGluingData where
  functor := F.rightOp ‚ãô Scheme.Spec
  natTrans := Functor.whiskerRight Œ±.rightOp Scheme.Spec ‚â´ (restrictIsoSpec X).inv
  equifibered := (H.rightOp.whiskerRight _).comp (.of_isIso _)

@[deprecated "By `inferInstance`." (since := "2026-02-01")]
lemma PreservesLocalization.isLocallyDirected (F : X.AffineZariskiSite·µí·µñ ‚•§ CommRingCat)
    (Œ± : (AffineZariskiSite.toOpensFunctor X).op ‚ãô X.presheaf ‚ü∂ F)
    (H : Œ±.Coequifibered) :
    ((F.rightOp ‚ãô Scheme.Spec) ‚ãô Scheme.forget).IsLocallyDirected :=
  (relativeGluingData H).instIsLocallyDirectedI‚ÇÄCompFunctorForgetOfIsThin

@[deprecated "By `inferInstance`." (since := "2026-02-01")]
lemma PreservesLocalization.isOpenImmersion (F : X.AffineZariskiSite·µí·µñ ‚•§ CommRingCat)
    (Œ± : (AffineZariskiSite.toOpensFunctor X).op ‚ãô X.presheaf ‚ü∂ F)
    (H : Œ±.Coequifibered) :
    ‚àÄ ‚¶ÉU V‚¶Ñ (f : U ‚ü∂ V), IsOpenImmersion ((F.rightOp ‚ãô Scheme.Spec).map f) := by
  exact fun U V ‚Ü¶ (relativeGluingData H).instIsOpenImmersionMapI‚ÇÄFunctor

lemma opensRange_relativeGluingData_map (F : X.AffineZariskiSite·µí·µñ ‚•§ CommRingCat)
    (Œ± : (AffineZariskiSite.toOpensFunctor X).op ‚ãô X.presheaf ‚ü∂ F)
    (H : Œ±.Coequifibered) {U : X.AffineZariskiSite} (r : Œì(X, U.1)) :
    ((relativeGluingData H).functor.map (homOfLE (U.basicOpen_le r))).opensRange =
      PrimeSpectrum.basicOpen (Œ±.app (.op U) r) := by
  have := coequifibered_iff_forall_isLocalizationAway.mp H U r
  let := (F.map (homOfLE (U.basicOpen_le r)).op).hom.toAlgebra
  apply TopologicalSpace.Opens.coe_inj.mp ?_
  refine PrimeSpectrum.localization_away_comap_range (F.obj (.op <| U.basicOpen r))
    (Œ±.app (.op U) r)

@[deprecated (since := "2026-02-01")]
alias PreservesLocalization.opensRange_map := opensRange_relativeGluingData_map

@[deprecated Cover.RelativeGluingData.toBase_preimage_eq_opensRange_Œπ (since := "2026-02-01")]
lemma PreservesLocalization.colimitDesc_preimage (F : X.AffineZariskiSite·µí·µñ ‚•§ CommRingCat)
    (Œ± : (AffineZariskiSite.toOpensFunctor X).op ‚ãô X.presheaf ‚ü∂ F)
    (H : Œ±.Coequifibered) (U : X.AffineZariskiSite) :
    (relativeGluingData H).toBase ‚Åª¬π·µÅ U.1 = ((relativeGluingData H).cover.f U).opensRange := by
  simpa using (relativeGluingData H).toBase_preimage_eq_opensRange_Œπ U

@[deprecated (since := "2026-02-01")]
alias _root_.AlgebraicGeometry.Scheme.preservesLocalization_toOpensFunctor :=
  NatTrans.Coequifibered.of_isIso

variable (X) in
/-- `X` is the colimit of its affine opens. -/
noncomputable def isColimitCocone : IsColimit (cocone X) :=
  letI D := relativeGluingData (X := X) (.of_isIso (ùüô _))
  letI F := D.functor
  -- Why doesn't typeclass synthesis work here?
  -- It does fire if one adds `(C := no_index(_))` to the composition in the instance.
  haveI : (D.functor ‚ãô forget).IsLocallyDirected :=
    Cover.RelativeGluingData.instIsLocallyDirectedI‚ÇÄCompFunctorForgetOfIsThin ..
  haveI : IsIso ((colimit.isColimit F).desc (cocone X:)) := by
    refine (IsZariskiLocalAtTarget.iff_of_openCover (P := .isomorphisms _)
      (X.openCoverOfIsOpenCover _ (iSup_affineOpens_eq_top X))).mpr fun U ‚Ü¶ ?_
    change IsIso (pullback.snd (colimit.desc F (cocone X)) U.1.Œπ)
    let e := IsOpenImmersion.isoOfRangeEq (pullback.fst (colimit.desc F (cocone X)) U.1.Œπ)
      (U.2.isoSpec.hom ‚â´ colimit.Œπ F U) <| by
      rw [Pullback.range_fst, Opens.range_Œπ, ‚Üê Hom.coe_opensRange, Hom.opensRange_comp_of_isIso,
        ‚Üê Scheme.Hom.coe_preimage]
      convert congr($(D.toBase_preimage_eq_opensRange_Œπ U).1)
      ¬∑ delta cocone
        congr with U
        simp [D, relativeGluingData, restrictIsoSpec]
      ¬∑ simp
    convert inferInstanceAs (IsIso e.hom)
    rw [‚Üê cancel_mono U.1.Œπ, ‚Üê Iso.inv_comp_eq]
    simp [e, ‚Üê pullback.condition, IsAffineOpen.isoSpec_hom]
  .ofPointIso (colimit.isColimit F)

end PreservesLocalization

end Scheme.AffineZariskiSite

end AlgebraicGeometry
