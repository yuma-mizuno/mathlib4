/-
Copyright (c) 2018 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Johannes HÃ¶lzl, Reid Barton, Sean Leather, Yury Kudryashov, Anne Baanen, Dagur Asgeirsson
-/
module

public import Mathlib.CategoryTheory.ObjectProperty.FullSubcategory

/-!
# Concrete categories

A concrete category is a category `C` where the objects and morphisms correspond with types and
(bundled) functions between these types. We define concrete categories using
`class ConcreteCategory`. To convert an object to a type, write `ToType`. To convert a morphism
to a (bundled) function, write `hom`.

Each concrete category `C` comes with a canonical faithful functor `forget C : C â¥¤ Type*`,
see the file `Mathlib.CategoryTheory.ConcreteCategory.Forget`

## Implementation notes

We do not use `CoeSort` to convert objects in a concrete category to types, since this would lead
to elaboration mismatches between results taking a `[ConcreteCategory C]` instance and specific
types `C` that hold a `ConcreteCategory C` instance: the first gets a literal `CoeSort.coe` and
the second gets unfolded to the actual `coe` field.

`ToType` and `ToHom` are `abbrev`s so that we do not need to copy over instances such as `Ring`
or `RingHomClass` respectively.

## References

See [Ahrens and Lumsdaine, *Displayed Categories*][ahrens2017] for
related work.
-/

@[expose] public section


assert_not_exists CategoryTheory.CommSq CategoryTheory.Adjunction

universe w w' v v' v'' u u' u''

namespace CategoryTheory

section ConcreteCategory

/-- A concrete category is a category `C` where objects correspond to types and morphisms to
(bundled) functions between those types.

In other words, it has a fixed faithful functor `forget : C â¥¤ Type`.

Note that `ConcreteCategory` potentially depends on three independent universe levels,
* the universe level `w` appearing in `forget : C â¥¤ Type w`
* the universe level `v` of the morphisms (i.e. we have a `Category.{v} C`)
* the universe level `u` of the objects (i.e `C : Type u`)
They are specified that order, to avoid unnecessary universe annotations.
-/
class ConcreteCategory (C : Type u) [Category.{v} C]
    (FC : outParam <| C â†’ C â†’ Type*) {CC : outParam <| C â†’ Type w}
    [outParam <| âˆ€ X Y, FunLike (FC X Y) (CC X) (CC Y)] where
  /-- Convert a morphism of `C` to a bundled function. -/
  (hom : âˆ€ {X Y}, (X âŸ¶ Y) â†’ FC X Y)
  /-- Convert a bundled function to a morphism of `C`. -/
  (ofHom : âˆ€ {X Y}, FC X Y â†’ (X âŸ¶ Y))
  (hom_ofHom : âˆ€ {X Y} (f : FC X Y), hom (ofHom f) = f := by cat_disch)
  (ofHom_hom : âˆ€ {X Y} (f : X âŸ¶ Y), ofHom (hom f) = f := by cat_disch)
  (id_apply : âˆ€ {X} (x : CC X), hom (ðŸ™ X) x = x := by cat_disch)
  (comp_apply : âˆ€ {X Y Z} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : CC X),
    hom (f â‰« g) x = hom g (hom f x) := by cat_disch)

export ConcreteCategory (id_apply comp_apply)

variable {C : Type u} [Category.{v} C] {FC : C â†’ C â†’ Type*} {CC : C â†’ Type w}
variable [âˆ€ X Y, FunLike (FC X Y) (CC X) (CC Y)]

/-- `ToType X` converts the object `X` of the concrete category `C` to a type.

This is an `abbrev` so that instances on `X` (e.g. `Ring`) do not need to be redeclared.
-/
@[nolint unusedArguments] -- Need the instance to trigger unification that finds `CC`.
abbrev ToType [ConcreteCategory C FC] := CC

/-- `ToHom X Y` is the type of (bundled) functions between objects `X Y : C`.

This is an `abbrev` so that instances (e.g. `RingHomClass`) do not need to be redeclared.
-/
@[nolint unusedArguments] -- Need the instance to trigger unification that finds `FC`.
abbrev ToHom [ConcreteCategory C FC] := FC

variable [ConcreteCategory C FC]

namespace ConcreteCategory

/-- We can apply morphisms of concrete categories by first casting them down
to the base functions.
-/
instance {X Y : C} : CoeFun (X âŸ¶ Y) (fun _ â†¦ ToType X â†’ ToType Y) where
  coe f := hom f

/-- A non-instance `FunLike` instance on `X âŸ¶ Y`. -/
abbrev _root_.CategoryTheory.HasForget.instFunLike {X Y : C} :
    FunLike (X âŸ¶ Y) (ToType X) (ToType Y) where
  coe f := f
  coe_injective' f g h := by
    rw [â† ofHom_hom f, â† ofHom_hom g]
    simp_all

/--
`ConcreteCategory.hom` bundled as an `Equiv`.
-/
def homEquiv {X Y : C} : (X âŸ¶ Y) â‰ƒ ToHom X Y where
  toFun := hom
  invFun := ofHom
  left_inv := ofHom_hom
  right_inv := hom_ofHom

lemma hom_bijective {X Y : C} : Function.Bijective (hom : (X âŸ¶ Y) â†’ ToHom X Y) :=
  homEquiv.bijective

lemma hom_injective {X Y : C} : Function.Injective (hom : (X âŸ¶ Y) â†’ ToHom X Y) :=
  hom_bijective.injective

/-- In any concrete category, we can test equality of morphisms by pointwise evaluations. -/
@[ext] lemma ext {X Y : C} {f g : X âŸ¶ Y} (h : hom f = hom g) : f = g :=
  hom_injective h

lemma coe_ext {X Y : C} {f g : X âŸ¶ Y} (h : â‡‘(hom f) = â‡‘(hom g)) : f = g :=
  ext (DFunLike.coe_injective h)

lemma ext_apply {X Y : C} {f g : X âŸ¶ Y} (h : âˆ€ x, f x = g x) : f = g :=
  ext (DFunLike.ext _ _ h)

/-- In any concrete category, we can test equality of morphisms by pointwise evaluations. -/
@[ext low]
theorem hom_ext {X Y : C} (f g : X âŸ¶ Y) (w : âˆ€ x : ToType X, f x = g x) : f = g := by
  apply ConcreteCategory.ext_apply
  exact w

/-- Analogue of `congr_fun h x`,
when `h : f = g` is an equality between morphisms in a concrete category.
-/
theorem congr_hom {X Y : C} {f g : X âŸ¶ Y} (h : f = g) (x : ToType X) : f x = g x :=
  congrFun (congrArg (fun k : X âŸ¶ Y => (k : ToType X â†’ ToType Y)) h) x

theorem coe_id {X : C} : (ðŸ™ X : ToType X â†’ ToType X) = id := by
  ext
  simp [ConcreteCategory.id_apply]

theorem coe_comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : (f â‰« g : ToType X â†’ ToType Z) = g âˆ˜ f := by
  ext
  simp [ConcreteCategory.comp_apply]

@[simp] theorem _root_.CategoryTheory.id_apply {X : C} (x : ToType X) :
    (ðŸ™ X : ToType X â†’ ToType X) x = x := by
  simp [ConcreteCategory.id_apply]

@[simp] theorem _root_.CategoryTheory.comp_apply {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
    (x : ToType X) : (f â‰« g) x = g (f x) := by
  simp [ConcreteCategory.comp_apply]

@[deprecated (since := "2026-02-06")] alias _root_.CategoryTheory.comp_apply' :=
  _root_.CategoryTheory.comp_apply

theorem congr_arg {X Y : C} (f : X âŸ¶ Y) {x x' : ToType X} (h : x = x') : f x = f x' :=
  congrArg (f : ToType X â†’ ToType Y) h

end ConcreteCategory

theorem hom_id {X : C} : (ðŸ™ X : ToType X â†’ ToType X) = id := by
  ext
  simp

theorem hom_comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : (f â‰« g : ToType X â†’ ToType Z) = g âˆ˜ f := by
  ext
  simp

open ConcreteCategory

instance InducedCategory.concreteCategory {C : Type u} {D : Type u'} [Category.{v'} D]
    {FD : D â†’ D â†’ Type*} {CD : D â†’ Type w} [âˆ€ X Y, FunLike (FD X Y) (CD X) (CD Y)]
    [ConcreteCategory.{w} D FD] (f : C â†’ D) :
    ConcreteCategory (InducedCategory D f) (fun X Y => FD (f X) (f Y)) where
  hom f := hom f.hom
  ofHom g := homMk (ofHom g)
  hom_ofHom _ := hom_ofHom _
  ofHom_hom _ := by ext; simp [ofHom_hom]
  comp_apply _ _ _ := ConcreteCategory.comp_apply _ _ _
  id_apply _ := ConcreteCategory.id_apply _

open ObjectProperty in
instance FullSubcategory.concreteCategory {C : Type u} [Category.{v} C]
    {FC : C â†’ C â†’ Type*} {CC : C â†’ Type w} [âˆ€ X Y, FunLike (FC X Y) (CC X) (CC Y)]
    [ConcreteCategory.{w} C FC]
    (P : ObjectProperty C) : ConcreteCategory P.FullSubcategory (fun X Y => FC X.1 Y.1) where
  hom f := hom f.hom
  ofHom g := homMk (ofHom g)
  hom_ofHom _ := hom_ofHom _
  ofHom_hom _ := by ext; simp [ofHom_hom]
  comp_apply _ _ _ := ConcreteCategory.comp_apply _ _ _
  id_apply _ := ConcreteCategory.id_apply _

end ConcreteCategory

end CategoryTheory
