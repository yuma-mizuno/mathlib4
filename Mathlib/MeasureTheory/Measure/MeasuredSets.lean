/-
Copyright (c) 2026 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
module

public import Mathlib.MeasureTheory.Measure.Typeclasses.Finite
public import Mathlib.MeasureTheory.SetSemiring
import Mathlib.Topology.MetricSpace.Lipschitz

/-!
# Measured sets

Consider a measure `Œº` on a measurable space. One can define an extended distance on the space
of measurable sets, by `edist s t := Œº (s ‚àÜ t)`. In this file, we introduce this definition
on the type synonym `MeasuredSets Œº`, and we prove that `Œº` is a continuous function on this space.

We also give a density criterion for this distance,
in `exists_measure_symmDiff_lt_of_generateFrom_isSetRing`: given a ring of sets `C` covering the
space modulo `0` and generating the measurable space structure, then any measurable set can be
approximated by elements of `C`.
Note that the covering condition is necessary: for a counterexample otherwise, take `{0, 1}` with
the counting measure and `C = {‚àÖ, {0}}`. Then the set `{1}` can not be approximated by
an element of `C`.
-/

@[expose] public section

open MeasurableSpace Set Filter
open scoped symmDiff ENNReal Topology

namespace MeasureTheory

variable {Œ± : Type*} [mŒ± : MeasurableSpace Œ±] {Œº : Measure Œ±}

set_option linter.unusedVariables false in
/-- The subtype of all measurable sets. We denote it as `MeasuredSets Œº`, with an explicit but
unused parameter `Œº`, to be able to define a distance on it given by `edist s t = Œº (s ‚àÜ t)` -/
@[nolint unusedArguments]
def MeasuredSets (Œº : Measure Œ±) : Type _ := {s : Set Œ± // MeasurableSet s}

instance : SetLike (MeasuredSets Œº) Œ± where
  coe s := s.1
  coe_injective' := Subtype.coe_injective

instance : PseudoEMetricSpace (MeasuredSets Œº) where
  edist s t := Œº ((s : Set Œ±) ‚àÜ t)
  edist_self := by simp
  edist_comm := by grind
  edist_triangle s t u := measure_symmDiff_le _ _ _

lemma MeasuredSets.edist_def (s t : MeasuredSets Œº) : edist s t = Œº ((s : Set Œ±) ‚àÜ t) := rfl

/-- Measure on `MeasuredSets` is a 1-lipschitz function.

We cannot state this in terms of `LipschitzWith`, because `‚Ñù‚â•0‚àû` is not a `PseudoEMetricSpace`. -/
lemma MeasuredSets.sub_le_edist (s t : MeasuredSets Œº) : Œº s - Œº t ‚â§ edist s t :=
  le_measure_diff.trans <| measure_mono subset_union_left

lemma MeasuredSets.continuous_measure : Continuous (fun (s : MeasuredSets Œº) ‚Ü¶ Œº s) := by
  refine continuous_of_le_add_edist 1 ENNReal.one_ne_top fun s t ‚Ü¶ ?_
  rw [one_mul, ‚Üê tsub_le_iff_left]
  exact sub_le_edist s t

instance [IsFiniteMeasure Œº] : PseudoMetricSpace (MeasuredSets Œº) :=
  PseudoEMetricSpace.toPseudoMetricSpaceOfDist
    (fun s t ‚Ü¶ Œº.real ((s : Set Œ±) ‚àÜ t)) (fun s t ‚Ü¶ ENNReal.toReal_nonneg)
    (fun s t ‚Ü¶ by simp [Measure.real, MeasuredSets.edist_def])

lemma MeasuredSets.dist_def [IsFiniteMeasure Œº] (s t : MeasuredSets Œº) :
    dist s t = Œº.real ((s : Set Œ±) ‚àÜ t) := rfl

lemma MeasuredSets.real_sub_real_le_dist [IsFiniteMeasure Œº] (s t : MeasuredSets Œº) :
    Œº.real s - Œº.real t ‚â§ dist s t := by
  grw [dist_edist, ‚Üê sub_le_edist]
  exacts [ENNReal.le_toReal_sub (measure_ne_top _ _), edist_ne_top _ _]

lemma MeasuredSets.lipschitzWith_measureReal [IsFiniteMeasure Œº] :
    LipschitzWith 1 (fun s : MeasuredSets Œº ‚Ü¶ Œº.real s) :=
  .of_le_add fun s t ‚Ü¶ sub_le_iff_le_add'.mp <| real_sub_real_le_dist s t

/-- Given a ring of sets `C` covering the space modulo `0` and generating the measurable space
structure, any measurable set can be approximated by elements of `C`. -/
lemma exists_measure_symmDiff_lt_of_generateFrom_isSetRing [IsFiniteMeasure Œº]
    {C : Set (Set Œ±)} (hC : IsSetRing C)
    (h'C : ‚àÉ D : Set (Set Œ±), D.Countable ‚àß D ‚äÜ C ‚àß Œº (‚ãÉ‚ÇÄ D)·∂ú = 0) (h : mŒ± = generateFrom C)
    {s : Set Œ±} (hs : MeasurableSet s) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : 0 < Œµ) :
    ‚àÉ t ‚àà C, Œº (t ‚àÜ s) < Œµ := by
  /- We check that the set of sets satisfying the conclusion of the lemma for all positive
  `Œµ` contains `C` and is stable under complement and disjoint union. It follows that it is
  all the sigma-algebra, as desired. -/
  apply MeasurableSpace.induction_on_inter (C := fun s hs ‚Ü¶ ‚àÄ (Œµ : ‚Ñù‚â•0‚àû) (hŒµ : 0 < Œµ),
    ‚àÉ t ‚àà C, Œº (t ‚àÜ s) < Œµ) h hC.isSetSemiring.isPiSystem ?_ ?_ ?_ ?_ s hs Œµ hŒµ
  ¬∑ intro Œµ Œµpos
    exact ‚ü®‚àÖ, hC.empty_mem, by simp [Œµpos]‚ü©
  ¬∑ intro s hs Œµ Œµpos
    exact ‚ü®s, hs, by simp [Œµpos]‚ü©
  ¬∑ /- To check the stability under complement, we use the condition `h'C` which guarantees
    that the space is almost an element of `C`. If `t` approximates `s`, then `univ \ t`
    approximates well `s·∂ú`, and therefore `t' \ t` approximates well `s·∂ú` when `t'` is a good
    enough approximation to `univ`. As `t' \ t` belongs to `C` when `t` and `t'` do, this
    concludes this step. -/
    intro s hs h's Œµ Œµpos
    obtain ‚ü®t, tC, ht‚ü© : ‚àÉ t ‚àà C, Œº (t ‚àÜ s) < Œµ / 2 := h's _ (ENNReal.half_pos Œµpos.ne')
    obtain ‚ü®t', t'C, ht'‚ü© : ‚àÉ t' ‚àà C, Œº (t'·∂ú) < Œµ / 2 := by
      obtain ‚ü®D, D_count, DC, hD, Dne‚ü© :
          ‚àÉ D : Set (Set Œ±), D.Countable ‚àß D ‚äÜ C ‚àß Œº (‚ãÉ‚ÇÄ D)·∂ú = 0 ‚àß D.Nonempty := by
        rcases h'C with ‚ü®D, D_count, DC, hD‚ü©
        refine ‚ü®D ‚à™ {‚àÖ}, D_count.union (by simp), ?_‚ü©
        simp only [union_subset_iff, DC, singleton_subset_iff, true_and, and_true, hC.empty_mem]
        simp only [union_singleton, sUnion_insert, empty_union, insert_nonempty, and_true, hD]
      obtain ‚ü®f, hf‚ü© : ‚àÉ f : ‚Ñï ‚Üí Set Œ±, D = Set.range f := Set.Countable.exists_eq_range D_count Dne
      have fC n : Set.accumulate f n ‚àà C := hC.accumulate_mem (fun n ‚Ü¶ DC (by simp [hf])) n
      have : Tendsto (fun n ‚Ü¶ Œº (Set.accumulate f n)·∂ú) atTop (ùìù 0) := by
        have : ‚ãÉ‚ÇÄ D = ‚ãÉ n, Set.accumulate f n := by simp [hf, iUnion_accumulate]
        rw [show (‚ãÉ‚ÇÄ D)·∂ú = ‚ãÇ n, (Set.accumulate f n)·∂ú by simp [this]] at hD
        rw [‚Üê hD]
        apply tendsto_measure_iInter_atTop (fun i ‚Ü¶ ?_)
          (fun i j hij ‚Ü¶ by simpa using monotone_accumulate hij) ‚ü®0, by simp‚ü©
        apply MeasurableSet.nullMeasurableSet
        rw [h]
        exact (measurableSet_generateFrom (fC i)).compl
      obtain ‚ü®n, hn‚ü© : ‚àÉ n, Œº (accumulate f n)·∂ú < Œµ / 2 :=
        ((tendsto_order.1 this).2 _ (ENNReal.half_pos Œµpos.ne')).exists
      exact ‚ü®accumulate f n, fC n, hn‚ü©
    refine ‚ü®t' \ t, hC.diff_mem t'C tC, ?_‚ü©
    calc Œº ((t' \ t) ‚àÜ s·∂ú)
      _ ‚â§ Œº (t ‚àÜ s ‚à™ t'·∂ú) := by gcongr; grind
      _ ‚â§ Œº (t ‚àÜ s) + Œº (t'·∂ú) := measure_union_le _ _
      _ < Œµ / 2 + Œµ / 2 := by gcongr
      _ = Œµ := ENNReal.add_halves Œµ
  ¬∑ /- To check the stability under disjoint union, approximate `f n` by a set `t n ‚àà C`. Then
    `‚ãÉ i, f i` is well approximated by `U i < n, f i` for large enough `n`, which is itself
    well approximated by `‚ãÉ i < n, t i`. As this set belongs to `C`, this concludes this step. -/
    intro f f_disj f_meas hf Œµ Œµpos
    rcases ENNReal.exists_pos_sum_of_countable' (ENNReal.half_pos Œµpos.ne').ne' ‚Ñï with ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü©
    have A i : ‚àÉ t ‚àà C, Œº (t ‚àÜ (f i)) < Œ¥ i := hf i _ (Œ¥pos i)
    choose! t tC ht using A
    have : Tendsto (fun n ‚Ü¶ Œº (‚ãÉ i ‚àà Ici n, f i)) atTop (ùìù 0) :=
      tendsto_measure_biUnion_Ici_zero_of_pairwise_disjoint
        (fun i ‚Ü¶ (f_meas i).nullMeasurableSet) f_disj
    obtain ‚ü®n, hn‚ü© : ‚àÉ n, Œº (‚ãÉ i ‚àà Ici n, f i) < Œµ / 2 :=
      ((tendsto_order.1 this).2 _ (ENNReal.half_pos Œµpos.ne')).exists
    refine ‚ü®‚ãÉ i ‚àà Finset.range n, t i, hC.biUnion_mem _ (fun i hi ‚Ü¶ tC _), ?_‚ü©
    calc Œº ((‚ãÉ i ‚àà Finset.range n, t i) ‚àÜ (‚ãÉ i, f i))
    _ ‚â§ Œº ((‚ãÉ i ‚àà Finset.range n, (t i) ‚àÜ (f i)) ‚à™ ‚ãÉ i ‚àà Ici n, f i) := by
      gcongr
      intro x hx
      simp only [Finset.mem_range, mem_symmDiff, mem_iUnion, exists_prop, not_exists, not_and,
        mem_Ici, mem_union] at hx ‚ä¢
      grind
    _ ‚â§ ‚àë i ‚àà Finset.range n, Œº (t i ‚àÜ f i) + Œº (‚ãÉ i ‚àà Ici n, f i) := by
      apply (measure_union_le _ _).trans
      gcongr
      apply measure_biUnion_finset_le
    _ ‚â§ ‚àë i ‚àà Finset.range n, Œ¥ i + Œº (‚ãÉ i ‚àà Ici n, f i) := by
      gcongr with i; exact (ht i).le
    _ ‚â§ ‚àë' i, Œ¥ i + Œº (‚ãÉ i ‚àà Ici n, f i) := by
      gcongr; exact ENNReal.sum_le_tsum (Finset.range n)
    _ < Œµ / 2 + Œµ / 2 := by gcongr
    _ = Œµ :=  ENNReal.add_halves Œµ

/-- Given a semiring of sets `C` covering the space modulo `0` and generating the measurable space
structure, any measurable set can be approximated by finite unions of elements of `C`. -/
lemma exists_measure_symmDiff_lt_of_generateFrom_isSetSemiring [IsFiniteMeasure Œº]
    {C : Set (Set Œ±)} (hC : IsSetSemiring C)
    (h'C : ‚àÉ D : Set (Set Œ±), D.Countable ‚àß D ‚äÜ C ‚àß Œº (‚ãÉ‚ÇÄ D)·∂ú = 0) (h : mŒ± = generateFrom C)
    {s : Set Œ±} (hs : MeasurableSet s) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : 0 < Œµ) :
    ‚àÉ t ‚àà supClosure C, Œº (t ‚àÜ s) < Œµ := by
  apply exists_measure_symmDiff_lt_of_generateFrom_isSetRing hC.isSetRing_supClosure ?_ ?_ hs hŒµ
  ¬∑ rcases h'C with ‚ü®D, D_count, DC, hD‚ü©
    exact ‚ü®D, D_count, DC.trans subset_supClosure, hD‚ü©
  ¬∑ rw [h]
    apply le_antisymm (generateFrom_mono subset_supClosure)
    apply generateFrom_le (fun t ht ‚Ü¶ ?_)
    apply measurableSet_generateFrom_of_mem_supClosure ht

/-- A ring of sets covering the space modulo `0` and generating the measurable space
structure is dense among measurable sets. -/
lemma dense_of_generateFrom_isSetRing [IsFiniteMeasure Œº]
    {C : Set (Set Œ±)} (hC : IsSetRing C)
    (h'C : ‚àÉ D : Set (Set Œ±), D.Countable ‚àß D ‚äÜ C ‚àß Œº (‚ãÉ‚ÇÄ D)·∂ú = 0) (h : mŒ± = generateFrom C) :
    Dense ((SetLike.coe : MeasuredSets Œº ‚Üí Set Œ±) ‚Åª¬π' C) := by
  rw [EMetric.dense_iff]
  rintro s Œµ Œµpos
  rcases exists_measure_symmDiff_lt_of_generateFrom_isSetRing hC h'C h s.2 Œµpos with ‚ü®t, tC, ht‚ü©
  have t_meas : MeasurableSet t := by rw [h]; exact measurableSet_generateFrom tC
  refine ‚ü®‚ü®t, t_meas‚ü©, ?_, tC‚ü©
  simpa [MeasuredSets.edist_def] using ht

/-- Given a semiring of sets `C` covering the space modulo `0` and generating the measurable space
structure, finite unions of elements of `C` are dense among measurable sets. -/
lemma dense_of_generateFrom_isSetSemiring [IsFiniteMeasure Œº]
    {C : Set (Set Œ±)} (hC : IsSetSemiring C)
    (h'C : ‚àÉ D : Set (Set Œ±), D.Countable ‚àß D ‚äÜ C ‚àß Œº (‚ãÉ‚ÇÄ D)·∂ú = 0) (h : mŒ± = generateFrom C) :
    Dense ((SetLike.coe : MeasuredSets Œº ‚Üí Set Œ±) ‚Åª¬π' (supClosure C)) := by
  rw [EMetric.dense_iff]
  rintro s Œµ Œµpos
  rcases exists_measure_symmDiff_lt_of_generateFrom_isSetSemiring hC h'C h s.2 Œµpos
    with ‚ü®t, tC, ht‚ü©
  refine ‚ü®‚ü®t, ?_‚ü©, by simpa [MeasuredSets.edist_def] using ht, tC‚ü©
  rw [h]
  exact measurableSet_generateFrom_of_mem_supClosure tC

end MeasureTheory
