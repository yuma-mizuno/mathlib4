/-
Copyright (c) 2020 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, S√©bastien Gou√´zel
-/
module

public import Mathlib.MeasureTheory.Function.LpSeminorm.Basic

/-!
# Monotonicity and ‚Ñíp seminorms
-/

public noncomputable section

open TopologicalSpace MeasureTheory Filter

open scoped NNReal ENNReal ComplexConjugate

variable {Œ± E F G : Type*} {m : MeasurableSpace Œ±} {p : ‚Ñù‚â•0‚àû} {q : ‚Ñù} {Œº : Measure Œ±}
  [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]

namespace MeasureTheory

section Monotonicity

variable {Œµ Œµ' : Type*} [TopologicalSpace Œµ] [ContinuousENorm Œµ]
  [TopologicalSpace Œµ'] [ContinuousENorm Œµ']

theorem eLpNorm'_le_nnreal_smul_eLpNorm'_of_ae_le_mul {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} {c : ‚Ñù‚â•0}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ c * ‚Äñg x‚Äñ‚Çä) {p : ‚Ñù} (hp : 0 < p) :
    eLpNorm' f p Œº ‚â§ c ‚Ä¢ eLpNorm' g p Œº := by
  simp_rw [eLpNorm'_eq_lintegral_enorm]
  rw [‚Üê ENNReal.rpow_le_rpow_iff hp, ENNReal.smul_def, smul_eq_mul,
    ENNReal.mul_rpow_of_nonneg _ _ hp.le]
  simp_rw [‚Üê ENNReal.rpow_mul, one_div, inv_mul_cancel‚ÇÄ hp.ne.symm, ENNReal.rpow_one, enorm,
    ‚Üê ENNReal.coe_rpow_of_nonneg _ hp.le, ‚Üê lintegral_const_mul' _ _ ENNReal.coe_ne_top,
    ‚Üê ENNReal.coe_mul]
  apply lintegral_mono_ae
  simp_rw [ENNReal.coe_le_coe, ‚Üê NNReal.mul_rpow, NNReal.rpow_le_rpow_iff hp]
  exact h

-- TODO: eventually, deprecate and remove the nnnorm version
theorem eLpNorm'_le_nnreal_smul_eLpNorm'_of_ae_le_mul' {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí Œµ'} {c : ‚Ñù‚â•0}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë) {p : ‚Ñù} (hp : 0 < p) :
    eLpNorm' f p Œº ‚â§ c ‚Ä¢ eLpNorm' g p Œº := by
  simp_rw [eLpNorm'_eq_lintegral_enorm]
  rw [‚Üê ENNReal.rpow_le_rpow_iff hp, ENNReal.smul_def, smul_eq_mul,
    ENNReal.mul_rpow_of_nonneg _ _ hp.le]
  simp_rw [‚Üê ENNReal.rpow_mul, one_div, inv_mul_cancel‚ÇÄ hp.ne', ENNReal.rpow_one,
    ‚Üê ENNReal.coe_rpow_of_nonneg _ hp.le, ‚Üê lintegral_const_mul' _ _ ENNReal.coe_ne_top]
  apply lintegral_mono_ae
  have aux (x) : (‚Üëc) ^ p * ‚Äñg x‚Äñ‚Çë ^ p = (‚Üëc * ‚Äñg x‚Äñ‚Çë) ^ p := by
    have : ¬¨(p < 0) := by linarith
    simp [ENNReal.mul_rpow_eq_ite, this]
  simpa [ENNReal.coe_rpow_of_nonneg _ hp.le, aux, ENNReal.rpow_le_rpow_iff hp]

section ESeminormedAddMonoid

variable {Œµ : Type*} [TopologicalSpace Œµ] [ESeminormedAddMonoid Œµ]

/-- If `‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë` a.e., `eLpNorm' f p Œº ‚â§ c * eLpNorm' g p Œº` for all `p ‚àà (0, ‚àû)`. -/
theorem eLpNorm'_le_mul_eLpNorm'_of_ae_le_mul {f : Œ± ‚Üí Œµ} {c : ‚Ñù‚â•0‚àû} {g : Œ± ‚Üí Œµ'} {p : ‚Ñù}
    (hg : AEStronglyMeasurable g Œº) (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë) (hp : 0 < p) :
    eLpNorm' f p Œº ‚â§ c * eLpNorm' g p Œº := by
  have hp' : ¬¨(p < 0) := by linarith
  by_cases hc : c = ‚ä§
  ¬∑ by_cases hg' : eLpNorm' g p Œº = 0
    ¬∑ have : ‚àÄ·µê (x : Œ±) ‚àÇŒº, ‚Äñg x‚Äñ‚Çë = 0 := by
        simp only [eLpNorm'_eq_lintegral_enorm, one_div, ENNReal.rpow_eq_zero_iff, inv_pos, hp,
          and_true, inv_neg'', hp', and_false, or_false] at hg'
        rw [MeasureTheory.lintegral_eq_zero_iff' (by fun_prop)] at hg'
        exact hg'.mono fun x hx ‚Ü¶ by simpa [hp, hp'] using hx
      have : ‚àÄ·µê (x : Œ±) ‚àÇŒº, ‚Äñf x‚Äñ‚Çë = 0 := (this.and h).mono fun x ‚ü®h, h'‚ü© ‚Ü¶ by simp_all
      simpa only [hg', mul_zero, nonpos_iff_eq_zero] using eLpNorm'_eq_zero_of_ae_eq_zero hp this
    ¬∑ simp_all
  have : c ^ p ‚â† ‚ä§ := by simp [hp.le, hc]
  simp_rw [eLpNorm'_eq_lintegral_enorm]
  rw [‚Üê ENNReal.rpow_le_rpow_iff hp, ENNReal.mul_rpow_of_nonneg _ _ hp.le]
  simp_rw [‚Üê ENNReal.rpow_mul, one_div, inv_mul_cancel‚ÇÄ hp.ne', ENNReal.rpow_one,
    ‚Üê lintegral_const_mul' _ _ this]
  apply lintegral_mono_ae
  have aux (x) : (‚Üëc) ^ p * ‚Äñg x‚Äñ‚Çë ^ p = (‚Üëc * ‚Äñg x‚Äñ‚Çë) ^ p := by
    simp [ENNReal.mul_rpow_eq_ite, hp']
  simpa [ENNReal.coe_rpow_of_nonneg _ hp.le, aux, ENNReal.rpow_le_rpow_iff hp]

end ESeminormedAddMonoid

-- TODO: eventually, deprecate and remove the nnnorm version
theorem eLpNormEssSup_le_nnreal_smul_eLpNormEssSup_of_ae_le_mul' {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí Œµ'} {c : ‚Ñù‚â•0‚àû}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë) : eLpNormEssSup f Œº ‚â§ c ‚Ä¢ eLpNormEssSup g Œº :=
  calc
    essSup (‚Äñf ¬∑‚Äñ‚Çë) Œº ‚â§ essSup (c * ‚Äñg ¬∑‚Äñ‚Çë) Œº := essSup_mono_ae <| h
    _ = c ‚Ä¢ essSup (‚Äñg ¬∑‚Äñ‚Çë) Œº := ENNReal.essSup_const_mul

theorem eLpNormEssSup_le_nnreal_smul_eLpNormEssSup_of_ae_le_mul {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} {c : ‚Ñù‚â•0}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ c * ‚Äñg x‚Äñ‚Çä) : eLpNormEssSup f Œº ‚â§ c ‚Ä¢ eLpNormEssSup g Œº :=
  calc
    essSup (‚Äñf ¬∑‚Äñ‚Çë) Œº ‚â§ essSup (fun x => (‚Üë(c * ‚Äñg x‚Äñ‚Çä) : ‚Ñù‚â•0‚àû)) Œº :=
      essSup_mono_ae <| h.mono fun _ hx => ENNReal.coe_le_coe.mpr hx
    _ = essSup (c * ‚Äñg ¬∑‚Äñ‚Çë) Œº := by simp_rw [ENNReal.coe_mul, enorm]
    _ = c ‚Ä¢ essSup (‚Äñg ¬∑‚Äñ‚Çë) Œº := ENNReal.essSup_const_mul

-- TODO: eventually, deprecate and remove the nnnorm version
theorem eLpNorm_le_nnreal_smul_eLpNorm_of_ae_le_mul' {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí Œµ'} {c : ‚Ñù‚â•0}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë) (p : ‚Ñù‚â•0‚àû) : eLpNorm f p Œº ‚â§ c ‚Ä¢ eLpNorm g p Œº := by
  by_cases h0 : p = 0
  ¬∑ simp [h0]
  by_cases h_top : p = ‚àû
  ¬∑ rw [h_top]
    exact eLpNormEssSup_le_nnreal_smul_eLpNormEssSup_of_ae_le_mul' h
  simp_rw [eLpNorm_eq_eLpNorm' h0 h_top]
  exact eLpNorm'_le_nnreal_smul_eLpNorm'_of_ae_le_mul' h (ENNReal.toReal_pos h0 h_top)

theorem eLpNorm_le_nnreal_smul_eLpNorm_of_ae_le_mul {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} {c : ‚Ñù‚â•0}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ c * ‚Äñg x‚Äñ‚Çä) (p : ‚Ñù‚â•0‚àû) : eLpNorm f p Œº ‚â§ c ‚Ä¢ eLpNorm g p Œº := by
  by_cases h0 : p = 0
  ¬∑ simp [h0]
  by_cases h_top : p = ‚àû
  ¬∑ rw [h_top]
    exact eLpNormEssSup_le_nnreal_smul_eLpNormEssSup_of_ae_le_mul h
  simp_rw [eLpNorm_eq_eLpNorm' h0 h_top]
  exact eLpNorm'_le_nnreal_smul_eLpNorm'_of_ae_le_mul h (ENNReal.toReal_pos h0 h_top)

-- TODO: add the whole family of lemmas?
private theorem le_mul_iff_eq_zero_of_nonneg_of_neg_of_nonneg {Œ±}
    [Semiring Œ±] [LinearOrder Œ±] [IsStrictOrderedRing Œ±]
    {a b c : Œ±} (ha : 0 ‚â§ a) (hb : b < 0) (hc : 0 ‚â§ c) : a ‚â§ b * c ‚Üî a = 0 ‚àß c = 0 := by
  constructor
  ¬∑ intro h
    exact
      ‚ü®(h.trans (mul_nonpos_of_nonpos_of_nonneg hb.le hc)).antisymm ha,
        (nonpos_of_mul_nonneg_right (ha.trans h) hb).antisymm hc‚ü©
  ¬∑ rintro ‚ü®rfl, rfl‚ü©
    rw [mul_zero]

/-- When `c` is negative, `‚Äñf x‚Äñ ‚â§ c * ‚Äñg x‚Äñ` is nonsense and forces both `f` and `g` to have an
`eLpNorm` of `0`. -/
theorem eLpNorm_eq_zero_and_zero_of_ae_le_mul_neg {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} {c : ‚Ñù}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c * ‚Äñg x‚Äñ) (hc : c < 0) (p : ‚Ñù‚â•0‚àû) :
    eLpNorm f p Œº = 0 ‚àß eLpNorm g p Œº = 0 := by
  simp_rw [le_mul_iff_eq_zero_of_nonneg_of_neg_of_nonneg (norm_nonneg _) hc (norm_nonneg _),
    norm_eq_zero, eventually_and] at h
  change f =·µê[Œº] 0 ‚àß g =·µê[Œº] 0 at h
  simp [eLpNorm_congr_ae h.1, eLpNorm_congr_ae h.2]

theorem eLpNorm_le_mul_eLpNorm_of_ae_le_mul {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} {c : ‚Ñù}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c * ‚Äñg x‚Äñ) (p : ‚Ñù‚â•0‚àû) :
    eLpNorm f p Œº ‚â§ ENNReal.ofReal c * eLpNorm g p Œº :=
  eLpNorm_le_nnreal_smul_eLpNorm_of_ae_le_mul
    (h.mono fun _x hx => hx.trans <| mul_le_mul_of_nonneg_right c.le_coe_toNNReal (norm_nonneg _)) _

-- TODO: eventually, deprecate and remove the nnnorm version
/-- If `‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë`, then `eLpNorm f p Œº ‚â§ c * eLpNorm g p Œº`.

This version assumes `c` is finite, but requires no measurability hypothesis on `g`. -/
theorem eLpNorm_le_mul_eLpNorm_of_ae_le_mul' {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí Œµ'} {c : ‚Ñù‚â•0}
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë) (p : ‚Ñù‚â•0‚àû) :
    eLpNorm f p Œº ‚â§ c * eLpNorm g p Œº := by
  apply eLpNorm_le_nnreal_smul_eLpNorm_of_ae_le_mul' h

variable {Œµ : Type*} [TopologicalSpace Œµ] [ESeminormedAddMonoid Œµ] in
/-- If `‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë`, then `eLpNorm f p Œº ‚â§ c * eLpNorm g p Œº`.

This version allows `c = ‚àû`, but requires `g` to be a.e. strongly measurable. -/
theorem eLpNorm_le_mul_eLpNorm_of_ae_le_mul'' {f : Œ± ‚Üí Œµ} {c : ‚Ñù‚â•0‚àû} {g : Œ± ‚Üí Œµ'} (p : ‚Ñù‚â•0‚àû)
    (hg : AEStronglyMeasurable g Œº) (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë) :
    eLpNorm f p Œº ‚â§ c * eLpNorm g p Œº := by
  by_cases h‚ÇÄ : p = 0
  ¬∑ simp [h‚ÇÄ]
  simp only [eLpNorm, h‚ÇÄ, ‚ÜìreduceIte, mul_ite]
  by_cases hp' : p = ‚ä§
  ¬∑ simpa [hp'] using eLpNormEssSup_le_nnreal_smul_eLpNormEssSup_of_ae_le_mul' h
  ¬∑ simpa [hp'] using eLpNorm'_le_mul_eLpNorm'_of_ae_le_mul hg h (ENNReal.toReal_pos h‚ÇÄ hp')

theorem MemLp.of_nnnorm_le_mul {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} {c : ‚Ñù‚â•0} (hg : MemLp g p Œº)
    (hf : AEStronglyMeasurable f Œº) (hfg : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ c * ‚Äñg x‚Äñ‚Çä) : MemLp f p Œº :=
  ‚ü®hf, (eLpNorm_le_nnreal_smul_eLpNorm_of_ae_le_mul hfg p).trans_lt <|
      ENNReal.mul_lt_top ENNReal.coe_lt_top (by finiteness)‚ü©

theorem MemLp.of_enorm_le_mul
    {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí Œµ'} {c : ‚Ñù‚â•0} (hg : MemLp g p Œº)
    (hf : AEStronglyMeasurable f Œº) (hfg : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë) : MemLp f p Œº :=
  ‚ü®hf, (eLpNorm_le_nnreal_smul_eLpNorm_of_ae_le_mul' hfg p).trans_lt <|
      ENNReal.mul_lt_top ENNReal.coe_lt_top (by finiteness)‚ü©

theorem MemLp.of_le_mul {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} {c : ‚Ñù} (hg : MemLp g p Œº)
    (hf : AEStronglyMeasurable f Œº) (hfg : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c * ‚Äñg x‚Äñ) : MemLp f p Œº :=
  ‚ü®hf,
    (eLpNorm_le_mul_eLpNorm_of_ae_le_mul hfg p).trans_lt <|
      ENNReal.mul_lt_top ENNReal.ofReal_lt_top (by finiteness)‚ü©

-- TODO: eventually, deprecate and remove the nnnorm version
theorem MemLp.of_le_mul' {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí Œµ'} {c : ‚Ñù‚â•0} (hg : MemLp g p Œº)
    (hf : AEStronglyMeasurable f Œº) (hfg : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çë ‚â§ c * ‚Äñg x‚Äñ‚Çë) : MemLp f p Œº :=
  ‚ü®hf, (eLpNorm_le_mul_eLpNorm_of_ae_le_mul' hfg p).trans_lt <|
      ENNReal.mul_lt_top ENNReal.coe_lt_top (by finiteness)‚ü©

end Monotonicity

theorem le_eLpNorm_of_bddBelow (hp : p ‚â† 0) (hp' : p ‚â† ‚àû) {f : Œ± ‚Üí F} (C : ‚Ñù‚â•0) {s : Set Œ±}
    (hs : MeasurableSet s) (hf : ‚àÄ·µê x ‚àÇŒº, x ‚àà s ‚Üí C ‚â§ ‚Äñf x‚Äñ‚Çä) :
    C ‚Ä¢ Œº s ^ (1 / p.toReal) ‚â§ eLpNorm f p Œº := by
  rw [ENNReal.smul_def, smul_eq_mul, eLpNorm_eq_lintegral_rpow_enorm hp hp',
    one_div, ENNReal.le_rpow_inv_iff (ENNReal.toReal_pos hp hp'),
    ENNReal.mul_rpow_of_nonneg _ _ ENNReal.toReal_nonneg, ‚Üê ENNReal.rpow_mul,
    inv_mul_cancel‚ÇÄ (ENNReal.toReal_pos hp hp').ne.symm, ENNReal.rpow_one, ‚Üê setLIntegral_const,
    ‚Üê lintegral_indicator hs]
  refine lintegral_mono_ae ?_
  filter_upwards [hf] with x hx
  by_cases hxs : x ‚àà s
  ¬∑ simp only [Set.indicator_of_mem, hxs, true_implies] at hx ‚ä¢
    gcongr
    rwa [coe_le_enorm]
  ¬∑ simp [Set.indicator_of_notMem hxs]

section Star

variable {R : Type*} [NormedAddCommGroup R] [StarAddMonoid R] [NormedStarGroup R]

@[simp]
theorem eLpNorm_star {p : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí R} : eLpNorm (star f) p Œº = eLpNorm f p Œº :=
  eLpNorm_congr_norm_ae <| .of_forall <| by simp

@[simp]
theorem AEEqFun.eLpNorm_star {p : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí‚Çò[Œº] R} : eLpNorm (star f : Œ± ‚Üí‚Çò[Œº] R) p Œº =
    eLpNorm f p Œº := eLpNorm_congr_ae (coeFn_star f) |>.trans <| by simp

protected theorem MemLp.star {p : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí R} (hf : MemLp f p Œº) : MemLp (star f) p Œº :=
  ‚ü®hf.1.star, by simpa using hf.2‚ü©

end Star

section RCLike

variable {ùïú : Type*} [RCLike ùïú] {f : Œ± ‚Üí ùïú}

@[simp] lemma eLpNorm_conj (f : Œ± ‚Üí ùïú) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    eLpNorm (conj f) p Œº = eLpNorm f p Œº := by simp [‚Üê eLpNorm_norm]

theorem MemLp.re (hf : MemLp f p Œº) : MemLp (fun x => RCLike.re (f x)) p Œº := by
  have : ‚àÄ x, ‚ÄñRCLike.re (f x)‚Äñ ‚â§ 1 * ‚Äñf x‚Äñ := by
    intro x
    rw [one_mul]
    exact RCLike.norm_re_le_norm (f x)
  refine hf.of_le_mul ?_ (Eventually.of_forall this)
  exact RCLike.continuous_re.comp_aestronglyMeasurable hf.1

theorem MemLp.im (hf : MemLp f p Œº) : MemLp (fun x => RCLike.im (f x)) p Œº := by
  have : ‚àÄ x, ‚ÄñRCLike.im (f x)‚Äñ ‚â§ 1 * ‚Äñf x‚Äñ := by
    intro x
    rw [one_mul]
    exact RCLike.norm_im_le_norm (f x)
  refine hf.of_le_mul ?_ (Eventually.of_forall this)
  exact RCLike.continuous_im.comp_aestronglyMeasurable hf.1

end RCLike

end MeasureTheory
