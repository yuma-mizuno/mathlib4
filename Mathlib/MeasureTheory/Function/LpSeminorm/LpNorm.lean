/-
Copyright (c) 2024 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
module

public import Mathlib.MeasureTheory.Integral.Bochner.Basic

import Mathlib.Analysis.RCLike.Lemmas
import Mathlib.Tactic.Positivity.Finset

/-!
# Real-valued L·µñ norm

This file proves theorems about `MeasureTheory.lpNorm`,
a real-valued version of `MeasureTheory.eLpNorm`.
-/

open Filter
open scoped BigOperators ComplexConjugate ENNReal NNReal

public section

namespace MeasureTheory
variable {Œ± E : Type*} {m : MeasurableSpace Œ±} {p : ‚Ñù‚â•0‚àû} {q : ‚Ñù} {Œº ŒΩ : Measure Œ±}
  [NormedAddCommGroup E] {f g h : Œ± ‚Üí E}

lemma toReal_eLpNorm (hf : AEStronglyMeasurable f Œº) : (eLpNorm f p Œº).toReal = lpNorm f p Œº := by
  rw [lpNorm, if_pos hf]

lemma ofReal_lpNorm (hf : MemLp f p Œº) : .ofReal (lpNorm f p Œº) = eLpNorm f p Œº := by
  rw [‚Üê toReal_eLpNorm hf.aestronglyMeasurable, ENNReal.ofReal_toReal hf.eLpNorm_ne_top]

@[simp]
lemma lpNorm_of_not_aestronglyMeasurable (hf : ¬¨ AEStronglyMeasurable f Œº) : lpNorm f p Œº = 0 :=
  if_neg hf

@[simp]
lemma lpNorm_of_not_memLp (hf' : ¬¨ MemLp f p Œº) : lpNorm f p Œº = 0 := by simp_all [MemLp, lpNorm]

@[simp] lemma lpNorm_nonneg : 0 ‚â§ lpNorm f p Œº := by simp [lpNorm, apply_ite]

lemma lpNorm_eq_integral_norm_rpow_toReal (hp‚ÇÄ : p ‚â† 0) (hp : p ‚â† ‚àû)
    (hf : AEStronglyMeasurable f Œº) :
    lpNorm f p Œº = (‚à´ x, ‚Äñf x‚Äñ ^ p.toReal ‚àÇŒº) ^ p.toReal‚Åª¬π := by
  rw [‚Üê toReal_eLpNorm hf, eLpNorm_eq_lintegral_rpow_enorm_toReal hp‚ÇÄ hp, ‚Üê ENNReal.toReal_rpow,
    ‚Üê integral_toReal]
  ¬∑ simp [‚Üê ENNReal.toReal_rpow]
  ¬∑ simp_rw [‚Üê ofReal_norm]
    borelize E
    fun_prop
  ¬∑ exact .of_forall fun x ‚Ü¶ ENNReal.rpow_lt_top_of_nonneg (by positivity) (by simp)

lemma lpNorm_nnreal_eq_integral_norm_rpow {p : ‚Ñù‚â•0} (hp : p ‚â† 0) (hf : AEStronglyMeasurable f Œº) :
    lpNorm f p Œº = (‚à´ x, ‚Äñf x‚Äñ ^ (p : ‚Ñù) ‚àÇŒº) ^ (p‚Åª¬π : ‚Ñù) := by
  rw [lpNorm_eq_integral_norm_rpow_toReal (by positivity) (by simp) hf]; simp

lemma lpNorm_one_eq_integral_norm (hf : AEStronglyMeasurable f Œº) :
    lpNorm f 1 Œº = ‚à´ x, ‚Äñf x‚Äñ ‚àÇŒº := by
  simp [lpNorm_eq_integral_norm_rpow_toReal one_ne_zero ENNReal.coe_ne_top hf]

@[simp] lemma lpNorm_exponent_zero (f : Œ± ‚Üí E) : lpNorm f 0 Œº = 0 := by simp [lpNorm]
@[simp] lemma lpNorm_measure_zero (f : Œ± ‚Üí E) : lpNorm f p (0 : Measure Œ±) = 0 := by simp [lpNorm]

lemma ae_le_lpNorm_exponent_top (hf : MemLp f ‚àû Œº) : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ lpNorm f ‚àû Œº := by
  simpa only [‚Üê toReal_eLpNorm hf.aestronglyMeasurable, ‚Üê ENNReal.ofReal_le_iff_le_toReal hf.2.ne,
    ofReal_norm] using ae_le_eLpNormEssSup

lemma lpNorm_exponent_top_eq_essSup (hf : MemLp f ‚àû Œº) : lpNorm f ‚àû Œº = essSup (‚Äñf ¬∑‚Äñ) Œº := by
  simp only [‚Üê toReal_eLpNorm hf.aestronglyMeasurable, eLpNorm_exponent_top, eLpNormEssSup]
  refine ENNReal.toReal_essSup (by simp) ‚ü®lpNorm f ‚àû Œº, ?_‚ü©
  simpa [-toReal_enorm, lpNorm] using ae_le_lpNorm_exponent_top hf

@[simp]
lemma lpNorm_zero (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) : lpNorm (0 : Œ± ‚Üí E) p Œº = 0 := by simp [lpNorm]

@[simp]
lemma lpNorm_fun_zero (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) : lpNorm (fun _ ‚Ü¶ 0 : Œ± ‚Üí E) p Œº = 0 := by
  simp [lpNorm]

@[simp]
lemma lpNorm_eq_zero (hf : MemLp f p Œº) (hp : p ‚â† 0) : lpNorm f p Œº = 0 ‚Üî f =·µê[Œº] 0 := by
  simp [‚Üê toReal_eLpNorm hf.aestronglyMeasurable, ENNReal.toReal_eq_zero_iff, hf.eLpNorm_ne_top,
    eLpNorm_eq_zero_iff hf.1 hp]

@[simp] lemma lpNorm_of_isEmpty [IsEmpty Œ±] (f : Œ± ‚Üí E) (p : ‚Ñù‚â•0‚àû) : lpNorm f p Œº = 0 := by
  simp [Subsingleton.elim f 0]

@[simp] lemma lpNorm_neg (f : Œ± ‚Üí E) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    lpNorm (-f) p Œº = lpNorm f p Œº := by
  by_cases hf : AEStronglyMeasurable f Œº
  ¬∑ simp [‚Üê toReal_eLpNorm, hf, hf.neg]
  ¬∑ rw [lpNorm_of_not_aestronglyMeasurable hf,
      lpNorm_of_not_aestronglyMeasurable fun h ‚Ü¶ hf <| by simpa using h.neg]

@[simp] lemma lpNorm_fun_neg (f : Œ± ‚Üí E) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    lpNorm (fun x ‚Ü¶ -f x) p Œº = lpNorm f p Œº := lpNorm_neg ..

lemma lpNorm_sub_comm (f g : Œ± ‚Üí E) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    lpNorm (f - g) p Œº = lpNorm (g - f) p Œº := by rw [‚Üê lpNorm_neg]; simp

@[simp] lemma lpNorm_norm (hf : AEStronglyMeasurable f Œº) (p : ‚Ñù‚â•0‚àû) :
    lpNorm (fun x ‚Ü¶ ‚Äñf x‚Äñ) p Œº = lpNorm f p Œº := by
  rw [‚Üê toReal_eLpNorm hf, ‚Üê toReal_eLpNorm (by fun_prop)]; simp

@[simp] lemma lpNorm_abs {f : Œ± ‚Üí ‚Ñù} (hf : AEStronglyMeasurable f Œº) (p : ‚Ñù‚â•0‚àû) :
    lpNorm (|f|) p Œº = lpNorm f p Œº := lpNorm_norm hf p

@[simp] lemma lpNorm_fun_abs {f : Œ± ‚Üí ‚Ñù} (hf : AEStronglyMeasurable f Œº) (p : ‚Ñù‚â•0‚àû) :
    lpNorm (fun x ‚Ü¶ |f x|) p Œº = lpNorm f p Œº := lpNorm_abs hf _

@[simp] lemma lpNorm_const (hp : p ‚â† 0) (hŒº : Œº ‚â† 0) (c : E) :
    lpNorm (fun _x ‚Ü¶ c) p Œº = ‚Äñc‚Äñ * Œº.real .univ ^ p.toReal‚Åª¬π := by
  simp [lpNorm, eLpNorm_const c hp hŒº, Measure.real, ENNReal.toReal_rpow,
    aestronglyMeasurable_const]

@[simp] lemma lpNorm_const' (hp‚ÇÄ : p ‚â† 0) (hp : p ‚â† ‚àû) (c : E) :
    lpNorm (fun _x ‚Ü¶ c) p Œº = ‚Äñc‚Äñ * Œº.real .univ ^ p.toReal‚Åª¬π := by
  simp [lpNorm, eLpNorm_const' c hp‚ÇÄ hp, Measure.real, ENNReal.toReal_rpow,
    aestronglyMeasurable_const]

section NormedField
variable {ùïú : Type*} [NormedField ùïú]

@[simp] lemma lpNorm_one (hp : p ‚â† 0) (hŒº : Œº ‚â† 0) :
    lpNorm (1 : Œ± ‚Üí ùïú) p Œº = Œº.real .univ ^ (p.toReal‚Åª¬π : ‚Ñù) := by
  simp [Pi.one_def, lpNorm_const hp hŒº, Measure.real, ENNReal.toReal_rpow]

@[simp] lemma lpNorm_one' (hp‚ÇÄ : p ‚â† 0) (hp : p ‚â† ‚àû) (Œº : Measure Œ±) :
    lpNorm (1 : Œ± ‚Üí ùïú) p Œº = Œº.real .univ ^ (p.toReal‚Åª¬π : ‚Ñù) := by
  simp [Pi.one_def, lpNorm_const' hp‚ÇÄ hp, Measure.real, ENNReal.toReal_rpow]

lemma lpNorm_const_smul [Module ùïú E] [NormSMulClass ùïú E] (c : ùïú) (f : Œ± ‚Üí E) (Œº : Measure Œ±) :
    lpNorm (c ‚Ä¢ f) p Œº = ‚Äñc‚Äñ‚Çä * lpNorm f p Œº := by
  by_cases hf : AEStronglyMeasurable f Œº
  ¬∑ simp [lpNorm, eLpNorm_const_smul, hf, hf.const_smul]
  obtain rfl | hc := eq_or_ne c 0
  ¬∑ simp
  rw [lpNorm_of_not_aestronglyMeasurable hf, lpNorm_of_not_aestronglyMeasurable fun h ‚Ü¶ hf <| by
    simpa [hc] using h.const_smul c‚Åª¬π]
  simp

lemma lpNorm_nsmul [NormedSpace ‚Ñù E] (n : ‚Ñï) (f : Œ± ‚Üí E) (Œº : Measure Œ±) :
    lpNorm (n ‚Ä¢ f) p Œº = n ‚Ä¢ lpNorm f p Œº := by
  simpa [Nat.cast_smul_eq_nsmul] using lpNorm_const_smul (n : ‚Ñù) f Œº (p := p)

variable [NormedSpace ‚Ñù ùïú]

lemma lpNorm_natCast_mul (n : ‚Ñï) (f : Œ± ‚Üí ùïú) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    lpNorm ((n : Œ± ‚Üí ùïú) * f) p Œº = n * lpNorm f p Œº := by
  simpa only [nsmul_eq_mul] using lpNorm_nsmul n f Œº

lemma lpNorm_fun_natCast_mul (n : ‚Ñï) (f : Œ± ‚Üí ùïú) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    lpNorm (n * f ¬∑) p Œº = n * lpNorm f p Œº := lpNorm_natCast_mul ..

lemma lpNorm_mul_natCast (f : Œ± ‚Üí ùïú) (n : ‚Ñï) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    lpNorm (f * (n : Œ± ‚Üí ùïú)) p Œº = lpNorm f p Œº * n := by
  simpa only [mul_comm] using lpNorm_natCast_mul n f p Œº

lemma lpNorm_fun_mul_natCast (f : Œ± ‚Üí ùïú) (n : ‚Ñï) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    lpNorm (f ¬∑ * n) p Œº = lpNorm f p Œº * n := lpNorm_mul_natCast ..

lemma lpNorm_div_natCast [CharZero ùïú] {n : ‚Ñï} (hn : n ‚â† 0) (f : Œ± ‚Üí ùïú) (p : ‚Ñù‚â•0‚àû)
    (Œº : Measure Œ±) : lpNorm (f / (n : Œ± ‚Üí ùïú)) p Œº = lpNorm f p Œº / n := by
  rw [eq_div_iff (by positivity), ‚Üê lpNorm_mul_natCast]; simp [Pi.mul_def, hn]

lemma lpNorm_fun_div_natCast [CharZero ùïú] {n : ‚Ñï} (hn : n ‚â† 0) (f : Œ± ‚Üí ùïú) (p : ‚Ñù‚â•0‚àû)
    (Œº : Measure Œ±) : lpNorm (f ¬∑ / n) p Œº = lpNorm f p Œº / n := lpNorm_div_natCast hn ..

end NormedField

lemma lpNorm_add_le (hf : MemLp f p Œº) (hp : 1 ‚â§ p) :
    lpNorm (f + g) p Œº ‚â§ lpNorm f p Œº + lpNorm g p Œº := by
  by_cases hg : MemLp g p Œº
  ¬∑ rw [‚Üê toReal_eLpNorm (hf.add hg).aestronglyMeasurable,
      ‚Üê toReal_eLpNorm hf.aestronglyMeasurable, ‚Üê toReal_eLpNorm hg.aestronglyMeasurable,
      ‚Üê ENNReal.toReal_add hf.eLpNorm_ne_top hg.eLpNorm_ne_top]
    gcongr
    exacts [ENNReal.add_ne_top.2 ‚ü®hf.eLpNorm_ne_top, hg.eLpNorm_ne_top‚ü©,
      eLpNorm_add_le hf.aestronglyMeasurable hg.aestronglyMeasurable hp]
  ¬∑ rw [lpNorm_of_not_memLp fun hfg ‚Ü¶ hg <| by simpa using hfg.sub hf, lpNorm_of_not_memLp hg]
    simp

lemma lpNorm_add_le' (hg : MemLp g p Œº) (hp : 1 ‚â§ p) :
    lpNorm (f + g) p Œº ‚â§ lpNorm f p Œº + lpNorm g p Œº := by
  simpa [add_comm] using lpNorm_add_le hg (g := f) hp

lemma lpNorm_sub_le (hf : MemLp f p Œº) (hp : 1 ‚â§ p) :
    lpNorm (f - g) p Œº ‚â§ lpNorm f p Œº + lpNorm g p Œº := by
  simpa [sub_eq_add_neg] using lpNorm_add_le hf (g := -g) hp

lemma lpNorm_le_lpNorm_add_lpNorm_sub' (hg : MemLp g p Œº) (hp : 1 ‚â§ p) :
    lpNorm f p Œº ‚â§ lpNorm g p Œº + lpNorm (f - g) p Œº := by
  simpa using lpNorm_add_le hg (g := f - g) hp

lemma lpNorm_le_lpNorm_add_lpNorm_sub (hg : MemLp g p Œº) (hp : 1 ‚â§ p) :
    lpNorm f p Œº ‚â§ lpNorm g p Œº + lpNorm (g - f) p Œº := by
  simpa [neg_add_eq_sub] using lpNorm_add_le hg.neg (g := g - f) hp

lemma lpNorm_le_add_lpNorm_add (hg : MemLp g p Œº) (hp : 1 ‚â§ p) :
    lpNorm f p Œº ‚â§ lpNorm (f + g) p Œº + lpNorm g p Œº := by
  simpa using lpNorm_add_le' (f := f + g) hg.neg hp

lemma lpNorm_sub_le_lpNorm_sub_add_lpNorm_sub (hf : MemLp f p Œº) (hg : MemLp g p Œº) (hp : 1 ‚â§ p) :
    lpNorm (f - h) p Œº ‚â§ lpNorm (f - g) p Œº + lpNorm (g - h) p Œº := by
  simpa using lpNorm_add_le (hf.sub hg) (g := g - h) hp

lemma lpNorm_sum_le {Œπ : Type*} {s : Finset Œπ} {f : Œπ ‚Üí Œ± ‚Üí E} (hf : ‚àÄ i ‚àà s, MemLp (f i) p Œº)
    (hp : 1 ‚â§ p) : lpNorm (‚àë i ‚àà s, f i) p Œº ‚â§ ‚àë i ‚àà s, lpNorm (f i) p Œº := by
  rw [‚Üê Finset.sum_congr rfl fun i hi ‚Ü¶ toReal_eLpNorm (hf i hi).aestronglyMeasurable,
    ‚Üê ENNReal.toReal_sum fun i hi ‚Ü¶ (hf i hi).2.ne,
    ‚Üê toReal_eLpNorm (Finset.aestronglyMeasurable_sum _ fun i hi ‚Ü¶ (hf i hi).aestronglyMeasurable)]
  grw [eLpNorm_sum_le (fun i hi ‚Ü¶ (hf _ hi).aestronglyMeasurable) hp]
  simpa using fun i hi ‚Ü¶ (hf i hi).2.ne

-- TODO: Golf using `eLpNorm_expect_le` once it exists
lemma lpNorm_expect_le [Module ‚Ñö‚â•0 E] [NormedSpace ‚Ñù E] {Œπ : Type*} {s : Finset Œπ}
    {f : Œπ ‚Üí Œ± ‚Üí E} (hf : ‚àÄ i ‚àà s, MemLp (f i) p Œº) (hp : 1 ‚â§ p) :
    lpNorm (ùîº i ‚àà s, f i) p Œº ‚â§ ùîº i ‚àà s, lpNorm (f i) p Œº  :=  by
  obtain rfl | hs := s.eq_empty_or_nonempty
  ¬∑ simp
  refine (le_inv_smul_iff_of_pos <| by positivity).2 ?_
  rw [Nat.cast_smul_eq_nsmul, ‚Üê lpNorm_nsmul, Finset.card_smul_expect]
  exact lpNorm_sum_le hf hp

lemma lpNorm_mono_real {g : Œ± ‚Üí ‚Ñù} (hg : MemLp g p Œº) (h : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ g x) :
    lpNorm f p Œº ‚â§ lpNorm g p Œº := by
  by_cases hf : AEStronglyMeasurable f Œº
  ¬∑ rw [‚Üê toReal_eLpNorm hf, ‚Üê toReal_eLpNorm hg.aestronglyMeasurable]
    exact ENNReal.toNNReal_mono (hg.eLpNorm_ne_top) (eLpNorm_mono_real h)
  ¬∑ simp [hf]

lemma lpNorm_smul_measure_of_ne_zero {f : Œ± ‚Üí E} {c : ‚Ñù‚â•0} (hc : c ‚â† 0) :
    lpNorm f p (c ‚Ä¢ Œº) = c ^ p.toReal‚Åª¬π ‚Ä¢ lpNorm f p Œº := by
  by_cases hf : AEStronglyMeasurable f Œº
  ¬∑ simp [‚Üê toReal_eLpNorm, hf, hf.smul_measure, eLpNorm_smul_measure_of_ne_zero' hc f p Œº]
    simp [ENNReal.smul_def, NNReal.smul_def]
  ¬∑ rw [lpNorm_of_not_aestronglyMeasurable hf, lpNorm_of_not_aestronglyMeasurable fun h ‚Ü¶ hf <| by
      simpa [hc] using h.smul_measure c‚Åª¬π]
    simp

lemma lpNorm_smul_measure_of_ne_top (hp : p ‚â† ‚àû) {f : Œ± ‚Üí E} (c : ‚Ñù‚â•0) :
    lpNorm f p (c ‚Ä¢ Œº) = c ^ p.toReal‚Åª¬π ‚Ä¢ lpNorm f p Œº := by
  by_cases hf : AEStronglyMeasurable f Œº
  ¬∑ simp [‚Üê toReal_eLpNorm, hf, hf.smul_measure, eLpNorm_smul_measure_of_ne_top' hp]
    simp [ENNReal.smul_def, NNReal.smul_def]
  obtain rfl | hp‚ÇÄ := eq_or_ne p 0
  ¬∑ simp
  obtain rfl | hc := eq_or_ne c 0
  ¬∑ rw [NNReal.zero_rpow (by simp [ENNReal.toReal_eq_zero_iff, *])]
    simp
  rw [lpNorm_of_not_aestronglyMeasurable hf, lpNorm_of_not_aestronglyMeasurable fun h ‚Ü¶ hf <| by
    simpa [hc] using h.smul_measure c‚Åª¬π]
  simp

@[simp] lemma lpNorm_conj {K : Type*} [RCLike K] (f : Œ± ‚Üí K) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    lpNorm (conj f) p Œº = lpNorm f p Œº := by
  by_cases hf : AEStronglyMeasurable f Œº
  ¬∑ rw [‚Üê lpNorm_norm hf, ‚Üê lpNorm_norm]
    ¬∑ simp
    ¬∑ exact (continuous_star.measurable.comp_aemeasurable hf.aemeasurable).aestronglyMeasurable
  ¬∑ rw [lpNorm_of_not_aestronglyMeasurable hf, lpNorm_of_not_aestronglyMeasurable fun h ‚Ü¶ hf ?_]
    simpa [Function.comp_def]
      using (continuous_star.measurable.comp_aemeasurable h.aemeasurable).aestronglyMeasurable

end MeasureTheory
