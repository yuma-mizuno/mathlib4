/-
Copyright (c) 2020 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, SÃ©bastien GouÃ«zel
-/
module

public import Mathlib.MeasureTheory.Function.LpSeminorm.Monotonicity

/-!
# Scalar multiplication on â„’p space
-/

public noncomputable section

open Filter

open scoped ENNReal

namespace MeasureTheory

variable {Î± F : Type*} {m : MeasurableSpace Î±} {p : â„â‰¥0âˆ} {q : â„} {Î¼ : Measure Î±}
  [NormedAddCommGroup F] {f : Î± â†’ F}

section Lp

/-!
### Bounded actions by normed rings
In this section we show inequalities on the norm.
-/

section IsBoundedSMul

variable {ğ•œ : Type*} [NormedRing ğ•œ] [MulActionWithZero ğ•œ F] [IsBoundedSMul ğ•œ F] {c : ğ•œ}

theorem eLpNorm'_const_smul_le (hq : 0 < q) : eLpNorm' (c â€¢ f) q Î¼ â‰¤ â€–câ€–â‚‘ * eLpNorm' f q Î¼ :=
  eLpNorm'_le_nnreal_smul_eLpNorm'_of_ae_le_mul (Eventually.of_forall fun _ => nnnorm_smul_le ..) hq

theorem eLpNormEssSup_const_smul_le : eLpNormEssSup (c â€¢ f) Î¼ â‰¤ â€–câ€–â‚‘ * eLpNormEssSup f Î¼ :=
  eLpNormEssSup_le_nnreal_smul_eLpNormEssSup_of_ae_le_mul
    (Eventually.of_forall fun _ => by simp [nnnorm_smul_le])

theorem eLpNorm_const_smul_le : eLpNorm (c â€¢ f) p Î¼ â‰¤ â€–câ€–â‚‘ * eLpNorm f p Î¼ :=
  eLpNorm_le_nnreal_smul_eLpNorm_of_ae_le_mul
    (Eventually.of_forall fun _ => by simp [nnnorm_smul_le]) _

theorem MemLp.const_smul (hf : MemLp f p Î¼) (c : ğ•œ) : MemLp (c â€¢ f) p Î¼ :=
  âŸ¨hf.1.const_smul c, eLpNorm_const_smul_le.trans_lt (ENNReal.mul_lt_top ENNReal.coe_lt_top hf.2)âŸ©

theorem MemLp.const_mul {f : Î± â†’ ğ•œ} (hf : MemLp f p Î¼) (c : ğ•œ) : MemLp (fun x => c * f x) p Î¼ :=
  hf.const_smul c

theorem MemLp.mul_const {f : Î± â†’ ğ•œ} (hf : MemLp f p Î¼) (c : ğ•œ) :
    MemLp (fun x => f x * c) p Î¼ :=
  hf.const_smul (MulOpposite.op c)

end IsBoundedSMul

section ENormSMulClass

variable {ğ•œ : Type*} [NormedRing ğ•œ]
  {Îµ : Type*} [TopologicalSpace Îµ] [ESeminormedAddMonoid Îµ] [SMul ğ•œ Îµ] [ENormSMulClass ğ•œ Îµ]
  {c : ğ•œ} {f : Î± â†’ Îµ}

theorem eLpNorm'_const_smul_le' (hq : 0 < q) : eLpNorm' (c â€¢ f) q Î¼ â‰¤ â€–câ€–â‚‘ * eLpNorm' f q Î¼ :=
  eLpNorm'_le_nnreal_smul_eLpNorm'_of_ae_le_mul'
    (Eventually.of_forall fun _ â†¦ le_of_eq (enorm_smul ..)) hq

theorem eLpNormEssSup_const_smul_le' : eLpNormEssSup (c â€¢ f) Î¼ â‰¤ â€–câ€–â‚‘ * eLpNormEssSup f Î¼ :=
  eLpNormEssSup_le_nnreal_smul_eLpNormEssSup_of_ae_le_mul'
    (Eventually.of_forall fun _ => by simp [enorm_smul])

theorem eLpNorm_const_smul_le' : eLpNorm (c â€¢ f) p Î¼ â‰¤ â€–câ€–â‚‘ * eLpNorm f p Î¼ :=
  eLpNorm_le_nnreal_smul_eLpNorm_of_ae_le_mul'
    (Eventually.of_forall fun _ => le_of_eq (enorm_smul ..)) _

theorem MemLp.const_smul' [ContinuousConstSMul ğ•œ Îµ] (hf : MemLp f p Î¼) (c : ğ•œ) :
    MemLp (c â€¢ f) p Î¼ :=
  âŸ¨hf.1.const_smul c, eLpNorm_const_smul_le'.trans_lt (ENNReal.mul_lt_top ENNReal.coe_lt_top hf.2)âŸ©

theorem MemLp.const_mul' {f : Î± â†’ ğ•œ} (hf : MemLp f p Î¼) (c : ğ•œ) : MemLp (fun x => c * f x) p Î¼ :=
  hf.const_smul c

end ENormSMulClass

/-!
### Bounded actions by normed division rings
The inequalities in the previous section are now tight.

TODO: do these results hold for any `NormedRing` assuming `NormSMulClass`?
-/

section NormedSpace

variable {ğ•œ : Type*} [NormedDivisionRing ğ•œ] [Module ğ•œ F] [NormSMulClass ğ•œ F]

theorem eLpNorm'_const_smul {f : Î± â†’ F} (c : ğ•œ) (hq_pos : 0 < q) :
    eLpNorm' (c â€¢ f) q Î¼ = â€–câ€–â‚‘ * eLpNorm' f q Î¼ := by
  obtain rfl | hc := eq_or_ne c 0
  Â· simp [eLpNorm'_eq_lintegral_enorm, hq_pos]
  refine le_antisymm (eLpNorm'_const_smul_le hq_pos) <| ENNReal.mul_le_of_le_div' ?_
  simpa [enorm_inv, hc, ENNReal.div_eq_inv_mul]
    using eLpNorm'_const_smul_le (c := câ»Â¹) (f := c â€¢ f) hq_pos

theorem eLpNormEssSup_const_smul (c : ğ•œ) (f : Î± â†’ F) :
    eLpNormEssSup (c â€¢ f) Î¼ = â€–câ€–â‚‘ * eLpNormEssSup f Î¼ := by
  simp_rw [eLpNormEssSup_eq_essSup_enorm, Pi.smul_apply, enorm_smul,
    ENNReal.essSup_const_mul]

theorem eLpNorm_const_smul (c : ğ•œ) (f : Î± â†’ F) (p : â„â‰¥0âˆ) (Î¼ : Measure Î±) :
    eLpNorm (c â€¢ f) p Î¼ = â€–câ€–â‚‘ * eLpNorm f p Î¼ := by
  obtain rfl | hc := eq_or_ne c 0
  Â· simp
  refine le_antisymm eLpNorm_const_smul_le <| ENNReal.mul_le_of_le_div' ?_
  simpa [enorm_inv, hc, ENNReal.div_eq_inv_mul]
    using eLpNorm_const_smul_le (c := câ»Â¹) (f := c â€¢ f)

lemma eLpNorm_nsmul [NormedSpace â„ F] (n : â„•) (f : Î± â†’ F) :
    eLpNorm (n â€¢ f) p Î¼ = n * eLpNorm f p Î¼ := by
  simpa [Nat.cast_smul_eq_nsmul] using eLpNorm_const_smul (n : â„) f ..

end NormedSpace

end Lp
end MeasureTheory
