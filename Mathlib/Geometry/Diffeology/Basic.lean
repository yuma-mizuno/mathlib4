/-
Copyright (c) 2024 Ben Eltschig. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ben Eltschig
-/
module

public import Mathlib.Analysis.InnerProductSpace.EuclideanDist

/-!
# Diffeological spaces

A diffeological space is a concrete sheaf on the site of cartesian spaces ‚Ñù‚Åø and smooth maps
between them, or equivalently a type `X` with a well-behaved notion of smoothness for functions from
the spaces ‚Ñù‚Åø to X - see https://ncatlab.org/nlab/show/diffeological+space. In this file we focus on
the latter viewpoint and define some of the basic notions of diffeology, like diffeological spaces
and smooth maps between them.

Concretely, this means that for our purposes a diffeological space is a type `X` together with a set
`plots n` of maps ‚Ñù‚Åø ‚Üí X for each n (called plots), such that the following three properties hold:
* Every constant map is a plot.
* For every plot p : ‚Ñù‚Åø ‚Üí X and smooth map f : ‚Ñù·µê ‚Üí ‚Ñù‚Åø, p ‚àò f is a plot.
* Every map p : ‚Ñù‚Åø ‚Üí X that is locally smooth is a plot, where by locally smooth we mean that ‚Ñù‚Åø can
  be covered by open sets U such that p ‚àò f is a plot for every smooth f : ‚Ñù·µê ‚Üí U.
Every normed space, smooth manifold etc. is then naturally a diffeological space by simply taking
the plots to be those maps ‚Ñù‚Åø ‚Üí X that are smooth in the traditional sense. A map `f : X ‚Üí Y`
between diffeological spaces is furthermore called smooth if postcomposition with it sends plots of
`X` to plots of `Y`. This is equivalent to the usual definition of smoothness for maps between e.g.
manifolds, and equivalent to being a plot for maps p : ‚Ñù‚Åø ‚Üí X.

In addition to this notion of smoothness, every diffeological space `X` also comes equipped with a
natural diffeology, called the D-topology; it is the finest topology on `X` that makes all plots
continuous, and consists precisely of those sets whose preimages under plots are all open. This
recovers the standard topology of e.g. normed spaces and manifolds, and makes all smooth maps
continuous. We provide this as a definition but not as an instance, for reasons outlined in the
implementation details below.

## Main definitions / results

* `DiffeologicalSpace X`: the type of diffeologies on a type `X`.
* `IsPlot p`: predicate stating that a map p : ‚Ñù‚Åø ‚Üí X is a plot, i.e. belongs to the diffeology
  on `X`.
* `DSmooth f`: predicate stating that a map `f : X ‚Üí Y` between diffeological spaces is smooth in
  the sense that it sends plots to plots. This is the correct notion of morphisms between
  diffeological spaces.
* `dTopology`: the D-topology on a diffeological space, consisting of all sets `U` whose preimage
  `p ‚Åª¬π' u` is open for all plots `p`. This is a definition rather than an instance to avoid
  typeclass diamonds (see below), and meant for use with notation such as
  `Continuous[_, dTopology]`.
* `IsDTopologyCompatible X`: typeclass stating that `X` is equipped with a topology that is equal
  (but not necessarily defeq) to the D-topology.
* `NormedSpace.toDiffeology`: the standard diffeology on any finite-dimensional normed space `X`,
  consisting of all conventionally smooth maps ‚Ñù‚Åø ‚Üí X. This is again a definition rather than a
  instance for typeclass diamond reasons; however, we do put this as an instance on `‚Ñù` and
  `EuclideanSpace ‚Ñù Œπ` for finite `Œπ`.
* `IsContDiffCompatible X`: typeclass stating that the diffeology on a normed space `X` is equal to
  the diffeology whose plots are precisely the smooth maps.
* `isPlot_iff_dSmooth`: a map ‚Ñù‚Åø ‚Üí X is a plot iff it is smooth.
* `dSmooth_iff_contDiff`: a map between finite-dimensional normed spaces is smooth in the sense of
  `DSmooth` iff it is smooth in the sense of `ContDiff ‚Ñù ‚àû`.

## Implementation notes

### Choice of test spaces

Instead of defining diffeologies as collections of plots ‚Ñù‚Åø ‚Üí X whose domains are the spaces ‚Ñù‚Åø, we
could have also defined them in terms of maps from some other collection of test spaces; for
example:
* all open balls in the spaces ‚Ñù‚Åø
* all open subsets of the spaces ‚Ñù‚Åø
* all finite-dimensional normed spaces, or open balls therein / open subsets thereof
* all finite-dimensional smooth manifolds.

All of these result in equivalent notions of diffeologies and smooth maps; the abstract way to see
this is that the corresponding sites are all dense subsites of the site of finite-dimensional smooth
manifolds, and hence give rise to equivalent sheaf topoi. Which of those sites / collections of test
spaces to use is hence mainly a matter of convenience; we have gone with the cartesian spaces ‚Ñù‚Åø
mainly for two reasons:
* They are the simplest to work with for practical purposes: maps between subsets are more annoying
  to deal with formally than maps between types, and e.g. smooth manifolds are extremely annoying
  to quantify over, while the cartesian spaces ‚Ñù‚Åø are indexed simply by the natural numbers ‚Ñï.
* Working with e.g. all finite-dimensional normed spaces instead of this particular choice of
  representatives would lead to an unnecessary universe bump.

One downside of this choice compared to that of all open subsets of ‚Ñù‚Åø is however that it makes the
sheaf condition / locality condition of diffeologies ("any map that is locally a plot is also
globally a plot") somewhat awkward to state and prove. To mitigate this, we provide
`DiffeologicalSpace.ofPlotsOn` as a way to construct a diffeology from plots whose domains are
subsets of ‚Ñù‚Åø. See the definition of `NormedSpace.toDiffeology` for an example where this is used.

### D-Topology

While the D-topology is quite well-behaved in some regards, it does unfortunately not always commute
with e.g. products. This means that it can not be registered as an instance - otherwise, there would
be two `TopologicalSpace`-instances on binary products, the product topology of the D-topologies on
the factors and the D-topology of the product diffeology. For emphasis we repeat: in general these
topologies can be mathematically distinct not just non-defeq. We thus instead define a typeclass
`IsDTopologyCompatible X` to express when the topology on `X` does match the D-topology, and also
give the D-topology the short name `dTopology` to enable use of notations such as
`Continuous[_, dTopology]` for continuity with respect to the D-topology.

In order to make it easier to work with diffeological spaces whose natural diffeology does match
the D-topology, we also include the D-topology as part of the data of `DiffeologicalSpace X`.
This allows the diffeologies on e.g. ‚Ñù‚Åø, manifolds and quotients of diffeological spaces to be
defined in such a way that the D-topology is defeq to the topology that the space already carries.

### Diffeologies on normed spaces

Every normed spaces carries a natural diffeology consisting of all smooth maps from ‚Ñù‚Åø. While this
"normed space diffeology" does commute with arbitrary products, we can't register it as an instance
because it wouldn't be defeq to the product diffeology on products of normed spaces. We thus only
give it as a definition (`NormedSpace.toDiffeology`) instead of an instance, and instead provide a
typeclass `IsContDiffCompatible X` for talking about normed spaces equipped with the normed space
diffeology.

To make working with finite-dimensional spaces easier, `NormedSpace.toDiffeology` is defined in such
a way that its D-topology is defeq to the topology induced by the norm - however, this currently
comes at the cost of making the definition work only on finite-dimensional spaces. It should be
possible to extend this to all normed spaces though in the future.

## TODO

Much of the basic theory of diffeological spaces has already been formalised at
https://github.com/peabrainiac/lean-orbifolds and just needs to be upstreamed. However, some TODOs
that haven't been formalised at all yet and only depend on the material here are:
* Generalise `NormedSpace.toDiffeology` to infinite-dimensional normed spaces. The hard part of this
  is showing that the D-topology of any normed space is just its usual topology, as is needed to
  make that equality definitional. On paper, this is relatively straightforward:
  for a set U ‚äÜ X that is not open under the standard normed space topology, take a sequence x_i
  outside of U that converges to a point in U, a smooth map ‚Ñù ‚Üí X under which a convergent sequence
  in ‚Ñù maps to this sequence (x_i), and use it to conclude that U is not D-open either. However,
  constructing the needed smooth map explicitly is probably a lot of work.
* Generalise `dSmooth_iff_contDiff` to infinite-dimensional normed spaces if possible. There should
  be some references at least for the case of Banach spaces in the literature.

## References

* [Patrick Iglesias-Zemmour, *Diffeology*][zemmour2013diffeology]
* <https://ncatlab.org/nlab/show/diffeological+space>

## Tags
diffeology, diffeological space, smoothness, smooth function
-/

@[expose] public section

noncomputable section

assert_not_exists ChartedSpace

local macro:max "ùîº" noWs n:superscript(term) : term => `(EuclideanSpace ‚Ñù (Fin $(‚ü®n.raw[0]‚ü©)))

open Topology ContDiff

/-- A diffeology on `X`, given by the smooth functions (or "plots") from ‚Ñù‚Åø to `X`. -/
class DiffeologicalSpace (X : Type*) where
  /-- The plots `EuclideanSpace ‚Ñù (Fin n) ‚Üí X` representing the smooth ways to map
  `EuclideanSpace ‚Ñù (Fin n)` into `X`. This is the main
  piece of data underlying the diffeology. -/
  plots (n : ‚Ñï) : Set (ùîº‚Åø ‚Üí X)
  /-- Every constant map needs to be a plot. -/
  constant_plots {n : ‚Ñï} (x : X) : (fun _ ‚Ü¶ x) ‚àà plots n
  /-- Smooth reparametrisations of plots need to be plots. -/
  plot_reparam {n m : ‚Ñï} {p : ùîº·µê ‚Üí X} {f : ùîº‚Åø ‚Üí ùîº·µê} (hp : p ‚àà plots m) (hf : ContDiff ‚Ñù ‚àû f) :
    p ‚àò f ‚àà plots n
  /-- Every locally smooth map `EuclideanSpace ‚Ñù (Fin n) ‚Üí X` is a plot. -/
  locality {n : ‚Ñï} {p : ùîº‚Åø ‚Üí X} (hp : ‚àÄ x : ùîº‚Åø, ‚àÉ u : Set ùîº‚Åø, IsOpen u ‚àß x ‚àà u ‚àß
    ‚àÄ {m : ‚Ñï} {f : ùîº·µê ‚Üí ùîº‚Åø}, (‚àÄ x, f x ‚àà u) ‚Üí ContDiff ‚Ñù ‚àû f ‚Üí p ‚àò f ‚àà plots m) : p ‚àà plots n
  /-- The D-topology of the diffeology. This is included as part of the data in order to give
  control over what the D-topology is defeq to. See also note [forgetful inheritance]. -/
  dTopology : TopologicalSpace X := {
    IsOpen u := ‚àÄ ‚¶Én : ‚Ñï‚¶Ñ, ‚àÄ p ‚àà plots n, IsOpen (p ‚Åª¬π' u)
    isOpen_univ := fun _ _ _ ‚Ü¶ isOpen_univ
    isOpen_inter := fun _ _ hs ht _ p hp ‚Ü¶
      Set.preimage_inter.symm ‚ñ∏ (IsOpen.inter (hs p hp) (ht p hp))
    isOpen_sUnion := fun _ hs _ p hp ‚Ü¶
      Set.preimage_sUnion ‚ñ∏ isOpen_biUnion fun u hu ‚Ü¶ hs u hu p hp }
  /-- The D-topology consists of exactly those sets whose preimages under plots are all open. -/
  isOpen_iff_preimages_plots {u : Set X} :
    dTopology.IsOpen u ‚Üî ‚àÄ {n : ‚Ñï}, ‚àÄ p ‚àà plots n, IsOpen (p ‚Åª¬π' u) := by rfl

namespace Diffeology

variable {X Y Z : Type*} [DiffeologicalSpace X] [DiffeologicalSpace Y] [DiffeologicalSpace Z]

section Defs

alias dTopology := DiffeologicalSpace.dTopology

/-- A map `p : EuclideanSpace ‚Ñù (Fin n) ‚Üí X` is called a plot iff it is part of the diffeology on
`X`. This is equivalent to `p` being smooth with respect to the standard diffeology on
`EuclideanSpace ‚Ñù (Fin n)`. -/
@[fun_prop]
def IsPlot {n : ‚Ñï} (p : ùîº‚Åø ‚Üí X) : Prop := p ‚àà DiffeologicalSpace.plots n

/-- A function between diffeological spaces is smooth iff composition with it preserves
smoothness of plots. -/
@[fun_prop]
def DSmooth (f : X ‚Üí Y) : Prop := ‚àÄ (n : ‚Ñï) (p : ùîº‚Åø ‚Üí X), IsPlot p ‚Üí IsPlot (f ‚àò p)

end Defs

@[ext]
protected theorem _root_.DiffeologicalSpace.ext {X : Type*} {d‚ÇÅ d‚ÇÇ : DiffeologicalSpace X}
    (h : @IsPlot _ d‚ÇÅ = @IsPlot _ d‚ÇÇ) : d‚ÇÅ = d‚ÇÇ := by
  obtain ‚ü®p‚ÇÅ, _, _, _, t‚ÇÅ, h‚ÇÅ‚ü© := d‚ÇÅ
  obtain ‚ü®p‚ÇÇ, _, _, _, t‚ÇÇ, h‚ÇÇ‚ü© := d‚ÇÇ
  congr 1; ext s
  exact ((show p‚ÇÅ = p‚ÇÇ from h) ‚ñ∏ @h‚ÇÅ s).trans (@h‚ÇÇ s).symm

@[fun_prop]
lemma isPlot_const {n : ‚Ñï} {x : X} : IsPlot (fun _ ‚Ü¶ x : ùîº‚Åø ‚Üí X) :=
  DiffeologicalSpace.constant_plots x

lemma isPlot_reparam {n m : ‚Ñï} {p : ùîº·µê ‚Üí X} {f : ùîº‚Åø ‚Üí ùîº·µê} (hp : IsPlot p) (hf : ContDiff ‚Ñù ‚àû f) :
    IsPlot (p ‚àò f) :=
  DiffeologicalSpace.plot_reparam hp hf

protected lemma IsPlot.dSmooth_comp {n : ‚Ñï} {p : ùîº‚Åø ‚Üí X} {f : X ‚Üí Y}
    (hp : IsPlot p) (hf : DSmooth f) : IsPlot (f ‚àò p) :=
  hf n p hp

@[fun_prop]
protected lemma IsPlot.dSmooth_comp' {n : ‚Ñï} {p : ùîº‚Åø ‚Üí X} {f : X ‚Üí Y}
    (hp : IsPlot p) (hf : DSmooth f) : IsPlot fun x ‚Ü¶ f (p x) :=
  hf n p hp

lemma isOpen_iff_preimages_plots {u : Set X} :
    IsOpen[dTopology] u ‚Üî ‚àÄ (n : ‚Ñï) (p : ùîº‚Åø ‚Üí X), IsPlot p ‚Üí IsOpen (p ‚Åª¬π' u) :=
  DiffeologicalSpace.isOpen_iff_preimages_plots

protected lemma IsPlot.continuous {n : ‚Ñï} {p : ùîº‚Åø ‚Üí X} (hp : IsPlot p) :
    Continuous[_, dTopology] p :=
  continuous_def.2 fun _ hu ‚Ü¶ isOpen_iff_preimages_plots.1 hu n p hp

protected theorem DSmooth.continuous {f : X ‚Üí Y} (hf : DSmooth f) :
    Continuous[dTopology, dTopology] f := by
  simp_rw [continuous_def, isOpen_iff_preimages_plots (X := X), isOpen_iff_preimages_plots (X := Y)]
  exact fun u hu n p hp ‚Ü¶ hu n (f ‚àò p) (hf n p hp)

theorem dSmooth_iff {f : X ‚Üí Y} :
    DSmooth f ‚Üî ‚àÄ (n : ‚Ñï) (p : ùîº‚Åø ‚Üí X), IsPlot p ‚Üí IsPlot (f ‚àò p) :=
  Iff.rfl

theorem dSmooth_id : DSmooth (@id X) := by simp [DSmooth]

@[fun_prop]
theorem dSmooth_id' : DSmooth fun x : X ‚Ü¶ x := dSmooth_id

theorem DSmooth.comp {f : X ‚Üí Y} {g : Y ‚Üí Z} (hg : DSmooth g) (hf : DSmooth f) :
    DSmooth (g ‚àò f) :=
  fun _ _ hp ‚Ü¶ hg _ _ (hf _ _ hp)

@[fun_prop]
theorem DSmooth.comp' {f : X ‚Üí Y} {g : Y ‚Üí Z} (hg : DSmooth g) (hf : DSmooth f) :
    DSmooth (fun x ‚Ü¶ g (f x)) := hg.comp hf

@[fun_prop]
theorem dSmooth_const {y : Y} : DSmooth fun _ : X ‚Ü¶ y :=
  fun _ _ _ ‚Ü¶ isPlot_const

end Diffeology

namespace DiffeologicalSpace

/-- Replaces the D-topology of a diffeology with another topology equal to it. Useful
to construct diffeologies with better definitional equalities. -/
def replaceDTopology {X : Type*} (d : DiffeologicalSpace X)
    (t : TopologicalSpace X) (h : @dTopology _ d = t) : DiffeologicalSpace X where
  dTopology := t
  isOpen_iff_preimages_plots := by intro _; rw [‚Üê d.isOpen_iff_preimages_plots, ‚Üê h]
  __ := d

lemma replaceDTopology_eq {X : Type*} {d : DiffeologicalSpace X}
    {t : TopologicalSpace X} {h : @dTopology _ d = t} : d.replaceDTopology t h = d := by
  ext; rfl

/-- A structure with plots specified on open subsets of ‚Ñù‚Åø rather than ‚Ñù‚Åø itself. Useful
for constructing diffeologies, as it often makes the locality condition easier to prove. -/
structure CorePlotsOn (X : Type*) where
  /-- The predicate determining which maps `u ‚Üí X` with `u : Set (EuclideanSpace ‚Ñù (Fin n))` open
  are plots. -/
  isPlotOn {n : ‚Ñï} {u : Set ùîº‚Åø} (hu : IsOpen u) : (ùîº‚Åø ‚Üí X) ‚Üí Prop
  isPlotOn_congr {n : ‚Ñï} {u : Set ùîº‚Åø} (hu : IsOpen u) {p q : ùîº‚Åø ‚Üí X} (h : Set.EqOn p q u) :
    isPlotOn hu p ‚Üî isPlotOn hu q
  /-- The predicate that the diffeology built from this structure will use. Can be overwritten
  to allow for better definitional equalities. -/
  isPlot {n : ‚Ñï} : (ùîº‚Åø ‚Üí X) ‚Üí Prop := fun p ‚Ü¶ isPlotOn isOpen_univ p
  isPlotOn_univ {n : ‚Ñï} {p : ùîº‚Åø ‚Üí X} :
    isPlotOn isOpen_univ p ‚Üî isPlot p := by simp
  isPlot_const {n : ‚Ñï} (x : X) : isPlot fun (_ : ùîº‚Åø) ‚Ü¶ x
  isPlotOn_reparam {n m : ‚Ñï} {u : Set ùîº‚Åø} {v : Set ùîº·µê} {hu : IsOpen u} (hv : IsOpen v)
    {p : ùîº‚Åø ‚Üí X} {f : ùîº·µê ‚Üí ùîº‚Åø} (h : Set.MapsTo f v u) (hp : isPlotOn hu p)
    (hf : ContDiffOn ‚Ñù ‚àû f v) : isPlotOn hv (p ‚àò f)
  /-- The locality axiom of diffeologies, phrased in terms of `isPlotOn`. -/
  locality {n : ‚Ñï} {u : Set ùîº‚Åø} (hu : IsOpen u) {p : ùîº‚Åø ‚Üí X}
    (hp : ‚àÄ x ‚àà u, ‚àÉ (v : Set _) (hv : IsOpen v), x ‚àà v ‚àß isPlotOn hv p) : isPlotOn hu p
  /-- The D-topology that the diffeology built from this structure will use. Can be overwritten
  to allow for better definitional equalities. -/
  dTopology : TopologicalSpace X := {
    IsOpen u := ‚àÄ ‚¶Én : ‚Ñï‚¶Ñ, ‚àÄ p : ùîº‚Åø ‚Üí X, isPlot p ‚Üí IsOpen (p ‚Åª¬π' u)
    isOpen_univ := fun _ _ _ ‚Ü¶ isOpen_univ
    isOpen_inter := fun _ _ hs ht _ p hp ‚Ü¶
      Set.preimage_inter.symm ‚ñ∏ (IsOpen.inter (hs p hp) (ht p hp))
    isOpen_sUnion := fun _ hs _ p hp ‚Ü¶
      Set.preimage_sUnion ‚ñ∏ isOpen_biUnion fun u hu ‚Ü¶ hs u hu p hp }
  isOpen_iff_preimages_plots {u : Set X} : dTopology.IsOpen u ‚Üî
    ‚àÄ {n : ‚Ñï}, ‚àÄ p : ùîº‚Åø ‚Üí X, isPlot p ‚Üí IsOpen (p ‚Åª¬π' u) := by rfl

/-- Constructs a diffeology from plots defined on open subsets or ‚Ñù‚Åø rather than ‚Ñù‚Åø itself,
organised in the form of the auxiliary `CorePlotsOn` structure.
This is more involved in most regards, but also often makes it quite a lot easier to prove
the locality condition. -/
def ofCorePlotsOn {X : Type*} (d : DiffeologicalSpace.CorePlotsOn X) :
    DiffeologicalSpace X where
  plots _ := {p | d.isPlot p}
  constant_plots _ := d.isPlot_const _
  plot_reparam hp hf := d.isPlotOn_univ.mp <|
    d.isPlotOn_reparam _ (Set.mapsTo_univ _ _) (d.isPlotOn_univ.mpr hp) hf.contDiffOn
  locality {n p} h := by
    refine d.isPlotOn_univ.mp <| d.locality _ fun x _ ‚Ü¶ ?_
    let ‚ü®u, hu, hxu, hu'‚ü© := h x
    let ‚ü®Œµ, hŒµ, hŒµ'‚ü© := Metric.isOpen_iff.mp hu x hxu
    have h : d.isPlot (p ‚àò OpenPartialHomeomorph.univBall x Œµ) := hu'
      (f := OpenPartialHomeomorph.univBall x Œµ)
      (fun x' ‚Ü¶ by
        replace h := (OpenPartialHomeomorph.univBall x Œµ).map_source (x := x')
        rw [OpenPartialHomeomorph.univBall_target x hŒµ] at h
        aesop)
      OpenPartialHomeomorph.contDiff_univBall
    have h' := d.isPlotOn_reparam (Metric.isOpen_ball) (Set.mapsTo_univ _ _)
      (d.isPlotOn_univ.mpr h) (OpenPartialHomeomorph.contDiffOn_univBall_symm (c := x) (r := Œµ))
    refine ‚ü®_, Metric.isOpen_ball, Metric.mem_ball_self hŒµ, (d.isPlotOn_congr _ ?_).mp h'‚ü©
    rw [Function.comp_assoc, ‚Üê OpenPartialHomeomorph.coe_trans]
    apply Set.EqOn.comp_left
    convert (OpenPartialHomeomorph.symm_trans_self (OpenPartialHomeomorph.univBall x Œµ)).2
    simp [OpenPartialHomeomorph.univBall_target x hŒµ]
  dTopology := d.dTopology
  isOpen_iff_preimages_plots := d.isOpen_iff_preimages_plots

end DiffeologicalSpace

namespace Diffeology

/-- Technical condition saying that the topology on a type agrees with the D-topology.
Necessary because the D-topologies on for example products and subspaces don't agree with
the product and subspace topologies. -/
class IsDTopologyCompatible (X : Type*) [t : TopologicalSpace X] [DiffeologicalSpace X] : Prop where
  dTop_eq (X) : dTopology = t

/-- A smooth function between spaces that are equipped with the D-topology is continuous. -/
protected theorem DSmooth.continuous' {X Y : Type*}
    [TopologicalSpace X] [DiffeologicalSpace X] [IsDTopologyCompatible X]
    [TopologicalSpace Y] [DiffeologicalSpace Y] [IsDTopologyCompatible Y]
    {f : X ‚Üí Y} (hf : DSmooth f) : Continuous f := by
  convert hf.continuous
  ¬∑ rw [IsDTopologyCompatible.dTop_eq X]
  ¬∑ rw [IsDTopologyCompatible.dTop_eq Y]

/-- Technical condition saying that the diffeology on a space is the one coming from
smoothness in the sense of `ContDiff ‚Ñù ‚àû`. Necessary as a hypothesis for some theorems
because some normed spaces carry a diffeology that is equal but not defeq to the normed space
diffeology (for example the product diffeology in the case of `Fin n ‚Üí ‚Ñù`), so the information
that these theorems still holds needs to be made available via this typeclass. -/
class IsContDiffCompatible (X : Type*)
    [NormedAddCommGroup X] [NormedSpace ‚Ñù X] [DiffeologicalSpace X] : Prop where
  isPlot_iff {n : ‚Ñï} {p : ùîº‚Åø ‚Üí X} : IsPlot p ‚Üî ContDiff ‚Ñù ‚àû p

/-- Diffeology on a finite-dimensional normed space. We make this a definition instead of an
instance because we also want to have product diffeologies as an instance, and having both would
cause instance diamonds on spaces like `Fin n ‚Üí ‚Ñù`. -/
def _root_.NormedSpace.toDiffeology (X : Type*)
    [NormedAddCommGroup X] [NormedSpace ‚Ñù X] [FiniteDimensional ‚Ñù X] :
    DiffeologicalSpace X :=
  .ofCorePlotsOn {
    isPlotOn := fun {_ u} _ p ‚Ü¶ ContDiffOn ‚Ñù ‚àû p u
    isPlotOn_congr := fun _ _ _ h ‚Ü¶ contDiffOn_congr h
    isPlot := fun p ‚Ü¶ ContDiff ‚Ñù ‚àû p
    isPlotOn_univ := contDiffOn_univ
    isPlot_const := fun _ ‚Ü¶ contDiff_const
    isPlotOn_reparam := fun _ _ _ h hp hf ‚Ü¶ hp.comp hf h.subset_preimage
    locality := fun _ _ h ‚Ü¶ fun x hxu ‚Ü¶ by
      let ‚ü®v, hv, hxv, hv'‚ü© := h x hxu
      exact ((hv' x hxv).contDiffAt (hv.mem_nhds hxv)).contDiffWithinAt
    dTopology := inferInstance
    isOpen_iff_preimages_plots := fun {u} ‚Ü¶ by
      refine ‚ü®fun hu _ _ hp ‚Ü¶ IsOpen.preimage (hp.continuous) hu, fun h ‚Ü¶ ?_‚ü©
      rw [‚Üê toEuclidean.preimage_symm_preimage u]
      exact toEuclidean.continuous.isOpen_preimage _ (h _ toEuclidean.symm.contDiff) }

attribute [local instance] NormedSpace.toDiffeology in
instance {X : Type*} [NormedAddCommGroup X] [NormedSpace ‚Ñù X] [FiniteDimensional ‚Ñù X] :
    IsContDiffCompatible X :=
  ‚ü®Iff.rfl‚ü©

lemma _root_.NormedSpace.isContDiffCompatible_iff_eq_toDiffeology {X : Type*}
    [NormedAddCommGroup X] [NormedSpace ‚Ñù X] [FiniteDimensional ‚Ñù X] [d : DiffeologicalSpace X] :
    IsContDiffCompatible X ‚Üî d = NormedSpace.toDiffeology X :=
  ‚ü®fun _ ‚Ü¶ by ext n p; exact IsContDiffCompatible.isPlot_iff, fun h ‚Ü¶ h ‚ñ∏ inferInstance‚ü©

attribute [local instance] NormedSpace.toDiffeology in
instance {X : Type*} [NormedAddCommGroup X] [NormedSpace ‚Ñù X] [FiniteDimensional ‚Ñù X] :
    IsDTopologyCompatible X :=
  ‚ü®rfl‚ü©

instance : DiffeologicalSpace ‚Ñù := NormedSpace.toDiffeology _

instance {Œπ : Type*} [Fintype Œπ] : DiffeologicalSpace (EuclideanSpace ‚Ñù Œπ) :=
  NormedSpace.toDiffeology _

variable {X : Type*} [DiffeologicalSpace X] {n : ‚Ñï}

@[fun_prop]
protected theorem IsPlot.dSmooth {p : ùîº‚Åø ‚Üí X} (hp : IsPlot p) : DSmooth p :=
  fun _ _ ‚Ü¶ isPlot_reparam hp

@[fun_prop]
protected theorem DSmooth.isPlot {p : ùîº‚Åø ‚Üí X} (hp : DSmooth p) : IsPlot p :=
  hp n id <| contDiff_id (n := ‚àû)

theorem isPlot_iff_dSmooth {p : ùîº‚Åø ‚Üí X} : IsPlot p ‚Üî DSmooth p :=
  ‚ü®IsPlot.dSmooth, DSmooth.isPlot‚ü©

lemma isPlot_id : IsPlot (@id ùîº‚Åø) := contDiff_id (n := ‚àû)

@[fun_prop]
lemma isPlot_id' : IsPlot fun x : ùîº‚Åø ‚Ü¶ x := isPlot_id

variable {Y : Type*}
  [NormedAddCommGroup X] [NormedSpace ‚Ñù X] [IsContDiffCompatible X]
  [NormedAddCommGroup Y] [NormedSpace ‚Ñù Y] [DiffeologicalSpace Y] [IsContDiffCompatible Y]

theorem isPlot_iff_contDiff {p : ùîº‚Åø ‚Üí X} : IsPlot p ‚Üî ContDiff ‚Ñù ‚àû p :=
  IsContDiffCompatible.isPlot_iff

@[fun_prop]
protected theorem _root_.ContDiff.isPlot {p : ùîº‚Åø ‚Üí X} (hp : ContDiff ‚Ñù ‚àû p) : IsPlot p :=
  isPlot_iff_contDiff.2 hp

@[fun_prop]
protected theorem IsPlot.contDiff {p : ùîº‚Åø ‚Üí X} (hp : IsPlot p) : ContDiff ‚Ñù ‚àû p :=
  isPlot_iff_contDiff.1 hp

@[fun_prop]
protected theorem _root_.ContDiff.dSmooth {f : X ‚Üí Y} (hf : ContDiff ‚Ñù ‚àû f) : DSmooth f :=
  fun _ _ hp ‚Ü¶ (hf.comp hp.contDiff).isPlot

@[fun_prop]
protected theorem DSmooth.contDiff [FiniteDimensional ‚Ñù X] {f : X ‚Üí Y} (hf : DSmooth f) :
    ContDiff ‚Ñù ‚àû f := by
  let g := toEuclidean (E := X)
  rw [‚Üê Function.comp_id f, ‚Üê g.symm_comp_self]
  exact (hf _ _ g.symm.contDiff.isPlot).contDiff.comp g.contDiff

theorem dSmooth_iff_contDiff [FiniteDimensional ‚Ñù X] {f : X ‚Üí Y} : DSmooth f ‚Üî ContDiff ‚Ñù ‚àû f :=
  ‚ü®DSmooth.contDiff, ContDiff.dSmooth‚ü©

end Diffeology
