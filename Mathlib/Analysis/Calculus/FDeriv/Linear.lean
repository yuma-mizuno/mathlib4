/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
module

public import Mathlib.Analysis.Calculus.FDeriv.Basic
public import Mathlib.Analysis.Normed.Operator.BoundedLinearMaps

/-!
# The derivative of bounded linear maps

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Mathlib/Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of
bounded linear maps.
-/

public section

open Asymptotics

namespace ContinuousLinearMap

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
variable {F : Type*} [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F]
variable (f : E â†’L[ğ•œ] F)
variable {x : E}
variable {s : Set E}
variable {L : Filter E}

/-!
### Bundled continuous linear maps

There are currently two variants of these in mathlib, the bundled version
(named `ContinuousLinearMap`, and denoted `E â†’L[ğ•œ] F`, works for topological vector spaces),
and the unbundled version (with a predicate `IsBoundedLinearMap`, requires normed spaces).
This section deals with the first form, see below for the unbundled version
-/

@[fun_prop]
protected theorem hasStrictFDerivAt : HasStrictFDerivAt f f x :=
  .of_isLittleOTVS <| (IsLittleOTVS.zero _ _).congr_left fun x => by
    simp only [f.map_sub, sub_self, Pi.zero_apply]

protected theorem hasFDerivAtFilter : HasFDerivAtFilter f f x L :=
  .of_isLittleOTVS <| (IsLittleOTVS.zero _ _).congr_left fun x => by
    simp only [f.map_sub, sub_self, Pi.zero_apply]

@[fun_prop]
protected theorem hasFDerivWithinAt : HasFDerivWithinAt f f s x :=
  f.hasFDerivAtFilter

@[fun_prop]
protected theorem hasFDerivAt : HasFDerivAt f f x :=
  f.hasFDerivAtFilter

@[simp, fun_prop]
protected theorem differentiableAt : DifferentiableAt ğ•œ f x :=
  f.hasFDerivAt.differentiableAt

@[fun_prop]
protected theorem differentiableWithinAt : DifferentiableWithinAt ğ•œ f s x :=
  f.differentiableAt.differentiableWithinAt

@[simp, fun_prop]
protected theorem differentiable : Differentiable ğ•œ f := fun _ =>
  f.differentiableAt

@[fun_prop]
protected theorem differentiableOn : DifferentiableOn ğ•œ f s :=
  f.differentiable.differentiableOn

variable [ContinuousAdd E] [ContinuousSMul ğ•œ E] [ContinuousAdd F] [ContinuousSMul ğ•œ F] [T2Space F]

@[simp]
protected theorem fderiv : fderiv ğ•œ f x = f :=
  f.hasFDerivAt.fderiv

protected theorem fderivWithin (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ f s x = f := by
  rw [DifferentiableAt.fderivWithin f.differentiableAt hxs]
  exact f.fderiv

end ContinuousLinearMap

/-! ### Unbundled continuous linear maps -/

namespace IsBoundedLinearMap
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {f : E â†’ F}
variable {x : E}
variable {s : Set E}
variable {L : Filter E}

theorem hasFDerivAtFilter (h : IsBoundedLinearMap ğ•œ f) :
    HasFDerivAtFilter f h.toContinuousLinearMap x L :=
  h.toContinuousLinearMap.hasFDerivAtFilter

@[fun_prop]
theorem hasFDerivWithinAt (h : IsBoundedLinearMap ğ•œ f) :
    HasFDerivWithinAt f h.toContinuousLinearMap s x :=
  h.hasFDerivAtFilter

@[fun_prop]
theorem hasFDerivAt (h : IsBoundedLinearMap ğ•œ f) :
    HasFDerivAt f h.toContinuousLinearMap x :=
  h.hasFDerivAtFilter

@[fun_prop]
theorem differentiableAt (h : IsBoundedLinearMap ğ•œ f) : DifferentiableAt ğ•œ f x :=
  h.hasFDerivAt.differentiableAt

@[fun_prop]
theorem differentiableWithinAt (h : IsBoundedLinearMap ğ•œ f) :
    DifferentiableWithinAt ğ•œ f s x :=
  h.differentiableAt.differentiableWithinAt

protected theorem fderiv (h : IsBoundedLinearMap ğ•œ f) :
    fderiv ğ•œ f x = h.toContinuousLinearMap :=
  HasFDerivAt.fderiv h.hasFDerivAt

protected theorem fderivWithin (h : IsBoundedLinearMap ğ•œ f)
    (hxs : UniqueDiffWithinAt ğ•œ s x) : fderivWithin ğ•œ f s x = h.toContinuousLinearMap := by
  rw [DifferentiableAt.fderivWithin h.differentiableAt hxs]
  exact h.fderiv

@[fun_prop]
theorem differentiable (h : IsBoundedLinearMap ğ•œ f) : Differentiable ğ•œ f :=
  fun _ => h.differentiableAt

@[fun_prop]
theorem differentiableOn (h : IsBoundedLinearMap ğ•œ f) : DifferentiableOn ğ•œ f s :=
  h.differentiable.differentiableOn

end IsBoundedLinearMap
