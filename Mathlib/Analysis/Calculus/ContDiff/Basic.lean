/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
module

public import Mathlib.Analysis.Calculus.ContDiff.Defs
public import Mathlib.Analysis.Calculus.FDeriv.Affine

/-!
# Basic properties of continuously-differentiable functions

This file continues the development of the API for `ContDiff`, `ContDiffAt`, etc, covering
constants, products, composition with linear maps, etc.

## Tags

derivative, differentiability, higher derivative, `C^n`, multilinear, Taylor series, formal series
-/

public noncomputable section

open Set Fin Filter Function

open scoped Topology ContDiff

attribute [local instance 1001] NormedAddCommGroup.toAddCommGroup AddCommGroup.toAddCommMonoid

variable {ğ•œ E F G : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {s t : Set E} {f : E â†’ F} {x : E} {b : E Ã— F â†’ G} {m n : WithTop â„•âˆ}
  {p : E â†’ FormalMultilinearSeries ğ•œ E F}

/-! ### Constants -/
section constants

theorem iteratedFDerivWithin_succ_const (n : â„•) (c : F) :
    iteratedFDerivWithin ğ•œ (n + 1) (fun _ : E â†¦ c) s = 0 := by
  induction n with
  | zero =>
    ext1
    simp [iteratedFDerivWithin_succ_eq_comp_left, iteratedFDerivWithin_zero_eq_comp, comp_def]
  | succ n IH =>
    rw [iteratedFDerivWithin_succ_eq_comp_left, IH]
    simp only [Pi.zero_def, comp_def, fderivWithin_fun_const, map_zero]

@[simp]
theorem iteratedFDerivWithin_zero_fun {i : â„•} :
    iteratedFDerivWithin ğ•œ i (fun _ : E â†¦ (0 : F)) s = 0 := by
  cases i with
  | zero => ext; simp
  | succ i => apply iteratedFDerivWithin_succ_const

@[simp]
theorem iteratedFDeriv_zero_fun {n : â„•} : (iteratedFDeriv ğ•œ n fun _ : E â†¦ (0 : F)) = 0 :=
  funext fun x â†¦ by simp only [â† iteratedFDerivWithin_univ, iteratedFDerivWithin_zero_fun]

theorem contDiff_zero_fun : ContDiff ğ•œ n fun _ : E => (0 : F) :=
  analyticOnNhd_const.contDiff

/-- Constants are `C^âˆ`. -/
@[fun_prop]
theorem contDiff_const {c : F} : ContDiff ğ•œ n fun _ : E => c :=
  analyticOnNhd_const.contDiff

@[fun_prop]
theorem contDiffOn_const {c : F} {s : Set E} : ContDiffOn ğ•œ n (fun _ : E => c) s :=
  contDiff_const.contDiffOn

@[fun_prop]
theorem contDiffAt_const {c : F} : ContDiffAt ğ•œ n (fun _ : E => c) x :=
  contDiff_const.contDiffAt

@[fun_prop]
theorem contDiffWithinAt_const {c : F} : ContDiffWithinAt ğ•œ n (fun _ : E => c) s x :=
  contDiffAt_const.contDiffWithinAt

@[nontriviality]
theorem contDiff_of_subsingleton [Subsingleton F] : ContDiff ğ•œ n f := by
  rw [Subsingleton.elim f fun _ => 0]; exact contDiff_const

@[nontriviality]
theorem contDiffAt_of_subsingleton [Subsingleton F] : ContDiffAt ğ•œ n f x := by
  rw [Subsingleton.elim f fun _ => 0]; exact contDiffAt_const

@[nontriviality]
theorem contDiffWithinAt_of_subsingleton [Subsingleton F] : ContDiffWithinAt ğ•œ n f s x := by
  rw [Subsingleton.elim f fun _ => 0]; exact contDiffWithinAt_const

@[nontriviality]
theorem contDiffOn_of_subsingleton [Subsingleton F] : ContDiffOn ğ•œ n f s := by
  rw [Subsingleton.elim f fun _ => 0]; exact contDiffOn_const

theorem iteratedFDerivWithin_const_of_ne {n : â„•} (hn : n â‰  0) (c : F) (s : Set E) :
    iteratedFDerivWithin ğ•œ n (fun _ : E â†¦ c) s = 0 := by
  cases n with
  | zero => contradiction
  | succ n => exact iteratedFDerivWithin_succ_const n c

theorem iteratedFDeriv_const_of_ne {n : â„•} (hn : n â‰  0) (c : F) :
    (iteratedFDeriv ğ•œ n fun _ : E â†¦ c) = 0 := by
  simp only [â† iteratedFDerivWithin_univ, iteratedFDerivWithin_const_of_ne hn]

theorem iteratedFDeriv_succ_const (n : â„•) (c : F) :
    (iteratedFDeriv ğ•œ (n + 1) fun _ : E â†¦ c) = 0 :=
  iteratedFDeriv_const_of_ne (by simp) _

theorem contDiffWithinAt_singleton : ContDiffWithinAt ğ•œ n f {x} x :=
  (contDiffWithinAt_const (c := f x)).congr (by simp) rfl

end constants

/-! ### Smoothness of linear functions -/
section linear

/-- Unbundled bounded linear functions are `C^n`. -/
theorem IsBoundedLinearMap.contDiff (hf : IsBoundedLinearMap ğ•œ f) : ContDiff ğ•œ n f :=
  (ContinuousLinearMap.analyticOnNhd hf.toContinuousLinearMap univ).contDiff

@[fun_prop]
theorem ContinuousLinearMap.contDiff (f : E â†’L[ğ•œ] F) : ContDiff ğ•œ n f :=
  f.isBoundedLinearMap.contDiff

@[fun_prop]
theorem ContinuousLinearEquiv.contDiff (f : E â‰ƒL[ğ•œ] F) : ContDiff ğ•œ n f :=
  (f : E â†’L[ğ•œ] F).contDiff

@[fun_prop]
theorem LinearIsometry.contDiff (f : E â†’â‚—áµ¢[ğ•œ] F) : ContDiff ğ•œ n f :=
  f.toContinuousLinearMap.contDiff

@[fun_prop]
theorem LinearIsometryEquiv.contDiff (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) : ContDiff ğ•œ n f :=
  (f : E â†’L[ğ•œ] F).contDiff

/-- The identity is `C^n`. -/
theorem contDiff_id : ContDiff ğ•œ n (id : E â†’ E) :=
  IsBoundedLinearMap.id.contDiff

@[fun_prop]
theorem contDiff_fun_id : ContDiff ğ•œ n (fun x : E => x) :=
  IsBoundedLinearMap.id.contDiff

theorem contDiffWithinAt_id {s x} : ContDiffWithinAt ğ•œ n (id : E â†’ E) s x :=
  contDiff_id.contDiffWithinAt

@[fun_prop]
theorem contDiffWithinAt_fun_id {s x} : ContDiffWithinAt ğ•œ n (fun x : E => x) s x :=
  contDiff_id.contDiffWithinAt

theorem contDiffAt_id {x} : ContDiffAt ğ•œ n (id : E â†’ E) x :=
  contDiff_id.contDiffAt

@[fun_prop]
theorem contDiffAt_fun_id {x} : ContDiffAt ğ•œ n (fun x : E => x) x :=
  contDiff_id.contDiffAt

theorem contDiffOn_id {s} : ContDiffOn ğ•œ n (id : E â†’ E) s :=
  contDiff_id.contDiffOn

@[fun_prop]
theorem contDiffOn_fun_id {s} : ContDiffOn ğ•œ n (fun x : E => x) s :=
  contDiff_id.contDiffOn

/-- Bilinear functions are `C^n`. -/
theorem IsBoundedBilinearMap.contDiff (hb : IsBoundedBilinearMap ğ•œ b) : ContDiff ğ•œ n b :=
  (hb.toContinuousLinearMap.analyticOnNhd_bilinear _).contDiff

/-- If `f` admits a Taylor series `p` in a set `s`, and `g` is linear, then `g âˆ˜ f` admits a Taylor
series whose `k`-th term is given by `g âˆ˜ (p k)`. -/
theorem HasFTaylorSeriesUpToOn.continuousLinearMap_comp {n : WithTop â„•âˆ} (g : F â†’L[ğ•œ] G)
    (hf : HasFTaylorSeriesUpToOn n f p s) :
    HasFTaylorSeriesUpToOn n (g âˆ˜ f) (fun x k => g.compContinuousMultilinearMap (p x k)) s where
  zero_eq x hx := congr_arg g (hf.zero_eq x hx)
  fderivWithin m hm x hx := (ContinuousLinearMap.compContinuousMultilinearMapL ğ•œ
    (fun _ : Fin m => E) F G g).hasFDerivAt.comp_hasFDerivWithinAt x (hf.fderivWithin m hm x hx)
  cont m hm := (ContinuousLinearMap.compContinuousMultilinearMapL ğ•œ
    (fun _ : Fin m => E) F G g).continuous.comp_continuousOn (hf.cont m hm)

/-- Composition by continuous linear maps on the left preserves `C^n` functions in a domain
at a point. -/
theorem ContDiffWithinAt.continuousLinearMap_comp (g : F â†’L[ğ•œ] G)
    (hf : ContDiffWithinAt ğ•œ n f s x) : ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x := by
  match n with
  | Ï‰ =>
    obtain âŸ¨u, hu, p, hp, h'pâŸ© := hf
    refine âŸ¨u, hu, _, hp.continuousLinearMap_comp g, fun i â†¦ ?_âŸ©
    change AnalyticOn ğ•œ
      (fun x â†¦ (ContinuousLinearMap.compContinuousMultilinearMapL ğ•œ
      (fun _ : Fin i â†¦ E) F G g) (p x i)) u
    apply AnalyticOnNhd.comp_analyticOn _ (h'p i) (Set.mapsTo_univ _ _)
    exact ContinuousLinearMap.analyticOnNhd _ _
  | (n : â„•âˆ) =>
    intro m hm
    rcases hf m hm with âŸ¨u, hu, p, hpâŸ©
    exact âŸ¨u, hu, _, hp.continuousLinearMap_comp gâŸ©

/-- Composition by continuous linear maps on the left preserves `C^n` functions in a domain
at a point. -/
theorem ContDiffAt.continuousLinearMap_comp (g : F â†’L[ğ•œ] G) (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (g âˆ˜ f) x :=
  ContDiffWithinAt.continuousLinearMap_comp g hf

/-- Composition by continuous linear maps on the left preserves `C^n` functions on domains. -/
theorem ContDiffOn.continuousLinearMap_comp (g : F â†’L[ğ•œ] G) (hf : ContDiffOn ğ•œ n f s) :
    ContDiffOn ğ•œ n (g âˆ˜ f) s := fun x hx => (hf x hx).continuousLinearMap_comp g

/-- Composition by continuous linear maps on the left preserves `C^n` functions. -/
theorem ContDiff.continuousLinearMap_comp {f : E â†’ F} (g : F â†’L[ğ•œ] G) (hf : ContDiff ğ•œ n f) :
    ContDiff ğ•œ n fun x => g (f x) :=
  contDiffOn_univ.1 <| ContDiffOn.continuousLinearMap_comp _ (contDiffOn_univ.2 hf)

/-- The iterated derivative within a set of the composition with a linear map on the left is
obtained by applying the linear map to the iterated derivative. -/
theorem ContinuousLinearMap.iteratedFDerivWithin_comp_left {f : E â†’ F} (g : F â†’L[ğ•œ] G)
    (hf : ContDiffWithinAt ğ•œ n f s x) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      g.compContinuousMultilinearMap (iteratedFDerivWithin ğ•œ i f s x) := by
  rcases hf.contDiffOn' hi (by simp) with âŸ¨U, hU, hxU, hfUâŸ©
  rw [â† iteratedFDerivWithin_inter_open hU hxU, â† iteratedFDerivWithin_inter_open (f := f) hU hxU]
  rw [insert_eq_of_mem hx] at hfU
  exact .symm <| (hfU.ftaylorSeriesWithin (hs.inter hU)).continuousLinearMap_comp g
    |>.eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl (hs.inter hU) âŸ¨hx, hxUâŸ©

/-- The iterated derivative of the composition with a linear map on the left is
obtained by applying the linear map to the iterated derivative. -/
theorem ContinuousLinearMap.iteratedFDeriv_comp_left {f : E â†’ F} (g : F â†’L[ğ•œ] G)
    (hf : ContDiffAt ğ•œ n f x) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDeriv ğ•œ i (g âˆ˜ f) x = g.compContinuousMultilinearMap (iteratedFDeriv ğ•œ i f x) := by
  simp only [â† iteratedFDerivWithin_univ]
  exact g.iteratedFDerivWithin_comp_left hf.contDiffWithinAt uniqueDiffOn_univ (mem_univ x) hi

/-- The iterated derivative within a set of the composition with a linear equiv on the left is
obtained by applying the linear equiv to the iterated derivative. This is true without
differentiability assumptions. -/
theorem ContinuousLinearEquiv.iteratedFDerivWithin_comp_left (g : F â‰ƒL[ğ•œ] G) (f : E â†’ F)
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (i : â„•) :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      (g : F â†’L[ğ•œ] G).compContinuousMultilinearMap (iteratedFDerivWithin ğ•œ i f s x) := by
  induction i generalizing x with ext1 m
  | zero =>
    simp only [iteratedFDerivWithin_zero_apply, comp_apply,
      ContinuousLinearMap.compContinuousMultilinearMap_coe, coe_coe]
  | succ i IH =>
    rw [iteratedFDerivWithin_succ_apply_left]
    have Z : fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s) s x =
        fderivWithin ğ•œ (g.continuousMultilinearMapCongrRight (fun _ : Fin i => E) âˆ˜
          iteratedFDerivWithin ğ•œ i f s) s x :=
      fderivWithin_congr' (@IH) hx
    simp_rw [Z]
    rw [(g.continuousMultilinearMapCongrRight fun _ : Fin i => E).comp_fderivWithin (hs x hx)]
    simp only [ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe, comp_apply,
      ContinuousLinearEquiv.continuousMultilinearMapCongrRight_apply,
      ContinuousLinearMap.compContinuousMultilinearMap_coe, EmbeddingLike.apply_eq_iff_eq]
    rw [iteratedFDerivWithin_succ_apply_left]

/-- Iterated derivatives commute with left composition by continuous linear equivalences. -/
theorem ContinuousLinearEquiv.iteratedFDeriv_comp_left {f : E â†’ F} {x : E} (g : F â‰ƒL[ğ•œ] G) {i : â„•} :
    iteratedFDeriv ğ•œ i (g âˆ˜ f) x =
      g.toContinuousLinearMap.compContinuousMultilinearMap (iteratedFDeriv ğ•œ i f x) := by
  simp only [â† iteratedFDerivWithin_univ]
  apply g.iteratedFDerivWithin_comp_left f uniqueDiffOn_univ trivial

/-- Composition with a linear isometry on the left preserves the norm of the iterated
derivative within a set. -/
theorem LinearIsometry.norm_iteratedFDerivWithin_comp_left {f : E â†’ F} (g : F â†’â‚—áµ¢[ğ•œ] G)
    (hf : ContDiffWithinAt ğ•œ n f s x) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {i : â„•} (hi : i â‰¤ n) :
    â€–iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s xâ€– = â€–iteratedFDerivWithin ğ•œ i f s xâ€– := by
  have :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      g.toContinuousLinearMap.compContinuousMultilinearMap (iteratedFDerivWithin ğ•œ i f s x) :=
    g.toContinuousLinearMap.iteratedFDerivWithin_comp_left hf hs hx hi
  rw [this]
  apply LinearIsometry.norm_compContinuousMultilinearMap

/-- Composition with a linear isometry on the left preserves the norm of the iterated
derivative. -/
theorem LinearIsometry.norm_iteratedFDeriv_comp_left {f : E â†’ F} (g : F â†’â‚—áµ¢[ğ•œ] G)
    (hf : ContDiffAt ğ•œ n f x) {i : â„•} (hi : i â‰¤ n) :
    â€–iteratedFDeriv ğ•œ i (g âˆ˜ f) xâ€– = â€–iteratedFDeriv ğ•œ i f xâ€– := by
  simp only [â† iteratedFDerivWithin_univ]
  exact g.norm_iteratedFDerivWithin_comp_left hf.contDiffWithinAt uniqueDiffOn_univ (mem_univ x) hi

/-- Composition with a linear isometry equiv on the left preserves the norm of the iterated
derivative within a set. -/
theorem LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left (g : F â‰ƒâ‚—áµ¢[ğ•œ] G) (f : E â†’ F)
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (i : â„•) :
    â€–iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s xâ€– = â€–iteratedFDerivWithin ğ•œ i f s xâ€– := by
  have :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      (g : F â†’L[ğ•œ] G).compContinuousMultilinearMap (iteratedFDerivWithin ğ•œ i f s x) :=
    g.toContinuousLinearEquiv.iteratedFDerivWithin_comp_left f hs hx i
  rw [this]
  apply LinearIsometry.norm_compContinuousMultilinearMap g.toLinearIsometry

/-- Composition with a linear isometry equiv on the left preserves the norm of the iterated
derivative. -/
theorem LinearIsometryEquiv.norm_iteratedFDeriv_comp_left (g : F â‰ƒâ‚—áµ¢[ğ•œ] G) (f : E â†’ F) (x : E)
    (i : â„•) : â€–iteratedFDeriv ğ•œ i (g âˆ˜ f) xâ€– = â€–iteratedFDeriv ğ•œ i f xâ€– := by
  rw [â† iteratedFDerivWithin_univ, â† iteratedFDerivWithin_univ]
  apply g.norm_iteratedFDerivWithin_comp_left f uniqueDiffOn_univ (mem_univ x) i

/-- Composition by continuous linear equivs on the left respects higher differentiability at a
point in a domain. -/
theorem ContinuousLinearEquiv.comp_contDiffWithinAt_iff (e : F â‰ƒL[ğ•œ] G) :
    ContDiffWithinAt ğ•œ n (e âˆ˜ f) s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun H => by
    simpa only [Function.comp_def, e.symm.coe_coe, e.symm_apply_apply] using
      H.continuousLinearMap_comp (e.symm : G â†’L[ğ•œ] F),
    fun H => H.continuousLinearMap_comp (e : F â†’L[ğ•œ] G)âŸ©

/-- Composition by continuous linear equivs on the left respects higher differentiability at a
point. -/
theorem ContinuousLinearEquiv.comp_contDiffAt_iff (e : F â‰ƒL[ğ•œ] G) :
    ContDiffAt ğ•œ n (e âˆ˜ f) x â†” ContDiffAt ğ•œ n f x := by
  simp only [â† contDiffWithinAt_univ, e.comp_contDiffWithinAt_iff]

/-- Composition by continuous linear equivs on the left respects higher differentiability on
domains. -/
theorem ContinuousLinearEquiv.comp_contDiffOn_iff (e : F â‰ƒL[ğ•œ] G) :
    ContDiffOn ğ•œ n (e âˆ˜ f) s â†” ContDiffOn ğ•œ n f s := by
  simp [ContDiffOn, e.comp_contDiffWithinAt_iff]

/-- Composition by continuous linear equivs on the left respects higher differentiability. -/
theorem ContinuousLinearEquiv.comp_contDiff_iff (e : F â‰ƒL[ğ•œ] G) :
    ContDiff ğ•œ n (e âˆ˜ f) â†” ContDiff ğ•œ n f := by
  simp only [â† contDiffOn_univ, e.comp_contDiffOn_iff]

/-- If `f` admits a Taylor series `p` in a set `s`, and `g` is affine, then `f âˆ˜ g` admits a Taylor
series in `g â»Â¹' s`, whose `k`-th term at `x` is given
by `p (g x) k (g.contLinear vâ‚, ..., g.contLinear vâ‚–)` . -/
theorem HasFTaylorSeriesUpToOn.comp_continuousAffineMap
    (hf : HasFTaylorSeriesUpToOn n f p s) (g : G â†’á´¬[ğ•œ] E) :
    HasFTaylorSeriesUpToOn n (f âˆ˜ g)
      (fun x k => (p (g x) k).compContinuousLinearMap fun _ => g.contLinear) (g â»Â¹' s) := by
  let A : âˆ€ m : â„•, (E [Ã—m]â†’L[ğ•œ] F) â†’ G [Ã—m]â†’L[ğ•œ] F :=
    fun m h â†¦ h.compContinuousLinearMap fun _ â†¦ g.contLinear
  have hA : âˆ€ m, IsBoundedLinearMap ğ•œ (A m) := fun m =>
    isBoundedLinearMap_continuousMultilinearMap_comp_linear g.contLinear
  constructor
  Â· intro x hx
    simp only [(hf.zero_eq (g x) hx).symm, Function.comp_apply]
    change (p (g x) 0 fun _ : Fin 0 => g.contLinear 0) = p (g x) 0 0
    rw [map_zero]
    rfl
  Â· intro m hm x hx
    convert (hA m).hasFDerivAt.comp_hasFDerivWithinAt x
        ((hf.fderivWithin m hm (g x) hx).comp x g.hasFDerivWithinAt (Subset.refl _))
    ext y v
    change p (g x) (Nat.succ m) (g.contLinear âˆ˜ cons y v)
      = p (g x) m.succ (cons (g.contLinear y) (g.contLinear âˆ˜ v))
    rw [comp_cons]
  Â· intro m hm
    exact (hA m).continuous.comp_continuousOn <| (hf.cont m hm).comp g.continuous.continuousOn <|
      Subset.refl _

/-- If `f` admits a Taylor series `p` in a set `s`, and `g` is linear, then `f âˆ˜ g` admits a Taylor
series in `g â»Â¹' s`, whose `k`-th term at `x` is given by `p (g x) k (g vâ‚, ..., g vâ‚–)` . -/
theorem HasFTaylorSeriesUpToOn.compContinuousLinearMap
    (hf : HasFTaylorSeriesUpToOn n f p s) (g : G â†’L[ğ•œ] E) :
    HasFTaylorSeriesUpToOn n (f âˆ˜ g)
      (fun x k => (p (g x) k).compContinuousLinearMap fun _ => g) (g â»Â¹' s) :=
  hf.comp_continuousAffineMap g.toContinuousAffineMap

/-- Composition by continuous linear maps on the right preserves `C^n` functions at a point on
a domain. -/
theorem ContDiffWithinAt.comp_continuousLinearMap {x : G} (g : G â†’L[ğ•œ] E)
    (hf : ContDiffWithinAt ğ•œ n f s (g x)) : ContDiffWithinAt ğ•œ n (f âˆ˜ g) (g â»Â¹' s) x := by
  match n with
  | Ï‰ =>
    obtain âŸ¨u, hu, p, hp, h'pâŸ© := hf
    refine âŸ¨g â»Â¹' u, ?_, _, hp.compContinuousLinearMap g, ?_âŸ©
    Â· refine g.continuous.continuousWithinAt.tendsto_nhdsWithin ?_ hu
      exact (mapsTo_singleton.2 <| mem_singleton _).union_union (mapsTo_preimage _ _)
    Â· intro i
      change AnalyticOn ğ•œ (fun x â†¦
        ContinuousMultilinearMap.compContinuousLinearMapL (fun _ â†¦ g) (p (g x) i)) (â‡‘g â»Â¹' u)
      apply AnalyticOn.comp _ _ (Set.mapsTo_univ _ _)
      Â· exact ContinuousLinearMap.analyticOn _ _
      Â· exact (h'p i).comp (g.analyticOn _) (mapsTo_preimage _ _)
  | (n : â„•âˆ) =>
    intro m hm
    rcases hf m hm with âŸ¨u, hu, p, hpâŸ©
    refine âŸ¨g â»Â¹' u, ?_, _, hp.compContinuousLinearMap gâŸ©
    refine g.continuous.continuousWithinAt.tendsto_nhdsWithin ?_ hu
    exact (mapsTo_singleton.2 <| mem_singleton _).union_union (mapsTo_preimage _ _)

/-- Composition by continuous linear maps on the right preserves `C^n` functions on domains. -/
theorem ContDiffOn.comp_continuousLinearMap (hf : ContDiffOn ğ•œ n f s) (g : G â†’L[ğ•œ] E) :
    ContDiffOn ğ•œ n (f âˆ˜ g) (g â»Â¹' s) := fun x hx => (hf (g x) hx).comp_continuousLinearMap g

/-- Composition by continuous linear maps on the right preserves `C^n` functions. -/
theorem ContDiff.comp_continuousLinearMap {f : E â†’ F} {g : G â†’L[ğ•œ] E} (hf : ContDiff ğ•œ n f) :
    ContDiff ğ•œ n (f âˆ˜ g) :=
  contDiffOn_univ.1 <| ContDiffOn.comp_continuousLinearMap (contDiffOn_univ.2 hf) _

/-- The iterated derivative within a set of the composition with a linear map on the right is
obtained by composing the iterated derivative with the linear map. -/
theorem ContinuousLinearMap.iteratedFDerivWithin_comp_right {f : E â†’ F} (g : G â†’L[ğ•œ] E)
    (hf : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s) (h's : UniqueDiffOn ğ•œ (g â»Â¹' s)) {x : G}
    (hx : g x âˆˆ s) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s) x =
      (iteratedFDerivWithin ğ•œ i f s (g x)).compContinuousLinearMap fun _ => g :=
  ((((hf.of_le hi).ftaylorSeriesWithin hs).compContinuousLinearMap
    g).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl h's hx).symm

/-- The iterated derivative within a set of the composition with a linear equiv on the right is
obtained by composing the iterated derivative with the linear equiv. -/
theorem ContinuousLinearEquiv.iteratedFDerivWithin_comp_right (g : G â‰ƒL[ğ•œ] E) (f : E â†’ F)
    (hs : UniqueDiffOn ğ•œ s) {x : G} (hx : g x âˆˆ s) (i : â„•) :
    iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s) x =
      (iteratedFDerivWithin ğ•œ i f s (g x)).compContinuousLinearMap fun _ => g := by
  induction i generalizing x with ext1 m
  | zero =>
    simp only [iteratedFDerivWithin_zero_apply, comp_apply,
      ContinuousMultilinearMap.compContinuousLinearMap_apply]
  | succ i IH =>
    simp only [ContinuousMultilinearMap.compContinuousLinearMap_apply,
      ContinuousLinearEquiv.coe_coe, iteratedFDerivWithin_succ_apply_left]
    have : fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s)) (g â»Â¹' s) x =
        fderivWithin ğ•œ
          (ContinuousLinearEquiv.continuousMultilinearMapCongrLeft _ (fun _x : Fin i => g) âˆ˜
            (iteratedFDerivWithin ğ•œ i f s âˆ˜ g)) (g â»Â¹' s) x :=
      fderivWithin_congr' (@IH) hx
    rw [this, ContinuousLinearEquiv.comp_fderivWithin _ (g.uniqueDiffOn_preimage_iff.2 hs x hx)]
    simp only [ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe, comp_apply,
      ContinuousLinearEquiv.continuousMultilinearMapCongrLeft_apply,
      ContinuousMultilinearMap.compContinuousLinearMap_apply]
    rw [ContinuousLinearEquiv.comp_right_fderivWithin _ (g.uniqueDiffOn_preimage_iff.2 hs x hx),
      ContinuousLinearMap.coe_comp', coe_coe, comp_apply, tail_def, tail_def]

/-- The iterated derivative of the composition with a linear map on the right is
obtained by composing the iterated derivative with the linear map. -/
theorem ContinuousLinearMap.iteratedFDeriv_comp_right (g : G â†’L[ğ•œ] E) {f : E â†’ F}
    (hf : ContDiff ğ•œ n f) (x : G) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDeriv ğ•œ i (f âˆ˜ g) x =
      (iteratedFDeriv ğ•œ i f (g x)).compContinuousLinearMap fun _ => g := by
  simp only [â† iteratedFDerivWithin_univ]
  exact g.iteratedFDerivWithin_comp_right hf.contDiffOn uniqueDiffOn_univ uniqueDiffOn_univ
      (mem_univ _) hi

/-- Composition with a linear isometry on the right preserves the norm of the iterated derivative
within a set. -/
theorem LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right (g : G â‰ƒâ‚—áµ¢[ğ•œ] E) (f : E â†’ F)
    (hs : UniqueDiffOn ğ•œ s) {x : G} (hx : g x âˆˆ s) (i : â„•) :
    â€–iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s) xâ€– = â€–iteratedFDerivWithin ğ•œ i f s (g x)â€– := by
  have : iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s) x =
      (iteratedFDerivWithin ğ•œ i f s (g x)).compContinuousLinearMap fun _ => g :=
    g.toContinuousLinearEquiv.iteratedFDerivWithin_comp_right f hs hx i
  rw [this, ContinuousMultilinearMap.norm_compContinuous_linearIsometryEquiv]

/-- Composition with a linear isometry on the right preserves the norm of the iterated derivative
within a set. -/
theorem LinearIsometryEquiv.norm_iteratedFDeriv_comp_right (g : G â‰ƒâ‚—áµ¢[ğ•œ] E) (f : E â†’ F) (x : G)
    (i : â„•) : â€–iteratedFDeriv ğ•œ i (f âˆ˜ g) xâ€– = â€–iteratedFDeriv ğ•œ i f (g x)â€– := by
  simp only [â† iteratedFDerivWithin_univ]
  apply g.norm_iteratedFDerivWithin_comp_right f uniqueDiffOn_univ (mem_univ (g x)) i

/-- Composition by continuous linear equivs on the right respects higher differentiability at a
point in a domain. -/
theorem ContinuousLinearEquiv.contDiffWithinAt_comp_iff (e : G â‰ƒL[ğ•œ] E) :
    ContDiffWithinAt ğ•œ n (f âˆ˜ e) (e â»Â¹' s) (e.symm x) â†” ContDiffWithinAt ğ•œ n f s x := by
  constructor
  Â· intro H
    simpa [â† preimage_comp, Function.comp_def] using H.comp_continuousLinearMap (e.symm : E â†’L[ğ•œ] G)
  Â· intro H
    rw [â† e.apply_symm_apply x, â† e.coe_coe] at H
    exact H.comp_continuousLinearMap _

/-- Composition by continuous linear equivs on the right respects higher differentiability at a
point. -/
theorem ContinuousLinearEquiv.contDiffAt_comp_iff (e : G â‰ƒL[ğ•œ] E) :
    ContDiffAt ğ•œ n (f âˆ˜ e) (e.symm x) â†” ContDiffAt ğ•œ n f x := by
  rw [â† contDiffWithinAt_univ, â† contDiffWithinAt_univ, â† preimage_univ]
  exact e.contDiffWithinAt_comp_iff

/-- Composition by continuous linear equivs on the right respects higher differentiability on
domains. -/
theorem ContinuousLinearEquiv.contDiffOn_comp_iff (e : G â‰ƒL[ğ•œ] E) :
    ContDiffOn ğ•œ n (f âˆ˜ e) (e â»Â¹' s) â†” ContDiffOn ğ•œ n f s :=
  âŸ¨fun H => by simpa [Function.comp_def] using H.comp_continuousLinearMap (e.symm : E â†’L[ğ•œ] G),
    fun H => H.comp_continuousLinearMap (e : G â†’L[ğ•œ] E)âŸ©

/-- Composition by continuous linear equivs on the right respects higher differentiability. -/
theorem ContinuousLinearEquiv.contDiff_comp_iff (e : G â‰ƒL[ğ•œ] E) :
    ContDiff ğ•œ n (f âˆ˜ e) â†” ContDiff ğ•œ n f := by
  rw [â† contDiffOn_univ, â† contDiffOn_univ, â† preimage_univ]
  exact e.contDiffOn_comp_iff

end linear

/-! ### The Cartesian product of two C^n functions is C^n. -/
section prod

/-- If two functions `f` and `g` admit Taylor series `p` and `q` in a set `s`, then the Cartesian
product of `f` and `g` admits the Cartesian product of `p` and `q` as a Taylor series. -/
theorem HasFTaylorSeriesUpToOn.prodMk {n : WithTop â„•âˆ}
    (hf : HasFTaylorSeriesUpToOn n f p s) {g : E â†’ G}
    {q : E â†’ FormalMultilinearSeries ğ•œ E G} (hg : HasFTaylorSeriesUpToOn n g q s) :
    HasFTaylorSeriesUpToOn n (fun y => (f y, g y)) (fun y k => (p y k).prod (q y k)) s := by
  set L := fun m => ContinuousMultilinearMap.prodL ğ•œ (fun _ : Fin m => E) F G
  constructor
  Â· intro x hx; rw [â† hf.zero_eq x hx, â† hg.zero_eq x hx]; rfl
  Â· intro m hm x hx
    convert (L m).hasFDerivAt.comp_hasFDerivWithinAt x
        ((hf.fderivWithin m hm x hx).prodMk (hg.fderivWithin m hm x hx))
  Â· intro m hm
    exact (L m).continuous.comp_continuousOn ((hf.cont m hm).prodMk (hg.cont m hm))

/-- The Cartesian product of `C^n` functions at a point in a domain is `C^n`. -/
@[fun_prop]
theorem ContDiffWithinAt.prodMk {s : Set E} {f : E â†’ F} {g : E â†’ G}
    (hf : ContDiffWithinAt ğ•œ n f s x) (hg : ContDiffWithinAt ğ•œ n g s x) :
    ContDiffWithinAt ğ•œ n (fun x : E => (f x, g x)) s x := by
  match n with
  | Ï‰ =>
    obtain âŸ¨u, hu, p, hp, h'pâŸ© := hf
    obtain âŸ¨v, hv, q, hq, h'qâŸ© := hg
    refine âŸ¨u âˆ© v, Filter.inter_mem hu hv, _,
      (hp.mono inter_subset_left).prodMk (hq.mono inter_subset_right), fun i â†¦ ?_âŸ©
    change AnalyticOn ğ•œ (fun x â†¦ ContinuousMultilinearMap.prodL _ _ _ _ (p x i, q x i)) (u âˆ© v)
    apply (LinearIsometryEquiv.analyticOnNhd _ _).comp_analyticOn _ (Set.mapsTo_univ _ _)
    exact ((h'p i).mono inter_subset_left).prod ((h'q i).mono inter_subset_right)
  | (n : â„•âˆ) =>
    intro m hm
    rcases hf m hm with âŸ¨u, hu, p, hpâŸ©
    rcases hg m hm with âŸ¨v, hv, q, hqâŸ©
    exact âŸ¨u âˆ© v, Filter.inter_mem hu hv, _,
      (hp.mono inter_subset_left).prodMk (hq.mono inter_subset_right)âŸ©

/-- The Cartesian product of `C^n` functions on domains is `C^n`. -/
@[fun_prop]
theorem ContDiffOn.prodMk {s : Set E} {f : E â†’ F} {g : E â†’ G} (hf : ContDiffOn ğ•œ n f s)
    (hg : ContDiffOn ğ•œ n g s) : ContDiffOn ğ•œ n (fun x : E => (f x, g x)) s := fun x hx =>
  (hf x hx).prodMk (hg x hx)

/-- The Cartesian product of `C^n` functions at a point is `C^n`. -/
@[fun_prop]
theorem ContDiffAt.prodMk {f : E â†’ F} {g : E â†’ G} (hf : ContDiffAt ğ•œ n f x)
    (hg : ContDiffAt ğ•œ n g x) : ContDiffAt ğ•œ n (fun x : E => (f x, g x)) x :=
  contDiffWithinAt_univ.1 <| hf.contDiffWithinAt.prodMk hg.contDiffWithinAt

/-- The Cartesian product of `C^n` functions is `C^n`. -/
@[fun_prop]
theorem ContDiff.prodMk {f : E â†’ F} {g : E â†’ G} (hf : ContDiff ğ•œ n f) (hg : ContDiff ğ•œ n g) :
    ContDiff ğ•œ n fun x : E => (f x, g x) :=
  contDiffOn_univ.1 <| hf.contDiffOn.prodMk hg.contDiffOn

theorem iteratedFDerivWithin_prodMk {f : E â†’ F} {g : E â†’ G} (hf : ContDiffWithinAt ğ•œ n f s x)
    (hg : ContDiffWithinAt ğ•œ n g s x) (hs : UniqueDiffOn ğ•œ s) (ha : x âˆˆ s) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (fun x â†¦ (f x, g x)) s x =
      (iteratedFDerivWithin ğ•œ i f s x).prod (iteratedFDerivWithin ğ•œ i g s x) := by
  ext <;>
  Â· rw [â† ContinuousLinearMap.iteratedFDerivWithin_comp_left _ (hf.prodMk hg) hs ha hi]
    simp [Function.comp_def]

theorem iteratedFDeriv_prodMk {f : E â†’ F} {g : E â†’ G} (hf : ContDiffAt ğ•œ n f x)
    (hg : ContDiffAt ğ•œ n g x) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDeriv ğ•œ i (fun x â†¦ (f x, g x)) x =
      (iteratedFDeriv ğ•œ i f x).prod (iteratedFDeriv ğ•œ i g x) := by
  simp only [â† iteratedFDerivWithin_univ]
  exact iteratedFDerivWithin_prodMk hf.contDiffWithinAt hg.contDiffWithinAt uniqueDiffOn_univ
    (Set.mem_univ _) hi

end prod

/-! ### Being `C^k` on a union of open sets can be tested on each set -/
section contDiffOn_union

/-- If a function is `C^k` on two open sets, it is also `C^n` on their union. -/
lemma ContDiffOn.union_of_isOpen (hf : ContDiffOn ğ•œ n f s) (hf' : ContDiffOn ğ•œ n f t)
    (hs : IsOpen s) (ht : IsOpen t) :
    ContDiffOn ğ•œ n f (s âˆª t) := by
  rintro x (hx | hx)
  Â· exact (hf x hx).contDiffAt (hs.mem_nhds hx) |>.contDiffWithinAt
  Â· exact (hf' x hx).contDiffAt (ht.mem_nhds hx) |>.contDiffWithinAt

/-- A function is `C^k` on two open sets iff it is `C^k` on their union. -/
lemma contDiffOn_union_iff_of_isOpen (hs : IsOpen s) (ht : IsOpen t) :
    ContDiffOn ğ•œ n f (s âˆª t) â†” ContDiffOn ğ•œ n f s âˆ§ ContDiffOn ğ•œ n f t :=
  âŸ¨fun h â†¦ âŸ¨h.mono subset_union_left, h.mono subset_union_rightâŸ©,
   fun âŸ¨hfs, hftâŸ© â†¦ ContDiffOn.union_of_isOpen hfs hft hs htâŸ©

lemma contDiff_of_contDiffOn_union_of_isOpen (hf : ContDiffOn ğ•œ n f s)
    (hf' : ContDiffOn ğ•œ n f t) (hst : s âˆª t = univ) (hs : IsOpen s) (ht : IsOpen t) :
    ContDiff ğ•œ n f := by
  rw [â† contDiffOn_univ, â† hst]
  exact hf.union_of_isOpen hf' hs ht

/-- If a function is `C^k` on open sets `s i`, it is `C^k` on their union -/
lemma ContDiffOn.iUnion_of_isOpen {Î¹ : Type*} {s : Î¹ â†’ Set E}
    (hf : âˆ€ i : Î¹, ContDiffOn ğ•œ n f (s i)) (hs : âˆ€ i, IsOpen (s i)) :
    ContDiffOn ğ•œ n f (â‹ƒ i, s i) := by
  rintro x âŸ¨si, âŸ¨i, rflâŸ©, hxsiâŸ©
  exact (hf i).contDiffAt ((hs i).mem_nhds hxsi) |>.contDiffWithinAt

/-- A function is `C^k` on a union of open sets `s i` iff it is `C^k` on each `s i`. -/
lemma contDiffOn_iUnion_iff_of_isOpen {Î¹ : Type*} {s : Î¹ â†’ Set E}
    (hs : âˆ€ i, IsOpen (s i)) :
    ContDiffOn ğ•œ n f (â‹ƒ i, s i) â†” âˆ€ i : Î¹, ContDiffOn ğ•œ n f (s i) :=
  âŸ¨fun h i â†¦ h.mono <| subset_iUnion_of_subset i fun _ a â†¦ a,
   fun h â†¦ ContDiffOn.iUnion_of_isOpen h hsâŸ©

lemma contDiff_of_contDiffOn_iUnion_of_isOpen {Î¹ : Type*} {s : Î¹ â†’ Set E}
    (hf : âˆ€ i : Î¹, ContDiffOn ğ•œ n f (s i)) (hs : âˆ€ i, IsOpen (s i)) (hs' : â‹ƒ i, s i = univ) :
    ContDiff ğ•œ n f := by
  rw [â† contDiffOn_univ, â† hs']
  exact ContDiffOn.iUnion_of_isOpen hf hs

end contDiffOn_union

/-- The natural equivalence `(E Ã— F) Ã— G â‰ƒ E Ã— (F Ã— G)` is smooth.

Warning: if you think you need this lemma, it is likely that you can simplify your proof by
reformulating the lemma that you're applying next using the tips in
Note [continuity lemma statement]
-/
theorem contDiff_prodAssoc {n : WithTop â„•âˆ} : ContDiff ğ•œ n <| Equiv.prodAssoc E F G :=
  (LinearIsometryEquiv.prodAssoc ğ•œ E F G).contDiff

/-- The natural equivalence `E Ã— (F Ã— G) â‰ƒ (E Ã— F) Ã— G` is smooth.

Warning: see remarks attached to `contDiff_prodAssoc`
-/
theorem contDiff_prodAssoc_symm {n : WithTop â„•âˆ} : ContDiff ğ•œ n <| (Equiv.prodAssoc E F G).symm :=
  (LinearIsometryEquiv.prodAssoc ğ•œ E F G).symm.contDiff
