/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
module

public import Mathlib.Analysis.Calculus.ContDiff.Comp

/-!
# Higher differentiability in one dimension

The general theory of higher derivatives in Mathlib is developed using the FrÃ©chet derivative
`fderiv`; but for maps defined on the field, the one-dimensional derivative `deriv` is often easier
to use. In this file, we reformulate some higher smoothness results in terms of `deriv`.

## Tags

derivative, differentiability, higher derivative, `C^n`, multilinear, Taylor series, formal series
-/

public noncomputable section

open scoped ContDiff

open Set

variable {ð•œ F : Type*} [NontriviallyNormedField ð•œ] [NormedAddCommGroup F] [NormedSpace ð•œ F]
  {m n : WithTop â„•âˆž} {f : ð•œ â†’ F} {s : Set ð•œ}

/-- A function is `C^(n + 1)` on a domain with unique derivatives if and only if it is
differentiable there, and its derivative (formulated with `derivWithin`) is `C^n`. -/
theorem contDiffOn_succ_iff_derivWithin (hs : UniqueDiffOn ð•œ s) :
    ContDiffOn ð•œ (n + 1) f s â†”
      DifferentiableOn ð•œ f s âˆ§ (n = Ï‰ â†’ AnalyticOn ð•œ f s) âˆ§ ContDiffOn ð•œ n (derivWithin f s) s := by
  have : derivWithin f s =
      ContinuousLinearMap.toSpanSingletonCLE.symm âˆ˜ fderivWithin ð•œ f s := by
    ext; simp [â† fderivWithin_derivWithin]
  simp [contDiffOn_succ_iff_fderivWithin hs, this, ContinuousLinearEquiv.comp_contDiffOn_iff]

theorem contDiffOn_infty_iff_derivWithin (hs : UniqueDiffOn ð•œ s) :
    ContDiffOn ð•œ âˆž f s â†” DifferentiableOn ð•œ f s âˆ§ ContDiffOn ð•œ âˆž (derivWithin f s) s := by
  rw [show âˆž = âˆž + 1 by rfl, contDiffOn_succ_iff_derivWithin hs]
  simp

/-- A function is `C^(n + 1)` on an open domain if and only if it is
differentiable there, and its derivative (formulated with `deriv`) is `C^n`. -/
theorem contDiffOn_succ_iff_deriv_of_isOpen (hs : IsOpen s) :
    ContDiffOn ð•œ (n + 1) f s â†”
      DifferentiableOn ð•œ f s âˆ§ (n = Ï‰ â†’ AnalyticOn ð•œ f s) âˆ§ ContDiffOn ð•œ n (deriv f) s := by
  rw [contDiffOn_succ_iff_derivWithin hs.uniqueDiffOn]
  exact Iff.rfl.and (Iff.rfl.and (contDiffOn_congr fun _ => derivWithin_of_isOpen hs))

theorem contDiffOn_infty_iff_deriv_of_isOpen (hs : IsOpen s) :
    ContDiffOn ð•œ âˆž f s â†” DifferentiableOn ð•œ f s âˆ§ ContDiffOn ð•œ âˆž (deriv f) s := by
  rw [show âˆž = âˆž + 1 by rfl, contDiffOn_succ_iff_deriv_of_isOpen hs]
  simp

protected theorem ContDiffOn.derivWithin (hf : ContDiffOn ð•œ n f s) (hs : UniqueDiffOn ð•œ s)
    (hmn : m + 1 â‰¤ n) : ContDiffOn ð•œ m (derivWithin f s) s :=
  ((contDiffOn_succ_iff_derivWithin hs).1 (hf.of_le hmn)).2.2

theorem ContDiffOn.deriv_of_isOpen (hf : ContDiffOn ð•œ n f s) (hs : IsOpen s) (hmn : m + 1 â‰¤ n) :
    ContDiffOn ð•œ m (deriv f) s :=
  (hf.derivWithin hs.uniqueDiffOn hmn).congr fun _ hx => (derivWithin_of_isOpen hs hx).symm

theorem ContDiffOn.continuousOn_derivWithin (h : ContDiffOn ð•œ n f s) (hs : UniqueDiffOn ð•œ s)
    (hn : 1 â‰¤ n) : ContinuousOn (derivWithin f s) s := by
  rw [show (1 : WithTop â„•âˆž) = 0 + 1 from rfl] at hn
  exact ((contDiffOn_succ_iff_derivWithin hs).1 (h.of_le hn)).2.2.continuousOn

theorem ContDiffOn.continuousOn_deriv_of_isOpen (h : ContDiffOn ð•œ n f s) (hs : IsOpen s)
    (hn : 1 â‰¤ n) : ContinuousOn (deriv f) s := by
  rw [show (1 : WithTop â„•âˆž) = 0 + 1 from rfl] at hn
  exact ((contDiffOn_succ_iff_deriv_of_isOpen hs).1 (h.of_le hn)).2.2.continuousOn

@[fun_prop]
protected lemma ContDiffWithinAt.derivWithin {x : ð•œ}
    (H : ContDiffWithinAt ð•œ n f s x) (hs : UniqueDiffOn ð•œ s)
    (hmn : m + 1 â‰¤ n) (hx : x âˆˆ s) :
    ContDiffWithinAt ð•œ m (derivWithin f s) s x := by
  exact ContDiffWithinAt.comp _ (by fun_prop) (g := fun f â†¦ f 1) (t := .univ)
    (H.fderivWithin_right hs hmn hx) (fun _ _ â†¦ trivial)

/-- A function is `C^(n + 1)` if and only if it is differentiable,
  and its derivative (formulated in terms of `deriv`) is `C^n`. -/
theorem contDiff_succ_iff_deriv :
    ContDiff ð•œ (n + 1) f â†” Differentiable ð•œ f âˆ§ (n = Ï‰ â†’ AnalyticOn ð•œ f univ) âˆ§
      ContDiff ð•œ n (deriv f) := by
  simp only [â† contDiffOn_univ, contDiffOn_succ_iff_deriv_of_isOpen, isOpen_univ,
    differentiableOn_univ]

theorem contDiff_one_iff_deriv :
    ContDiff ð•œ 1 f â†” Differentiable ð•œ f âˆ§ Continuous (deriv f) := by
  rw [show (1 : WithTop â„•âˆž) = 0 + 1 from rfl, contDiff_succ_iff_deriv]
  simp

theorem contDiff_infty_iff_deriv :
    ContDiff ð•œ âˆž f â†” Differentiable ð•œ f âˆ§ ContDiff ð•œ âˆž (deriv f) := by
  rw [show (âˆž : WithTop â„•âˆž) = âˆž + 1 from rfl, contDiff_succ_iff_deriv]
  simp

theorem ContDiff.continuous_deriv (h : ContDiff ð•œ n f) (hn : 1 â‰¤ n) : Continuous (deriv f) := by
  rw [show (1 : WithTop â„•âˆž) = 0 + 1 from rfl] at hn
  exact (contDiff_succ_iff_deriv.mp (h.of_le hn)).2.2.continuous

@[fun_prop]
theorem ContDiff.continuous_deriv_one (h : ContDiff ð•œ 1 f) : Continuous (deriv f) :=
  ContDiff.continuous_deriv h (le_refl 1)

@[fun_prop]
theorem ContDiff.differentiable_deriv_two (h : ContDiff ð•œ 2 f) : Differentiable ð•œ (deriv f) := by
  unfold deriv; fun_prop

@[fun_prop]
protected lemma ContDiffAt.derivWithin {x : ð•œ} (H : ContDiffAt ð•œ n f x) (hmn : m + 1 â‰¤ n) :
    ContDiffAt ð•œ m (deriv f) x := by
  simpa using ContDiffWithinAt.derivWithin (s := .univ) H.contDiffWithinAt (by simp) hmn

@[fun_prop]
theorem ContDiff.deriv' (h : ContDiff ð•œ (n + 1) f) : ContDiff ð•œ n (deriv f) := by
  unfold deriv; fun_prop

@[fun_prop]
theorem ContDiff.iterate_deriv :
    âˆ€ (n : â„•) {f : ð•œ â†’ F}, ContDiff ð•œ âˆž f â†’ ContDiff ð•œ âˆž (deriv^[n] f)
  | 0, _, hf => hf
  | n + 1, _, hf => ContDiff.iterate_deriv n (contDiff_infty_iff_deriv.mp hf).2

@[fun_prop]
theorem ContDiff.iterate_deriv' (n : â„•) :
    âˆ€ (k : â„•) {f : ð•œ â†’ F}, ContDiff ð•œ (n + k : â„•) f â†’ ContDiff ð•œ n (deriv^[k] f)
  | 0, _, hf => hf
  | k + 1, _, hf => ContDiff.iterate_deriv' _ k (contDiff_succ_iff_deriv.mp hf).2.2

end
